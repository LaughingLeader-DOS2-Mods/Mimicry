INIT
	FIXEDSTRING:%EmptyFixedStr = ""
	FLOAT3:%EmptyPos = null
	CHARACTER:%NullChar = null
	EXTERN STATUS:%Passive_IsMime = LLMIME_MIME
	EXTERN STATUS:%Passive_Brawler = LLMIME_BRAWLER
	EXTERN STATUS:%Passive_Beastmaster = LLMIME_BEASTMASTER
	EXTERN STATUS:%Passive_Concentration = LLMIME_CONCENTRATION
	FLOAT:%StatusStrength = 50
EVENTS

EVENT Mime_Setup
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "Mimicry_InitMimeJob")
ACTIONS
	SetTag(_Mime, "LLMIME_MIME")
IF "!c1"
	HasStatusInfluence(_Mime, %Passive_IsMime)
THEN
	AddStatusInfluence(_Mime, %Passive_IsMime, %StatusStrength, "", 0, 1)
ENDIF
IF "!c1"
	HasStatusInfluence(_Mime, %Passive_Brawler)
THEN
	AddStatusInfluence(_Mime, %Passive_Brawler, %StatusStrength, "", 0, 1)
ENDIF
IF "!c1"
	HasStatusInfluence(_Mime, %Passive_Beastmaster)
THEN
	AddStatusInfluence(_Mime, %Passive_Beastmaster, %StatusStrength, "", 0, 1)
ENDIF
IF "!c1"
	HasStatusInfluence(_Mime, %Passive_Concentration)
THEN
	AddStatusInfluence(_Mime, %Passive_Concentration, %StatusStrength, "", 0, 1)
ENDIF

EVENT Mime_Remove
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "Mimicry_RemoveMimeJob")
ACTIONS
	ClearTag(_Mime, "LLMIME_MIME")
IF "c1"
	HasStatusInfluence(_Mime, %Passive_IsMime)
THEN
	RemoveStatusInfluence(_Mime, %Passive_IsMime, %StatusStrength, "", 0)
ENDIF
IF "c1"
	HasStatusInfluence(_Mime, %Passive_Brawler)
THEN
	RemoveStatusInfluence(_Mime, %Passive_Brawler, %StatusStrength, "", 0)
ENDIF
IF "c1"
	HasStatusInfluence(_Mime, %Passive_Beastmaster)
THEN
	RemoveStatusInfluence(_Mime, %Passive_Beastmaster, %StatusStrength, "", 0)
ENDIF
IF "c1"
	HasStatusInfluence(_Mime, %Passive_Concentration)
THEN
	RemoveStatusInfluence(_Mime, %Passive_Concentration, %StatusStrength, "", 0)
ENDIF

EVENT Apply_Mimicking_Start
ON
	OnGlobalFlagSet("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
	IterateCharacters("LLMIME_Events_RequestMimicking", null, null, "LLMIME_MIME")
	//CharacterApplyStatus(LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, LLMIME_MIMICKING, -1)


EVENT Apply_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_RequestMimicking")
	OnIterateCharacter(_Mime, "LLMIME_Events_RequestMimicking")
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Mime, LLMIME_MIME)
	CharacterHasStatus(_Mime, LLMIME_MIMICKING)
THEN
	CharacterApplyStatus(_Mime, LLMIME_MIMICKING, -1)
ENDIF

EVENT Apply_Mimicking_CombatEnded
VARS
	CHARACTER:_Mime
ON
	OnLeftCombat(_Mime, _)
ACTIONS
IF "c1&c2&!c3"
	HasGlobalFlag("Mimicry_Mimic_CombatOnlyDisabled")
	CharacterHasStatus(_Mime, LLMIME_MIME)
	CharacterHasStatus(_Mime, LLMIME_MIMICKING)
THEN
	CharacterApplyStatus(_Mime, LLMIME_MIMICKING, -1)
ENDIF

EVENT Remove_Mimicking_Start
ON
	OnGlobalFlagCleared("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
	IterateCharacters("LLMIME_Events_RemoveMimicking", null, null, "LLMIME_MIME")

EVENT Remove_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_RemoveMimicking")
	OnIterateCharacter(_Mime, "LLMIME_Events_RemoveMimicking")
ACTIONS
IF "!c1|c2"
	IsInCombat(_Mime)
	IsInActiveTurn(_Mime)
THEN
	CharacterRemoveStatus(_Mime, LLMIME_MIMICKING)
ENDIF

EVENT Dummy_Iterate_NearbyItems
VARS
	CHARACTER:_Dummy
	FIXEDSTRING:_EventName
	FLOAT:_Radius
ON
	OnCharacterEvent(_Dummy, "LLMIME_Events_StartDummyItemIterator")
ACTIONS
IF "c1&c2"
	GetVar(_Radius, _Dummy, "LLMIME_Dummy_IterationRange")
	GetVar(_EventName, _Dummy, "LLMIME_Dummy_IterationEvent")
	//IsEqual(_EventName, %EmptyFixedStr)
THEN
	IterateItemsNear(_Dummy, _Radius, _EventName)
	SetVar(_Dummy, "LLMIME_Dummy_IterationEvent", %EmptyFixedStr)
ENDIF

EVENT Dummy_OnIterate
VARS
	ITEM:_Item
	FIXEDSTRING:_EventName
	STRING:_EventNameStr
ON
	OnIterateItem(_Item, _EventName)
ACTIONS
	Cast(_EventNameStr, _EventName)
	ItemEvent(_Item, _EventNameStr)