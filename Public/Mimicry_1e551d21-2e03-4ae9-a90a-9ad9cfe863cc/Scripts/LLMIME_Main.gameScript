INIT
	FIXEDSTRING:%EmptyFixedStr = ""
	FLOAT3:%EmptyPos = null
	CHARACTER:%NullChar = null
	EXTERN STATUS:%Passive_IsMime = LLMIME_MIME
	EXTERN STATUS:%Passive_Brawler = LLMIME_BRAWLER
	EXTERN STATUS:%Passive_Beastmaster = LLMIME_BEASTMASTER
	EXTERN STATUS:%Passive_Concentration = LLMIME_CONCENTRATION
	LIST<CHARACTER>:%Mimes
EVENTS

EVENT Mime_Setup
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_InitMime")
ACTIONS
	AddStatusInfluence(_Mime, %Passive_IsMime, 99, "", 0, 1)
	AddStatusInfluence(_Mime, %Passive_Brawler, 99, "", 0, 1)
	AddStatusInfluence(_Mime, %Passive_Beastmaster, 99, "", 0, 1)
	AddStatusInfluence(_Mime, %Passive_Concentration, 99, "", 0, 1)
	SetTag(_Mime, "LLMIME_MIME")
	ListAdd(%Mimes, _Mime)
EVENT Mime_Remove
VARS
	CHARACTER:_Mime
	CHARACTER:_GetMime
	INT:_Size
	INT:_Index
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_RemoveMime")
ACTIONS
	RemoveStatusInfluence(_Mime, %Passive_IsMime, 9999999)
	RemoveStatusInfluence(_Mime, %Passive_Brawler, 9999999)
	RemoveStatusInfluence(_Mime, %Passive_Beastmaster, 9999999)
	RemoveStatusInfluence(_Mime, %Passive_Concentration, 9999999)
	ClearTag(_Mime, "LLMIME_MIME")
	IF "c1"
		ListGetSize(%Mimes,_Size)
	THEN
		Set(_Index,1)
		WHILE "!c1"
			IsGreaterThen(_Index, _Size)
		DO
			IF "c1&c2"
				ListGet(%Mimes, _Index, _GetMime)
				IsEqual(_GetMime, _Mime)
			THEN
				ListRemove(%Mimes, _Index)
				Goto("BreakWhile")
			ELSE
				Add(_Index, 1)
			ENDIF
		ENDWHILE
		Label("BreakWhile")
	ENDIF

EVENT Func_RemoveSummonedMimes
VARS
	CHARACTER:_Mime
	INT:_Size
	INT:_Index
ON
	OnFunction("LLMIME_Func_CleanupMimeList")
ACTIONS
IF "c1"
	ListGetSize(%Mimes,_Size)
THEN
	Set(_Index,1)
	WHILE "!c1"
		IsGreaterThen(_Index, _Size)
	DO
		IF "c1&c2&c3"
			ListGet(%Mimes, _Index, _Mime)
			CharacterIsSummon(_Mime)
			CharacterIsDead(_Mime)
		THEN
			ListRemove(%Mimes, _Index)
		ENDIF
	Add(_Index, 1)
	ENDWHILE
ENDIF

EVENT StartListCleanup
VARS
	CHARACTER:_Mime
ON
	OnDie(_Mime, _, _, _)
ACTIONS
IF "c1&c2"
	CharacterIsSummon(_Mime)
	IsTagged(_Mime, "LLMIME_MIME")
THEN
	CallFunction("LLMIME_Func_CleanupMimeList")
ENDIF

EVENT Apply_Mimicking_Start
VARS
	CHARACTER:_Mime
	INT:_Size
	INT:_Index
ON
	OnGlobalFlagSet("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
IF "c1"
	ListGetSize(%Mimes,_Size)
THEN
	Set(_Index,1)
	WHILE "!c1"
		IsGreaterThen(_Index, _Size)
	DO
		IF "c1"
			ListGet(%Mimes, _Index, _Mime)
		THEN
			CharacterEvent(_Mime, "LLMIME_Events_RequestMimicking")
		ENDIF
	Add(_Index, 1)
	ENDWHILE
ENDIF
	
EVENT Apply_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_RequestMimicking")
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Mime, LLMIME_MIME)
	CharacterHasStatus(_Mime, LLMIME_MIMICKING)
THEN
	CharacterApplyStatus(_Mime, LLMIME_MIMICKING, -1)
ENDIF

EVENT Remove_Mimicking_Start
VARS
	CHARACTER:_Mime
	INT:_Size
	INT:_Index
ON
	OnGlobalFlagCleared("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
IF "c1"
	ListGetSize(%Mimes,_Size)
THEN
	Set(_Index,1)
	WHILE "!c1"
		IsGreaterThen(_Index, _Size)
	DO
		IF "c1"
			ListGet(%Mimes, _Index, _Mime)
		THEN
			CharacterEvent(_Mime, "LLMIME_Events_RemoveMimicking")
		ENDIF
	Add(_Index, 1)
	ENDWHILE
ENDIF

EVENT Remove_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "LLMIME_Events_RemoveMimicking")
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Mime, LLMIME_MIMICKING)
	IsInCombat(_Mime)
THEN
	CharacterRemoveStatus(_Mime, LLMIME_MIMICKING)
ENDIF

EVENT Debug_Item
VARS
	CHARACTER:_Char
	ITEM:_Item
ON
	OnUseItem(_Char, _Item)
ACTIONS
	CharacterItemEvent(_Char, _Item, "LLMIME_Events_OnItemUse")

EVENT Dummy_Iterate_NearbyItems
VARS
	CHARACTER:_Dummy
	FIXEDSTRING:_EventName
	FLOAT:_Radius
ON
	OnCharacterEvent(_Dummy, "LLMIME_Events_StartDummyItemIterator")
ACTIONS
IF "c1&c2"
	GetVar(_Radius, _Dummy, "LLMIME_Dummy_IterationRange")
	GetVar(_EventName, _Dummy, "LLMIME_Dummy_IterationEvent")
	//IsEqual(_EventName, %EmptyFixedStr)
THEN
	IterateItemsNear(_Dummy, _Radius, _EventName)
	SetVar(_Dummy, "LLMIME_Dummy_IterationEvent", %EmptyFixedStr)
ENDIF

EVENT Dummy_OnIterate
VARS
	ITEM:_Item
	FIXEDSTRING:_EventName
	STRING:_EventNameStr
ON
	OnIterateItem(_Item, _EventName)
ACTIONS
	Cast(_EventNameStr, _EventName)
	ItemEvent(_Item, _EventNameStr)