INIT
	FIXEDSTRING:%EmptyFixedStr = ""
	FLOAT3:%EmptyPos = null
	CHARACTER:%NullChar = null
	LIST<STATUS>:%MimeStatuses
	FLOAT:%StatusStrength = 50
	EXTERN FLOAT:%SourceRegenMinimumLevel = 10
	EXTERN INT:%SourceRegenDuration = 10
EVENTS

EVENT InitStatusList
VARS
	INT:_ListSize
ON
	OnActivate()
	OnFunction("Mimicry_InitStatusList")
ACTIONS
IF "!c1|(c1&c2)"
	ListGetSize(%MimeStatuses, _ListSize)
	IsLessThen(_ListSize, 1)
THEN
	ListClear(%MimeStatuses)
	ListAdd(%MimeStatuses, LLMIME_MIME)
	ListAdd(%MimeStatuses, LLMIME_BEASTMASTER)
	ListAdd(%MimeStatuses, LLMIME_BRAWLER)
	ListAdd(%MimeStatuses, LLMIME_CONCENTRATION)
ENDIF

EVENT LeaderLib_InitEvent
ON
	OnItemEvent(_, "LeaderLib_Initialized")
ACTIONS
	CallFunction("Mimicry_InitStatusList")
	IterateCharacters("LLMIME_Iterator_CheckUnarmed", null, null, "Mimicry_Mime")

EVENT Mime_InitIterate
VARS
	CHARACTER:_Mime
ON
	OnIterateCharacter(_Mime, "LLMIME_Iterator_CheckUnarmed")
ACTIONS
	CharacterEvent(_Mime, "Mimicry_MimeInitialized")
	
EVENT CheckMaskWearers
ON
	OnItemEvent(_, "LeaderLib_Events_OnDefaultEventFlowComplete")
ACTIONS
	IterateCharacters("LLMIME_Iterator_InitMime", null, null, "Mimicry_WearingMask")

EVENT Mime_MaskWearers_InitMimeJob
VARS
	CHARACTER:_Mime
ON
	OnIterateCharacter(_Mime, "LLMIME_Iterator_InitMime")
ACTIONS
	CharacterEvent(_Mime, "Mimicry_InitMimeJob")

EVENT Mime_Setup
VARS
	CHARACTER:_Mime
	INT:_Size
	INT:_Index
	STATUS:_Status
	STRING:_Message
	INT:_Turns
ON
	OnCharacterEvent(_Mime, "Mimicry_InitMimeJob")
ACTIONS
	CallFunction("Mimicry_InitStatusList")
IF "!c1"
	IsTagged(_Mime, "Mimicry_Mime")
THEN
	SetVar(_Mime, "LeaderLib_LogMessage", STRING:"[LLMIME:LLMIME_Main:Mime_Setup] Setting mime tag and statuses.")
	CharacterEvent(_Mime, "LeaderLib_DebugLog")
	SetTag(_Mime, "Mimicry_Mime")
ENDIF
	CharacterEvent(_Mime, "Mimicry_ApplySourceRegen")
	Set(_Index, 1)
IF "c1"
	ListGetSize(%MimeStatuses, _Size)
THEN
	WHILE "!c1"
		IsGreaterThen(_Index,_Size)
	DO
		IF "c1"
			ListGet(%MimeStatuses,_Index,_Status)
		THEN
			/*
			IF "!c1"
				HasStatusInfluence(_Mime, _Status)
			THEN
				AddStatusInfluence(_Mime, _Status, %StatusStrength, "", 0, 1)
			ENDIF
			*/
			IF "!c1|(c2&!c3)"
				CharacterHasStatus(_Mime, _Status)
				GetStatusTurns(_Mime, _Status, _Turns)
				IsEqual(_Turns, -1)
			THEN
				CharacterApplyStatus(_Mime, _Status, -1, 1)
			ENDIF
		ENDIF
	Add(_Index, 1)
	ENDWHILE
ENDIF
	CharacterEvent(_Mime, "Mimicry_MimeInitialized")
	
	/*
	Print(_Message, "[LLMIME:LLMIME_Main:Mime_Setup] Status list: Size([1]).", _Size)
	SetVar(_Mime, "LeaderLib_LogMessage", _Message)
	CharacterEvent(_Mime, "LeaderLib_DebugLog")
	*/
EVENT Mime_Remove
VARS
	CHARACTER:_Mime
ON
	OnCharacterEvent(_Mime, "Mimicry_RemoveMimeJob")
ACTIONS
IF "c1"
	IsTagged(_Mime, "Mimicry_Mime")
THEN
	SetVar(_Mime, "LeaderLib_LogMessage", STRING:"[LLMIME:LLMIME_Main:Mime_Remove] Remove mime tag and clearing status influences.")
	CharacterEvent(_Mime, "LeaderLib_DebugLog")
	ClearTag(_Mime, "Mimicry_Mime")
ENDIF
	CharacterEvent(_Mime, "Mimicry_ClearMimeBonuses")
	CharacterEvent(_Mime, "Mimicry_MimeJobRemoved")
	CharacterRemoveStatus(_Mime, LLMIME_SOURCE_REGENERATION_HIDDEN, null, 0)
	
EVENT Apply_Mimicking_Start
ON
	OnGlobalFlagSet("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
	IterateCharacters("LLMIME_Iterator_RequestMimicking", null, null, "Mimicry_Mime")
	//CharacterApplyStatus(LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, LLMIME_MIMICKING, -1)


EVENT Apply_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnIterateCharacter(_Mime, "LLMIME_Iterator_RequestMimicking")
ACTIONS
	CharacterEvent(_Mime, "LLMIME_Events_RequestMimicking")
	
EVENT Apply_Mimicking_CombatEnded
VARS
	CHARACTER:_Mime
ON
	OnLeftCombat(_Mime, _)
ACTIONS
IF "c1"
	HasGlobalFlag("Mimicry_Mimic_CombatOnlyDisabled")
THEN
	CharacterEvent(_Mime, "LLMIME_Events_RequestMimicking")
ENDIF

EVENT Remove_Mimicking_Start
ON
	OnGlobalFlagCleared("Mimicry_Mimic_CombatOnlyDisabled")
ACTIONS
	IterateCharacters("LLMIME_Iterator_RemoveMimicking", null, null, "Mimicry_Mime")

EVENT Remove_Mimicking
VARS
	CHARACTER:_Mime
ON
	OnIterateCharacter(_Mime, "LLMIME_Iterator_RemoveMimicking")
ACTIONS
IF "!c1|c2|c3"
	IsInCombat(_Mime)
	IsInActiveTurn(_Mime)
	CharacterHasStatus(_Mime, LLMIME_MIMICKING_DISABLED)
THEN
	CharacterEvent(_Mime, "LLMIME_Events_RemoveMimicking")
ENDIF

EVENT Dummy_Iterate_NearbyItems
VARS
	CHARACTER:_Dummy
	FIXEDSTRING:_EventName
	FLOAT:_Radius
ON
	OnCharacterEvent(_Dummy, "LLMIME_Events_StartDummyItemIterator")
ACTIONS
IF "c1&c2"
	GetVar(_Radius, _Dummy, "LLMIME_Dummy_IterationRange")
	GetVar(_EventName, _Dummy, "LLMIME_Dummy_IterationEvent")
	//IsEqual(_EventName, %EmptyFixedStr)
THEN
	IterateItemsNear(_Dummy, _Radius, _EventName)
	SetVar(_Dummy, "LLMIME_Dummy_IterationEvent", %EmptyFixedStr)
ENDIF

EVENT Dummy_OnIterate
VARS
	ITEM:_Item
	FIXEDSTRING:_EventName
	STRING:_EventNameStr
ON
	OnIterateItem(_Item, _EventName)
ACTIONS
	Cast(_EventNameStr, _EventName)
	ItemEvent(_Item, _EventNameStr)
	
EVENT Mime_ApplySourceRegen
VARS
	CHARACTER:_Mime
	FLOAT:_Level
ON
	OnCharacterStatusRemoved(_Mime, LLMIME_SOURCE_REGENERATION_HIDDEN)
	OnCharacterEvent(_Mime, "Mimicry_MimeLeveledUp")
	OnCharacterEvent(_Mime, "Mimicry_ApplySourceRegen")
ACTIONS
IF "c1&!c2&c3&(c4|c5)"
	IsTagged(_Mime, "Mimicry_Mime")
	CharacterHasStatus(_Mime, LLMIME_SOURCE_REGENERATION_HIDDEN)
	CharacterGetStat(_Level, _Mime, Level)
	IsGreaterThen(_Level, %SourceRegenMinimumLevel)
	IsEqual(_Level, %SourceRegenMinimumLevel)
THEN
	CharacterApplyStatus(_Mime, LLMIME_SOURCE_REGENERATION_HIDDEN, %SourceRegenDuration, 0)
ENDIF

EVENT Mime_Combat_Muted_Apply
VARS
	CHARACTER:_Mime
ON
	OnEnteredCombat(_Mime, _)
ACTIONS
IF "c1"
	IsTagged(_Mime, "Mimicry_Mime")
THEN
	CharacterApplyStatus(_Mime, LLMIME_COMBAT_MUTED, -1, 1)
ENDIF
	
EVENT Mime_Combat_Muted_Remove
VARS
	CHARACTER:_Mime
ON
	OnLeftCombat(_Mime, _)
ACTIONS
IF "c1"
	IsTagged(_Mime, "Mimicry_Mime")
THEN
	//Make it "cool down" instead of removing it immediately, for fleeing situations
	CharacterApplyStatus(_Mime, LLMIME_COMBAT_MUTED, 1, 1)
ENDIF

EVENT Mime_BasicAttack_Event
VARS
	CHARACTER:_Mime
	CHARACTER:_DefenderChar
	ITEM:_DefenderItem
ON
	OnCharacterStartAttackObject(_DefenderChar, _DefenderItem, _, _Mime)
ACTIONS
IF "c1&c2"
	IsTagged(_Mime, "Mimicry_Mime")
	HasFlag(_Mime, "Mimicry_MimeIsUnarmed")
THEN
	IF "!c1"
		IsEqual(_DefenderChar, null)
	THEN
		CharacterCharacterEvent(_Mime, _DefenderChar, "Mimicry_Events_OnMimeUnarmedBasicAttack")
	ENDIF
	IF "!c1"
		IsEqual(_DefenderItem, null)
	THEN
		CharacterItemEvent(_Mime, _DefenderItem, "Mimicry_Events_OnMimeUnarmedBasicAttack")
	ENDIF
ENDIF