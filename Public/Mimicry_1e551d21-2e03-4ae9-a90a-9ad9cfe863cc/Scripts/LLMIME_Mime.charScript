INIT
	CHARACTER:__Me
	INT:%LLMIME_Initialized = 0
	EXTERN FLOAT:%BrawlerStanceInfluenceStrength = 50.0
	INT:%LLMIME_InitialRefresh = 0
	FIXEDSTRING:%RefreshTimerName = ""
EVENTS

/*
EVENT Init_Vars
ON
	OnItemEvent(_, "LeaderLib_Initialized")
ACTIONS
	Set(%LLMIME_InitialRefresh, 0)
*/

/*
EVENT Region_ReInit
ON
	OnRegionEntered(__Me, _, _)
ACTIONS
	CharacterEvent(__Me, "Mimicry_Follower_ReInitialize")
*/

EVENT LLMIME_MimeFollower_Init_Event
VARS
	INT:_Ran
	FLOAT:_RanFloat
	STRING:_TimerName
ON
	//OnCharacterEvent(__Me, "Mimicry_MimeInitialized")
	//OnActivate()
	OnItemEvent(_, "LeaderLib_Initialized")
	OnCharacterEvent(__Me, "Mimicry_Follower_ReInitialize")
	OnCharacterEvent(__Me, "Mimicry_MimeInitialized")
ACTIONS
IF "!c1"
	HasFlag(__Me, "Mimicry_MimeIsUnarmed")
THEN
	SetFlag(__Me, "Mimicry_MimeIsUnarmed")
ENDIF
IF "c1"
	IsEqual(%LLMIME_InitialRefresh, 0)
THEN
	CallFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
	CharacterEvent(__Me, "Mimicry_InitMimeJob")
	Set(%LLMIME_InitialRefresh, 1)
ENDIF
IF "c1"
	IsEqual(%LLMIME_Initialized, 0)
THEN
	CharacterEvent(__Me, "Mimicry_RegisterMimeFollower")
	IF "!c1"
		IsTagged(__Me, "LeaderLib_ReceiveExtraEvents")
	THEN
		SetTag(__Me, "LeaderLib_ReceiveExtraEvents")
	ENDIF
	//SetVar(__Me, "LeaderLib_LogMessage", STRING:"[LLMIME:LLMIME_Mime.charScript:Init_Register] Activated.")
	//CharacterEvent(__Me, "LeaderLib_DebugLog")
	Set(%LLMIME_Initialized, 1)
ENDIF

EVENT LLMIME_MimeFollower_BrawlerStance_Activate
ON
	OnFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
ACTIONS
IF "!c1"
	CharacterHasStatus(__Me, LLMIME_BRAWLER_STANCE_CON)
THEN
	CharacterApplyStatus(__Me, LLMIME_BRAWLER_STANCE_CON, -1, 1)
ENDIF

EVENT LLMIME_MimeFollower_Apply_BrawlerStance
ON
	OnCharacterEvent(__Me, "Mimicry_MimickedWeaponsRemoved")
	OnCharacterEvent(__Me, "Mimicry_Events_OnMimeFollowerRevived")
	OnCharacterStatusApplied(__Me, "LLMIME_MIME")
ACTIONS
	CallFunction("Mimicry_MimeFollower_ActivateBrawlerStance")

EVENT LLMIME_MimeFollower_Refresh_Timer
ON
	OnTimer(%RefreshTimerName)
ACTIONS
	CharacterEvent(__Me, "LeaderLib_Commands_RefreshEquipment_Delayed")

EVENT LLMIME_MimeFollower_Mimicking_Applied
ON
	OnCharacterStatusApplied(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", 999)
	SetPriority("Mimic_Attack_Item", 999)

EVENT LLMIME_MimeFollower_Mimicking_Removed
ON
	OnCharacterStatusRemoved(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", -1)
	SetPriority("Mimic_Attack_Item", -1)
	
EVENT LLMIME_MimeFollower_Owner_Entered_Combat
VARS
	CHARACTER:_Character
	CHARACTER:_Owner
	FLOAT:_Dist
ON
	OnEnteredCombat(_Character, _)
ACTIONS
IF "c1&c2&c3&c4"
	GetVar(_Owner, __Me, "LeaderLib_Follower_Owner")
	IsEqual(_Character, _Owner)
	GetDistance(_Dist, _Character, __Me)
	IsLessThen(_Dist, 50)
THEN
	EnterCombat(__Me, _Owner)
ENDIF

EVENT LLMIME_MimeFollower_Entered_Combat
VARS
	CHARACTER:_Owner
	FLOAT:_Dist
ON
	OnEnteredCombat(__Me, _)
ACTIONS
IF "c1&!c2&c3&c4"
	GetVar(_Owner, __Me, "LeaderLib_Follower_Owner")
	IsEqual(_Owner, null)
	GetDistance(_Dist, _Owner, __Me)
	IsLessThen(_Dist, 50)
THEN
	EnterCombat(_Owner, __Me)
ENDIF

EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_Start
ON
	OnCharacterEvent(__Me, "Mimicry_Events_OnMimeFollowerRevived")
ACTIONS
	IterateParty("LLMIME_Iterator_IterateInventoryForMimeFollowerTreasure")
	IterateItems("LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	IterateItemsInInventory(__Me, "LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	
EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_Character
VARS
	CHARACTER:_Character
ON
	OnIterateCharacter(_Character, "LLMIME_Iterator_IterateInventoryForMimeFollowerTreasure")
ACTIONS
	IterateItemsInInventory(_Character, "LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	
EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_OnIterateItem
VARS
	ITEM:_Item
ON
	OnIterateItem(_Item, "LLMIME_Iterator_RemoveGeneratedMimeTreasure")
ACTIONS
	ItemEvent(_Item, "LLMIME_Events_OnMimeFollowerTreasureRemoved")
	ItemEvent(_Item, "LeaderLib_Commands_RemoveItem")