INIT
	CHARACTER:__Me
	INT:%FirstEquip = 0
	INT:%Following = 0
	EXTERN FLOAT:%BrawlerStanceInfluenceStrength = 50.0
EVENTS

EVENT Create_Mask
VARS
	ITEM:_Mask
ON
	OnActivate()
ACTIONS
IF "!c1"
	ItemGetFromInventory(_Mask, __Me, null, "LLMIME_ACTIVATE_MIME", 0)
THEN
	CharacterAddToInventory(__Me, "ARM_UNIQUE_LLMIME_MimeMask_A", 1, 0)
ENDIF

EVENT Follower_BrawlerStance_Activate
ON
	OnFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
ACTIONS
	//SetFaction(__Me, "Neutral_NPC")
	//CharacterSetHasDialog(__Me, 1)
IF "!c1"
	HasStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON)
THEN
	AddStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0, 1)
ELSE
	RemoveStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0)
	CharacterRemoveStatus(__Me, LLMIME_BRAWLER_STANCE_CON, null, 0)
	AddStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0, 1)
ENDIF

EVENT Reset_Follow_Priority
ON
	OnCharacterFlagSet("LeaderLib_CharacterIsFollowing", __Me)
ACTIONS
	Interrupt("Block_AutoFollow")
	SetPriority("Block_AutoFollow", 0)
	
EVENT Activate_Follow_Priority
ON
	OnCharacterFlagCleared("LeaderLib_CharacterIsFollowing", __Me)
ACTIONS
	SetPriority("Block_AutoFollow", 99998)
	//SetPriority("Block_AutoFollow", 0)
	
EVENT Mask_Equipped
VARS
	ITEM:_Mask
ON
	OnItemEquipped(__Me, _Mask)
ACTIONS
IF "c1&c2"
	IsTagged(_Mask, "LLMIME_ACTIVATE_MIME")
	IsEqual(%FirstEquip, 0)
THEN
	CallFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
	SetPriority("Equip_Mask_Auto", 0)
	Set(%FirstEquip, 1)
ENDIF

EVENT Mimicking_Applied
ON
	OnCharacterStatusApplied(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", 999)
	SetPriority("Mimic_Attack_Item", 999)

EVENT Mimicking_Removed
ON
	OnCharacterStatusRemoved(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", -1)
	SetPriority("Mimic_Attack_Item", -1)

BEHAVIOUR

REACTION Equip_Mask_Auto, 99999
USAGE PEACE
VARS
	ITEM:_Mask
CHECK "c1"
	IsEqual(%FirstEquip, 0)
ACTIONS
IF "!c1&c2&!c3"
	CharacterHasStatus(__Me, LLMIME_MIME)
	ItemGetFromInventory(_Mask, __Me, "ARM_UNIQUE_LLMIME_MimeMask_A", "LLMIME_ACTIVATE_MIME", 0)
	ItemGetFromInventory(_Mask, __Me, "ARM_UNIQUE_LLMIME_MimeMask_A", "LLMIME_ACTIVATE_MIME", 1)
THEN
	//Fix reloading story/stats shenanigans
	CharacterEvent(__Me, "Mimicry_RemoveAllMimicryStatuses")
	CharacterEvent(__Me, "Mimicry_RemoveMimeJob")
	//Sleep(1.0)
	//SetFlag(__Me, "Mimicry_HideMimePassives")
	//SetVar(__Me, "LeaderLib_ItemSlot", STRING:"Helmet")
	//CharacterEvent(__Me, "LeaderLib_UnequipItemInSlot")
	Sleep(0.50)
	CharacterItemEvent(__Me, _Mask, "LeaderLib_EquipItem")
	//CharacterUseItem(_Mask)
ELIF "c1"
	CharacterHasStatus(__Me, LLMIME_MIME)
THEN
	Set(%FirstEquip, 1)
	SetPriority("Equip_Mask_Auto", 0)
ELSE
	Reset()
ENDIF

REACTION Block_AutoFollow, 500
USAGE PEACE
VARS
	CHARACTER:_Owner
CHECK "!c1&!c2"
	HasFlag(__Me, "LeaderLib_CharacterIsFollowing")
	CharacterHasStatus(__Me, LLMIME_MIMIC_WEAPON_REQUIREMENTS)
ACTIONS
	Sleep(1)
	
REACTION Mimic_Attack_Character, 9999
USAGE ALL
VARS
	CHARACTER:_Target
CHECK "c1&!c2"
	GetVar(_Target, __Me, "Mimicry_MimicCharacterTarget")
	IsEqual(_Target, null)
ACTIONS
IF "c1"
	CharacterCanSee(__Me, _Target)
THEN
	CharacterAttack(_Target)
ELSE
	CharacterAttackWithoutMove(_Target)
ENDIF
	SetVar(__Me, "Mimicry_MimicCharacterTarget", CHARACTER:null)
	StatusText(__Me, "Attacked character")
	
REACTION Mimic_Attack_Item, 9999
USAGE ALL
VARS
	ITEM:_Target
CHECK "c1&!c2"
	GetVar(_Target, __Me, "Mimicry_MimicItemTarget")
	IsEqual(_Target, null)
ACTIONS
IF "c1"
	CharacterCanSee(__Me, _Target)
THEN
	CharacterAttack(_Target)
ELSE
	CharacterAttackWithoutMove(_Target)
ENDIF
	SetVar(__Me, "Mimicry_MimicItemTarget", ITEM:null)
	StatusText(__Me, "Attacked item")