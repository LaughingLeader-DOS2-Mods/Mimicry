INIT
	CHARACTER:__Me
	INT:%Following = 0
	INT:%LLMIME_Initialized = 0
	EXTERN FLOAT:%BrawlerStanceInfluenceStrength = 50.0
EVENTS

EVENT Init_Register
ON
	OnActivate()
ACTIONS
IF "c1"
	IsEqual(%LLMIME_Initialized, 0)
THEN
	CharacterEvent(__Me, "Mimicry_RegisterMimeFollower")
	IF "!c1"
		IsTagged(__Me, "LeaderLib_ReceiveExtraEvents")
	THEN
		SetTag(__Me, "LeaderLib_ReceiveExtraEvents")
	ENDIF
	SetVar(__Me, "LeaderLib_LogMessage", STRING:"[LLMIME:LLMIME_Mime.charScript:Init_Register] Activated.")
	CharacterEvent(__Me, "LeaderLib_DebugLog")
	Set(%LLMIME_Initialized, 1)
ENDIF

EVENT Follower_BrawlerStance_Activate
ON
	OnFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
ACTIONS
	//SetFaction(__Me, "Neutral_NPC")
	//CharacterSetHasDialog(__Me, 1)
IF "!c1"
	HasStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON)
THEN
	AddStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0, 1)
ELSE
	RemoveStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0)
	CharacterRemoveStatus(__Me, LLMIME_BRAWLER_STANCE_CON, null, 0)
	AddStatusInfluence(__Me, LLMIME_BRAWLER_STANCE_CON, %BrawlerStanceInfluenceStrength, "", 0, 1)
ENDIF

EVENT Reset_Follow_Priority
ON
	OnCharacterFlagSet("LeaderLib_CharacterIsFollowing", __Me)
ACTIONS
	Interrupt("Block_AutoFollow")
	SetPriority("Block_AutoFollow", 0)
	
EVENT Activate_Follow_Priority
ON
	OnCharacterFlagCleared("LeaderLib_CharacterIsFollowing", __Me)
ACTIONS
	SetPriority("Block_AutoFollow", 99998)
	//SetPriority("Block_AutoFollow", 0)
	
EVENT Mask_Equipped
VARS
	ITEM:_Mask
ON
	OnItemEquipped(__Me, _Mask)
ACTIONS
IF "c1"
	IsTagged(_Mask, "LLMIME_ACTIVATE_MIME")
THEN
	CallFunction("Mimicry_MimeFollower_ActivateBrawlerStance")
ENDIF

EVENT Mimicking_Applied
ON
	OnCharacterStatusApplied(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", 999)
	SetPriority("Mimic_Attack_Item", 999)

EVENT Mimicking_Removed
ON
	OnCharacterStatusRemoved(__Me, LLMIME_MIMICKING)
ACTIONS
	SetPriority("Mimic_Attack_Character", -1)
	SetPriority("Mimic_Attack_Item", -1)

BEHAVIOUR

REACTION Block_AutoFollow, 0
USAGE PEACE
VARS
	CHARACTER:_Owner
CHECK "!c1&!c2"
	HasFlag(__Me, "LeaderLib_CharacterIsFollowing")
	CharacterHasStatus(__Me, LLMIME_MIMIC_WEAPON_REQUIREMENTS)
ACTIONS
	Sleep(1)
	
REACTION Mimic_Attack_Character, 9999
USAGE ALL
VARS
	CHARACTER:_Target
CHECK "c1&c2&!c3"
	CharacterHasStatus(__Me, LLMIME_MIMICKING)
	GetVar(_Target, __Me, "Mimicry_MimicCharacterTarget")
	IsEqual(_Target, null)
ACTIONS
IF "c1"
	CharacterCanSee(__Me, _Target)
THEN
	CharacterAttack(_Target)
ELSE
	CharacterAttackWithoutMove(_Target)
ENDIF
	SetVar(__Me, "Mimicry_MimicCharacterTarget", CHARACTER:null)
	StatusText(__Me, "Attacked character")
	
REACTION Mimic_Attack_Item, 9999
USAGE ALL
VARS
	ITEM:_Target
CHECK "c1&c2&!c3"
	CharacterHasStatus(__Me, LLMIME_MIMICKING)
	GetVar(_Target, __Me, "Mimicry_MimicItemTarget")
	IsEqual(_Target, null)
ACTIONS
IF "c1"
	CharacterCanSee(__Me, _Target)
THEN
	CharacterAttack(_Target)
ELSE
	CharacterAttackWithoutMove(_Target)
ENDIF
	SetVar(__Me, "Mimicry_MimicItemTarget", ITEM:null)
	StatusText(__Me, "Attacked item")