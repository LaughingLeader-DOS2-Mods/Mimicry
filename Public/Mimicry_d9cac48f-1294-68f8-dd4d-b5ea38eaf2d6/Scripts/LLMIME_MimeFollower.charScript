INIT
	CHARACTER:__Me
	INT:%LLMIME_Follower_Initialized = 0
	INT:%LLMIME_Follower_InitialRefresh = 0
	FIXEDSTRING:%LLMIME_Follower_RefreshTimerName = ""
	CHARACTER:%LeaderLib_Follower_Owner
EVENTS

EVENT LLMIME_MimeFollower_Init_Event
VARS
	INT:_Ran
	FLOAT:_RanFloat
	STRING:_TimerName
ON
	//OnCharacterEvent(__Me, "Mimicry_MimeInitialized")
	//OnActivate()
	OnItemEvent(_, "LeaderLib_Initialized")
	OnCharacterEvent(__Me, "Mimicry_Follower_ReInitialize")
	OnCharacterEvent(__Me, "Mimicry_MimeInitialized")
ACTIONS
IF "!c1"
	IsTagged(_Mime, "LLMIME_UNARMED")
THEN
	SetTag(__Me, "LLMIME_UNARMED")
ENDIF
IF "c1"
	IsEqual(%LLMIME_Follower_InitialRefresh, 0)
THEN
	CharacterEvent(__Me, "Mimicry_InitMimeJob")
	Set(%LLMIME_Follower_InitialRefresh, 1)
ENDIF
IF "c1"
	IsEqual(%LLMIME_Follower_Initialized, 0)
THEN
	CharacterEvent(__Me, "Mimicry_RegisterMimeFollower")
	IF "!c1"
		IsTagged(__Me, "LeaderLib_ReceiveExtraEvents")
	THEN
		SetTag(__Me, "LeaderLib_ReceiveExtraEvents")
	ENDIF
	//SetVar(__Me, "LeaderLib_LogMessage", STRING:"[LLMIME:LLMIME_Mime.charScript:Init_Register] Activated.")
	//CharacterEvent(__Me, "LeaderLib_DebugLog")
	Set(%LLMIME_Follower_Initialized, 1)
	CharacterApplyStatus(__Me, LLMIME_BRAWLER_STANCE_CON_FOLLOWER, -1, 1)
ENDIF

EVENT LLMIME_MimeFollower_Refresh_Timer
ON
	OnTimer(%LLMIME_Follower_RefreshTimerName)
ACTIONS
	CharacterEvent(__Me, "LeaderLib_Commands_RefreshEquipment_Delayed")
	
EVENT LLMIME_MimeFollower_Owner_Unsheathed
VARS
	FLOAT:_Dist
ON
	OnCharacterStatusAttempt(%LeaderLib_Follower_Owner, UNSHEATHED)
ACTIONS
IF "!c1&!c2&c3&c4"
	CharacterHasStatus(__Me, UNSHEATHED)
	CharacterIsDead(__Me, 1)
	GetDistance(_Dist, %LeaderLib_Follower_Owner, __Me)
	IsLessThen(_Dist, 50)
THEN
	CharacterApplyStatus(__Me, UNSHEATHED, -1, 0, __Me)
ENDIF

EVENT LLMIME_MimeFollower_Owner_Unsheathed_Joined
VARS
	CHARACTER:_Owner
	FLOAT:_Dist
ON
	OnCharacterCharacterEvent(_Owner, __Me, "LeaderLib_Events_FollowerRecruited")
ACTIONS
IF "c1&!c2"
	CharacterHasStatus(_Owner, UNSHEATHED)
	CharacterHasStatus(__Me, UNSHEATHED)
THEN
	CharacterApplyStatus(__Me, UNSHEATHED, -1, 0, __Me)
ENDIF

EVENT LLMIME_MimeFollower_Owner_Unsheathed_Resurrected
VARS
	FLOAT:_Dist
ON
	OnCharacterStatusRemoved(__Me, RESURRECT)
ACTIONS
IF "c1&!c2&!c3"
	CharacterHasStatus(%LeaderLib_Follower_Owner, UNSHEATHED)
	CharacterHasStatus(__Me, UNSHEATHED)
	IsInCombat(__Me)
THEN
	CharacterApplyStatus(__Me, UNSHEATHED, -1, 0, __Me)
ENDIF

EVENT LLMIME_MimeFollower_Owner_Sheathed
VARS
	FLOAT:_Dist
ON
	OnCharacterStatusRemoved(%LeaderLib_Follower_Owner, UNSHEATHED)
ACTIONS
IF "!c1&!c2&c3"
	IsInCombat(__Me)
	CharacterIsDead(__Me, 1)
	CharacterHasStatus(__Me, UNSHEATHED)
THEN
	CharacterRemoveStatus(__Me, UNSHEATHED)
ENDIF

EVENT LLMIME_MimeFollower_Owner_Entered_Combat
VARS
	FLOAT:_Dist
ON
	OnEnteredCombat(%LeaderLib_Follower_Owner, _)
ACTIONS
IF "(c1&c2)|c3"
	GetDistance(_Dist, %LeaderLib_Follower_Owner, __Me)
	IsLessThen(_Dist, 20)
	CharacterCanSee(%LeaderLib_Follower_Owner, __Me)
THEN
	EnterCombat(__Me, %LeaderLib_Follower_Owner)
ENDIF

EVENT LLMIME_MimeFollower_Entered_Combat
VARS
	FLOAT:_Dist
ON
	OnEnteredCombat(__Me, _)
ACTIONS
IF "(c1&c2)|c3"
	GetDistance(_Dist, %LeaderLib_Follower_Owner, __Me)
	IsLessThen(_Dist, 20)
	CharacterCanSee(%LeaderLib_Follower_Owner, __Me)
THEN
	EnterCombat(%LeaderLib_Follower_Owner, __Me)
ENDIF

EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_Start
ON
	OnCharacterEvent(__Me, "Mimicry_Events_OnMimeFollowerRevived")
ACTIONS
	IterateParty("LLMIME_Iterator_IterateInventoryForMimeFollowerTreasure")
	IterateItems("LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	IterateItemsInInventory(__Me, "LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	
EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_Character
VARS
	CHARACTER:_Character
ON
	OnIterateCharacter(_Character, "LLMIME_Iterator_IterateInventoryForMimeFollowerTreasure")
ACTIONS
	IterateItemsInInventory(_Character, "LLMIME_Iterator_RemoveGeneratedMimeTreasure", "LLMIME_MimeFollowerTreasure")
	
EVENT LLMIME_MimeFollower_RemoveGeneratedTreasure_OnIterateItem
VARS
	ITEM:_Item
ON
	OnIterateItem(_Item, "LLMIME_Iterator_RemoveGeneratedMimeTreasure")
ACTIONS
	ItemEvent(_Item, "LLMIME_Events_OnMimeFollowerTreasureRemoved")
	ItemEvent(_Item, "LeaderLib_Commands_RemoveItem")
	
EVENT LLMIME_MimeFollower_JoinedParty
VARS
	CHARACTER:_Owner
ON
	OnCharacterCharacterEvent(_Owner, __Me, "LeaderLib_Events_FollowerRecruited")
ACTIONS
IF "c1"
	HasGlobalFlag("Mimicry_Mimic_CombatOnlyDisabled")
THEN
	CharacterEvent(__Me, "LLMIME_Events_RequestMimicking")
ENDIF

/*
EVENT LLMIME_MimeFollower_BrawlerFistSafeguard_Start
ON
	OnCharacterStatusApplied(__Me, LLMIME_BRAWLER_STANCE_CON_FOLLOWER)
ACTIONS
	StartTimer("LLMIME_Timers_Follower_CheckEquippedWeapon", 0.5, 0)

EVENT LLMIME_MimeFollower_BrawlerFistSafeguard_OnTimer
ON
	OnTimer("LLMIME_Timers_Follower_CheckEquippedWeapon")
ACTIONS
//WPN_LLMIME_BrawlerFist_e1bda904-93c2-4f6f-8136-1a547279fd1f
IF "c1"
	CharacterHasStatus(__Me, LLMIME_BRAWLER_STANCE_CON_FOLLOWER)
THEN
	CharacterEvent(__Me, "LLMIME_MimeFollower_CheckEquippedFist")
ENDIF
*/

/*
BEHAVIOUR

REACTION LLMIME_MimeFollower_OwnerDied, 1000
USAGE COMBAT
CHECK "c1&c2"
	CharacterIsDead(%LeaderLib_Follower_Owner, 1)
	IsInActiveTurn(__Me)
	CharacterHasSkill(__Me, 
ACTIONS
*/