INIT
	CHARACTER:__Me
	FIXEDSTRING:%ResetTimerName = ""
	EXTERN FLOAT:%ResetTimerDelay = 5.0
	INT:%DebugTimerActive = 0
	INT:%DebugMode = 0
EVENTS

EVENT DeleteRogueDummies
ON
	OnInit()
ACTIONS
IF "!c1"
	HasGlobalFlag("LLMIME_CreatingDummy")
THEN
	CharacterEvent(__Me, "LLMIME_Events_DummyInitialized")
ENDIF

EVENT StartIDResetTimer
VARS
	STRING:_UUID
	STRING:_TimerStr
ON
	OnCharacterEvent(__Me, "LLMIME_Events_DummyIDSet")
ACTIONS
IF "c1"
	GetVar(_UUID, __Me, "LLMIME_UUID")
THEN
	Print(_TimerStr, "LLMIME_Timers_ResetID_[1]", _UUID)
	Cast(%ResetTimerName, _TimerStr)
	StartTimer(%ResetTimerName, %ResetTimerDelay, 0)
ENDIF

EVENT ResetTimerFinished
ON
	OnTimer(%ResetTimerName)
ACTIONS
	CharacterEvent(__Me, "LLMIME_Events_ResetDummyID")
	
EVENT OnDamage_Debug
VARS
	DAMAGE:_DamageType
	DAMAGE:_CheckDamageType
	FLOAT:_Val
	CHARACTER:_Char
	CHARACTERTEMPLATE:_CharID
	ITEM:_Item
	STRING:_Message
ON
	OnDamage(_DamageType, _Val, _Char, _Item)
ACTIONS
IF "c1"
	IsEqual(%DebugMode, 1)
THEN
	Print(_Message, "[LLMIME:DummyBehavior:OnDamage_Debug] Dummy was attacked: Type: [1] | Amount: [2] | Character: [3]", _DamageType, _Val, _Char)
	SetVar(__Me, "LeaderLib_LogMessage", _Message)
	CharacterEvent(__Me, "LeaderLib_DebugLog")
	StatusText(__Me, "I was hit!")
ENDIF
	
EVENT DamageType_Debug_Start
ON
	OnCharacterStatusApplied(__Me, _)
	OnCharacterStatusAttempt(__Me, _)
	OnDamage(_, _, _, _)
ACTIONS
IF "c1&c2"
	IsEqual(%DebugTimerActive, 0)
	IsEqual(%DebugMode, 1)
THEN
	StartTimer("LLMIME_Timers_DummyDebug", 0.25, 0)
	Set(%DebugTimerActive, INT:1)
	//StatusText(__Me, "Started damage type debug timer!")
ENDIF
EVENT DamageType_Debug
VARS
	DAMAGE:_DamageType
	DAMAGE:_CheckDamageType
	STRING:_Message
ON
	OnTimer("LLMIME_Timers_DummyDebug")
ACTIONS
	Set(%DebugTimerActive, INT:0)
IF "c1"
	CharacterHasBeenHitBy(__Me, _DamageType)
THEN
	Print(_Message, "[LLMIME:DummyBehavior:DamageType_Debug] Dummy was hit with damage type: [1]", _CheckDamageType)
	SetVar(__Me, "LeaderLib_LogMessage", _Message)
	CharacterEvent(__Me, "LeaderLib_DebugLog")
ELSE
	SetVar(__Me, "LeaderLib_LogMessage", STRING:"[LLMIME:DummyBehavior:DamageType_Debug] No damage type detected!")
	CharacterEvent(__Me, "LeaderLib_DebugLog")
ENDIF