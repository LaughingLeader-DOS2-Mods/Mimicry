Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION ITEM_TRACKING
IF
CharacterUsedItem(_Caster, _Item)
AND
LLMIME_QRY_ItemIsAllowed(_Item)
AND
GetTemplate(_Item, _Template)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_QRY_Mime_CanMimicCharacter(_Caster, _Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterUsedItem] Queueing up next item mimic [",_Template,"] on mime.");
LLMIME_Mime_QueueNextSelfAction(_Mime, _Caster, _Template, "Item");

IF
CharacterUsedItem(_Caster, _Item)
AND
LLMIME_Mime_QRY_ActionTypeQueued(_Caster, "Item")
AND
GetTemplate(_Item, _Template)
AND
GetUUID(_Caster, _ID)
AND
StringConcatenate(_Template, _ID, _Str)
AND
StringConcatenate("LLMIME_Timers_MimicItemDelayTimer_", _Str, _TimerName)
THEN
DB_LLMIME_Mimicking_Item_DelayTimer(_TimerName, _Caster, _Template);
TimerLaunch(_TimerName, 500);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Mimicking_Item_DelayTimer(_TimerName, _Caster, _Template)
THEN
NOT DB_LLMIME_Mimicking_Item_DelayTimer(_TimerName, _Caster, _Template);
LLMIME_Mime_StartNextMimic(_Caster, _Template, "Item");
//END_REGION

//REGION ITEM_MIMIC
PROC
LLMIME_Mime_MimicAction((CHARACTERGUID)_Caster, (STRING)_Template, "Item")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Template, "Item")
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition(_Template, _x, _y, _z, _Item)
THEN
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster);
ItemToInventory(_Item, _Mime, 1, 0, 1);
CharacterUseItem(_Mime, _Item, "");
//ItemDestroy(_Item);
//SetOnStage(_Item, 0);
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Template, "Item");

IF
CharacterUsedItemFailed(_Mime, _Item)
AND
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster)
THEN
ItemDestroy(_Item);
SetOnStage(_Item, 0);
LLMIME_Mime_OnMimicFailed(_Mime, _Caster, _Template, "Item");
NOT DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster);

IF
CharacterUsedItem(_Mime, _Item)
AND
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster)
AND
ItemIsDestroyed(_Item, 0)
THEN
ItemDestroy(_Item);
SetOnStage(_Item, 0);

IF
CharacterUsedItem(_Mime, _Item)
AND
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster)
THEN
NOT DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Template, _Caster);
LLMIME_Mime_OnMimicSuccess(_Mime, _Template, "Item");

PROC
LLMIME_Mime_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Template, "Item")
AND
DB_LLMIME_Mimicking_Temp_ResetDummyAfterCast(_Mime, _Target, _Template, "Item")
THEN
NOT DB_LLMIME_Mimicking_Temp_ResetDummyAfterCast(_Mime, _Target, _Template, "Item");
LLMIME_Dummy_ResetDummy((GUIDSTRING)_Target);
//END_REGION

//REGION CLEAR
//Destroy mimicked items in case the use events never triggered

PROC
LLMIME_Mime_ClearQueue((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Action, _Caster)
AND
ItemIsDestroyed(_Item, 0)
THEN
ItemDestroy(_Item);
SetOnStage(_Item, 0);

PROC
LLMIME_Mime_ClearQueue((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Action, _Caster)
THEN
NOT DB_LLMIME_Mimicking_Items_DestroyNext(_Mime, _Item, _Action, _Caster);
//END_REGION


//REGION DEBUG
IF
CanUseItem(_Mime, _Item, _RequestID)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CanUseItem] [",_Name,"] is attempting to use item [",_Template,"].");

IF
CharacterUsedItem(_Mime, _Item)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItem] [",_Name,"] used item [",_Template,"].");

IF
CharacterUsedItemTemplate(_Mime, _Template, _Item)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItemTemplate] [",_Name,"] used item [",_Template,"].");

IF
ItemDestroying(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] is being destroyed.");

IF
ItemDestroyed(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] was destroyed.");

IF
CharacterItemEvent(_Mime, _Item, "LLMIME_Events_OnItemUse")
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:LLMIME_Events_OnItemUse] [",_Name,"] used item [",_Template,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
