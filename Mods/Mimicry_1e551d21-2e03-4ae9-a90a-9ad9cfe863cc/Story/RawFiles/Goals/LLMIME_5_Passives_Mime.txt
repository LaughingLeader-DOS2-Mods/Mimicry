Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Mime_InitSettings();
KBSECTION
PROC
LLMIME_Mime_InitSettings()
THEN
DB_LLMIME_Mime_MimeActivatingEquipmentTags("LLMIME_ACTIVATE_MIME");
DB_LLMIME_Mime_Skills("Shout_LLMIME_BrawlerStance");

QRY
LLMIME_Mime_QRY_IsMimeItem((ITEMGUID)_Item)
AND
DB_LLMIME_Mime_MimeActivatingEquipmentTags(_Tag)
AND
IsTagged(_Item, _Tag, 1)
THEN
DB_NOOP(1);

//REGION MIME_TOGGLING
QRY
LLMIME_Mime_QRY_SafeToSetup((CHARACTERGUID)_Mime, (ITEMGUID)_Item)
AND
NOT DB_InCharacterCreation(1)
THEN
DB_NOOP(1);

IF
ItemEquipped(_Item, _Mime)

AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Mime)
AND
NOT DB_LLMIME_Mime_Temp_BlockedItem(_Mime, _Item)
THEN
SetStoryEvent(_Mime, "LLMIME_Events_InitMime");

IF
CharacterCreationFinished(_Mime)
AND
DB_LLMIME_Mime_MimeActivatingEquipmentTags(_Tag)
AND
NOT DB_LLMIME_Temp_ActivateMime(_Mime)
AND
CharacterFindTaggedItem(_Mime, _Tag, _Mask)
THEN
DB_LLMIME_Temp_ActivateMime(_Mime);

IF
GameStarted(_,_)
AND
DB_LLMIME_Temp_ActivateMime(_Mime)
THEN
SetStoryEvent(_Mime, "LLMIME_Events_InitMime");
NOT DB_LLMIME_Temp_ActivateMime(_Mime);

IF
ItemUnEquipped(_Item, _Mime)
AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
LLMIME_QRY_CharacterIsAMime(_Mime)
THEN
SetStoryEvent(_Mime, "LLMIME_Events_RemoveMime");

IF
StoryEvent((CHARACTERGUID)_Mime, "LLMIME_Events_InitMime")
AND
DB_LLMIME_Mime_Skills(_Skill)
AND
CharacterHasSkill(_Mime, _Skill, 0)
THEN
CharacterAddSkill(_Mime, _Skill);

IF
StoryEvent((CHARACTERGUID)_Mime, "LLMIME_Events_RemoveMime")
AND
DB_LLMIME_Mime_Skills(_Skill)
AND
CharacterHasSkill(_Mime, _Skill, 1)
THEN
CharacterRemoveSkill(_Mime, _Skill);

IF
CanUseItem(_Mime, _Mask, _RequestID)
AND
GetTemplate(_Mask, _Template)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
NOT CharacterGetEquippedItem(_Mime, "Helmet", _Mask)
THEN
PlayEffect(_Mime, "LLMIME_FX_MimeMask_Transform_Root_02");
PlayEffect(_Mime, "LLMIME_FX_MimeMask_Transform_Overlay_Equipped_01");
RequestProcessed(_Mime, _RequestID, 1);

IF
ItemUnEquipped(_Mask, _Mime)
AND
GetTemplate(_Mask, _Template)
AND
DB_LLMIME_Templates("Mask_A", _Template)
THEN
PlayEffect(_Mime, "LLMIME_FX_MimeMask_Transform_Root_02");
PlayEffect(_Mime, "LLMIME_FX_MimeMask_Transform_Overlay_01");
PlayEffect(_Mime, "LLMIME_FX_MimeMask_Transform_Overlay_Unequipped_01");
//END_REGION

//REGION MASK_LEVELING
IF
CharacterLeveledUp(_Mime)
AND
IsTagged(_Mime, "LLMIME_Mime", 1)
AND
CharacterGetLevel(_Mime, _Level)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
THEN
ItemLevelUpTo(_Mask, _Level);

IF
ItemEquipped(_Mask, _Mime)
AND
GetTemplate(_Mask, _Template)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
CharacterGetLevel(_Mime, _Level)
THEN
ItemLevelUpTo(_Mask, _Level);
//END_REGION

//REGION SUMMONING
IF
GameStarted(_,_)
AND
DB_LLMIME_SummonedMime(_Caster, _Mime)
AND
ObjectExists(_Mime, 0)
THEN
NOT DB_LLMIME_SummonedMime(_Caster, _Mime);
LLMIME_Mime_SummonMime(_Caster);

PROC
LLMIME_Mime_SummonMime((CHARACTERGUID)_Caster)
AND
NOT DB_LLMIME_SummonedMime(_Caster, _)
AND
GetPosition(_Caster, _cx, _cy, _cz)
AND
TemporaryCharacterCreateAtPosition(_cx, _cy, _cz, "LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", 1, _Mime)
THEN
DB_LLMIME_SummonedMime(_Caster, _Mime);
CharacterAddToPlayerCharacter(_Mime, _Caster);

IF
SkillCast(_Caster, "Shout_LLMIME_Mime", _)
AND
NOT DB_LLMIME_Temp_Summoning(_Caster)
AND
NOT DB_LLMIME_SummonedMime(_Caster, _)
THEN
DB_LLMIME_Temp_Summoning(_Caster);
LLMIME_Mime_SummonMime(_Caster);

IF
SkillCast(_Character, "Shout_LLMIME_Mime", _)
AND
NOT DB_LLMIME_Temp_Summoning(_Character)
AND
DB_LLMIME_SummonedMime(_Character, _Mime)
THEN
DB_LLMIME_Temp_Summoning(_Character);
CharacterRemoveFromPlayerCharacter(_Mime, _Character);
Poof(_Mime);

IF
CharacterWentOnStage(_Mime, 0)
AND
DB_LLMIME_SummonedMime(_Character, _Mime)
THEN
NOT DB_LLMIME_SummonedMime(_Character, _Mime);
CharacterDie(_Mime, 0, "");

IF
SkillCast(_Character, "Shout_LLMIME_Mime", _)
AND
DB_LLMIME_Temp_Summoning(_Character)
THEN
NOT DB_LLMIME_Temp_Summoning(_Character);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
