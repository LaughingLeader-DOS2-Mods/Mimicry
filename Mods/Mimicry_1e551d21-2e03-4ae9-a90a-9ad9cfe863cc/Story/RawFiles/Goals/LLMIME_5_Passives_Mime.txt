Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Mime_InitSettings();
KBSECTION
PROC
LLMIME_Mime_InitSettings()
THEN
DB_LLMIME_Mime_MimeActivatingEquipmentTags("LLMIME_ACTIVATE_MIME");

QRY
LLMIME_Mime_QRY_IsMimeItem((ITEMGUID)_Item)
AND
DB_LLMIME_Mime_MimeActivatingEquipmentTags(_Tag)
AND
IsTagged(_Item, _Tag, 1)
THEN
DB_NOOP(1);

//REGION MIME_TOGGLING
IF
ItemEquipped(_Item, _Mime)
AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Mime)
THEN
SetStoryEvent(_Mime, "LLMIME_Events_InitMime");

IF
ItemUnEquipped(_Item, _Mime)
AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
LLMIME_QRY_CharacterIsAMime(_Mime)
THEN
SetStoryEvent(_Mime, "LLMIME_Events_RemoveMime");
//END_REGION

//REGION MASK_LEVELING
IF
CharacterLeveledUp(_Mime)
AND
CharacterGetLevel(_Mime, _Level)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_Clothing_LLMIME_MimeMask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mask)
THEN
ItemLevelUpTo(_Mask, _Level);

IF
ItemEquipped(_Mask, _Mime)
AND
GetTemplate(_Mask, "EQ_Clothing_LLMIME_MimeMask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8")
AND
CharacterGetLevel(_Mime, _Level)
THEN
ItemLevelUpTo(_Mask, _Level);
//END_REGION

//REGION SUMMONING
IF
GameStarted(_,_)
AND
DB_LLMIME_SummonedMime(_Caster, _Mime)
AND
ObjectExists(_Mime, 0)
THEN
NOT DB_LLMIME_SummonedMime(_Caster, _Mime);
LLMIME_Mime_SummonMime(_Caster);

PROC
LLMIME_Mime_SummonMime((CHARACTERGUID)_Caster)
AND
NOT DB_LLMIME_SummonedMime(_Caster, _)
AND
GetPosition(_Caster, _cx, _cy, _cz)
AND
TemporaryCharacterCreateAtPosition(_cx, _cy, _cz, "LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", 1, _Mime)
THEN
DB_LLMIME_SummonedMime(_Caster, _Mime);
CharacterAddToPlayerCharacter(_Mime, _Caster);

IF
SkillCast(_Caster, "Shout_LLMIME_Mime", _)
AND
NOT DB_LLMIME_Temp_Summoning(_Caster)
AND
NOT DB_LLMIME_SummonedMime(_Caster, _)
THEN
DB_LLMIME_Temp_Summoning(_Caster);
LLMIME_Mime_SummonMime(_Caster);

IF
SkillCast(_Character, "Shout_LLMIME_Mime", _)
AND
NOT DB_LLMIME_Temp_Summoning(_Character)
AND
DB_LLMIME_SummonedMime(_Character, _Mime)
THEN
DB_LLMIME_Temp_Summoning(_Character);
CharacterRemoveFromPlayerCharacter(_Mime, _Character);
Poof(_Mime);

IF
CharacterWentOnStage(_Mime, 0)
AND
DB_LLMIME_SummonedMime(_Character, _Mime)
THEN
NOT DB_LLMIME_SummonedMime(_Character, _Mime);
CharacterDie(_Mime, 0, "");

IF
SkillCast(_Character, "Shout_LLMIME_Mime", _)
AND
DB_LLMIME_Temp_Summoning(_Character)
THEN
NOT DB_LLMIME_Temp_Summoning(_Character);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
