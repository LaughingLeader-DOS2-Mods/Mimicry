Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION FIST_EQUIPPING
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
IsTagged(_Item, "LLMIME_BrawlerFist", 1)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Character)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_BrawlerFistBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);
//DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
//END_REGION

//REGION MASK_EQUIPPING
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
GetTemplate(_Item, _Template)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
NOT CharacterGetEquippedItem(_Character, "Helmet", _Item) // Mask not equipped
AND
CharacterIsInCombat(_Character, 1)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_MimeMaskBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);
//DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
//END_REGION

//REGION MIME_SKILLBOOKS
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSkillbook", 1)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Character)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_MimeSkillbookBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);
//END_REGION

//REGION MIME_FOLLOWER
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
IsTagged(_Item, "BOOK", 1)
AND
IsTagged(_Character, "LLMIME_MimeFollower", 1)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_SkillbookBlockedForFollower");
DB_CustomUseItemResponse(_Character, _Item, 0);

//Prevent general item use with the Mime follower
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
IsTagged(_Character, "LLMIME_MimeFollower", 1)
AND
NOT DB_CustomUseItemResponse(_Character, _Item, _)
AND
ItemIsInInventory(_Item, 1)
AND
NOT LLMIME_Mimicking_QRY_IsMimicking(_Character)
AND
NOT GetInventoryOwner(_Item, _Character)
THEN
DB_CustomUseItemResponse(_Character, _Item, 0);
//END_REGION

//REGION MUSIC_BOX_USAGE
PROC
ProcBlockUseOfItem(_Char, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
NOT GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
THEN
LLMIME_Items_OnMusicBoxUsed(_Char, _Item);

//Why do they still have this?
PROC
ProcBlockUseOfItem(_Char, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlocked");
DB_CustomUseItemResponse(_Char, _Item, 0);

//Resurrection dialog (mask required)
PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
AND
CharacterIsInCombat(_Char, 0)
AND
NOT LLMIME_Follower_QRY_CanSummon(_Char)
AND
LLMIME_Follower_QRY_CanResurrectMime(_Char)
THEN
DB_CustomUseItemResponse(_Char, _Item, 0);
LLMIME_Follower_Internal_SetupMusicBoxAttributeCheck(_Char);
Proc_StartDialog(0, "LLMIME_MusicBox_Resurrection", _Item, _Char);

PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
AND
CharacterIsInCombat(_Char, 0)
AND
NOT LLMIME_Follower_QRY_CanSummon(_Char)
AND
NOT LLMIME_Follower_QRY_CanResurrectMime(_Char)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlocked");
DB_CustomUseItemResponse(_Char, _Item, 0);

PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
AND
LLMIME_Follower_QRY_CanSummon(_Char)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxUsed");
DB_CustomUseItemResponse(_Char, _Item, 1);

PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlocked");
DB_CustomUseItemResponse(_Char, _Item, 0);

IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 1)
THEN
LLMIME_Follower_ReturnIfNotInCombat(_Char);

PROC
ProcBlockUseOfItem((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
IsTagged(_Item, "LLMIME_MusicBoxShard", 1)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
NOT ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 1)
THEN
DB_CustomUseItemResponse(_Char, _Item, 0);
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxShardBlocked");

IF
CharacterUsedItemTemplate(_Char, _Template, _Item)
AND
DB_LLMIME_Templates("MusicBox_Shard", _Template)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 1)
THEN
LLMIME_Follower_ReturnIfNotInCombat(_Char);
//END_REGION

//REGION MUSIC_BOX_TRANSFORM
PROC
LLMIME_Items_TransformMusicBox((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
NOT DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox)
AND
DB_LLMIME_Templates("MusicBox", _Template)
AND
GetTemplate(_MusicBox, _Template)
AND
DB_LLMIME_Templates("MusicBox_Cracked", _TransformTemplate)
THEN
Transform(_MusicBox, _TransformTemplate, 1, 0, 1);
SetTag(_MusicBox, "Mimicry_CrackedMusicBox");
DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox);

PROC
LLMIME_Items_TransformMusicBox((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
NOT DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox)
AND
DB_LLMIME_Templates("MusicBox_Cracked", _Template)
AND
GetTemplate(_MusicBox, _Template)
AND
DB_LLMIME_Templates("MusicBox_Shard", _TransformTemplate)
THEN
ClearTag(_MusicBox, "Mimicry_CrackedMusicBox");
Transform(_MusicBox, _TransformTemplate, 1, 1, 1);
CharacterStatusText(_Player, "LLMIME_StatusText_MusicBoxShatters");
LeaderLog_SetOneshotTarget(_Player);
LeaderLog_Log("COMBAT", "LLMIME_StatusText_MusicBoxShatters");
GlobalSetFlag("Mimicry_MusicBoxShattered");
DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox);

PROC
LLMIME_Items_TransformMusicBox((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox)
THEN
NOT DB_LLMIME_Items_Temp_MusicBoxTransformed(_Player, _MusicBox);
//END_REGION

//REGION EQUIP_BLOCKING
IF
ItemEquipped(_Item, _Character)
AND
DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item)
THEN
NOT DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
CharacterUnequipItem(_Character, _Item);
//END_REGION

//REGION DEBUG
IF
ItemEquipped(_Item, _Character)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemEquipped] [",_Name,"] equipped item [",_Template,"].");

IF
ItemUnEquipped(_Item, _Character)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemUnEquipped] [",_Name,"] unequipped item [",_Template,"].");

IF
CanUseItem(_Mime, _Item, _RequestID)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
AND
IntegertoString(_RequestID, _RequestIDStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CanUseItem] [",_Name,"] is attempting to use item [",_Template,"] with RequestID [",_RequestIDStr,"].");

IF
CharacterUsedItem(_Mime, _Item)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItem] [",_Name,"] used item [",_Template,"].");

IF
CharacterUsedItemTemplate(_Mime, _Template, _Item)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItemTemplate] [",_Name,"] used item [",_Template,"].");

IF
CharacterUsedItemFailed(_Mime, _Item)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItemFailed] [",_Name,"] failed to use item [",_Template,"].");

IF
ItemDestroying(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] is being destroyed.");

IF
ItemDestroyed(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] was destroyed.");

IF
CharacterItemEvent(_Mime, _Item, "LLMIME_Events_OnItemUse")
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:LLMIME_Events_OnItemUse] [",_Name,"] used item [",_Template,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
