Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION FIST_EQUIPPING
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
IsTagged(_Item, "LLMIME_BrawlerFist", 1)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Character)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_BrawlerFistBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);
//DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
//END_REGION

//REGION MASK_EQUIPPING
PROC
ProcBlockUseOfItem(_Character, _Item)
AND
LLMIME_Mime_QRY_IsMimeItem(_Item)
AND
GetTemplate(_Item, _Template)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
NOT CharacterGetEquippedItem(_Character, "Helmet", _Item) // Mask not equipped
AND
CharacterIsInCombat(_Character, 1)
THEN
CharacterStatusText(_Character, "LLMIME_StatusText_MimeMaskBlocked");
DB_CustomUseItemResponse(_Character, _Item, 0);
//DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
//END_REGION

//REGION MUSIC_BOX
PROC
ProcBlockUseOfItem(_Char, _Item)
AND
GetTemplate(_Item, _Template)
AND
DB_LLMIME_Templates("MusicBox", _Template)
THEN
LLMIME_Items_OnMusicBoxUsed(_Char, _Item);

//Resurrection dialog (mask required)
PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
AND
CharacterIsInCombat(_Char, 0)
AND
NOT LLMIME_Follower_QRY_CanSummon(_Char)
AND
LLMIME_Follower_QRY_CanResurrectMime(_Char)
THEN
DB_CustomUseItemResponse(_Char, _Item, 0);
Proc_StartDialog(0, "LLMIME_MusicBox_Resurrection", _Item, _Char);

PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
AND
LLMIME_Follower_QRY_CanSummon(_Char)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxUsed");
DB_CustomUseItemResponse(_Char, _Item, 1);

PROC
LLMIME_Items_OnMusicBoxUsed((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_CustomUseItemResponse(_Char, _Item, _)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlocked");
DB_CustomUseItemResponse(_Char, _Item, 0);

IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
GetTemplate(_Item, _Template)
AND
DB_LLMIME_Templates("MusicBox", _Template)
AND
CharacterIsDead(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 0)
AND
ObjectGetFlag(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, "LeaderLib_IsFollower", 1)
THEN
LLMIME_Follower_ReturnIfNotInCombat(_Char);
//END_REGION

//REGION ITEM_LEVELING
IF
ItemEquipped(_Item, _Character)
AND
IsTagged(_Item, "LLMIME_AUTO_LEVEL", 1)
AND
CharacterGetLevel(_Character, _Level)
THEN
ItemLevelUpTo(_Item, _Level);

IF
CharacterLeveledUp(_Character)
AND
CharacterFindTaggedItem(_Character, "LLMIME_AUTO_LEVEL", _Item)
AND
CharacterGetLevel(_Character, _Level)
THEN
ItemLevelUpTo(_Item, _Level);
//END_REGION

//REGION EQUIP_BLOCKING
IF
ItemEquipped(_Item, _Character)
AND
DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item)
THEN
NOT DB_LLMIME_Items_Temp_BlockedItem(_Character, _Item);
CharacterUnequipItem(_Character, _Item);
//END_REGION

//REGION DEBUG
IF
ItemEquipped(_Item, _Character)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemEquipped] [",_Name,"] equipped item [",_Template,"].");

IF
ItemUnEquipped(_Item, _Character)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Character, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemUnEquipped] [",_Name,"] unequipped item [",_Template,"].");

IF
CanUseItem(_Mime, _Item, _RequestID)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CanUseItem] [",_Name,"] is attempting to use item [",_Template,"].");

IF
CharacterUsedItem(_Mime, _Item)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItem] [",_Name,"] used item [",_Template,"].");

IF
CharacterUsedItemTemplate(_Mime, _Template, _Item)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItemTemplate] [",_Name,"] used item [",_Template,"].");

IF
CharacterUsedItemFailed(_Mime, _Item)
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterUsedItemFailed] [",_Name,"] failed to use item [",_Template,"].");

IF
ItemDestroying(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] is being destroyed.");

IF
ItemDestroyed(_Item)
AND
GetTemplate(_Item, _Template)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:ItemDestroying] Item [",_Template,"] was destroyed.");

IF
CharacterItemEvent(_Mime, _Item, "LLMIME_Events_OnItemUse")
AND
GetTemplate(_Item, _Template)
AND
CharacterGetDisplayName(_Mime, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:LLMIME_Events_OnItemUse] [",_Name,"] used item [",_Template,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
