Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION TUTORIAL_IMMUNITY
IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 0)
THEN
GlobalSetFlag("Mimicry_LevelIsOriginsTutorial");
DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime); // To avoid dying from the source blast

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Mime, _Template, _)
THEN
ItemTemplateAddTo(_Template, _Player, 1);

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
THEN
NOT DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime);

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Amulet)
THEN
ItemToInventory(_Amulet, _Player, 1, 1);

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 1)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInPartyInventory(_Player, _Template, _Amulet)
THEN
ObjectSetFlag(_Player, "Mimicry_HasMimeAmulet");

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "Mimicry_HasMimeAmulet", 1)
THEN
ObjectClearFlag(_Player, "Mimicry_HasMimeAmulet");

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "Mimicry_HasMimeAmulet", 1)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 1)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
NOT GetItemForItemTemplateInPartyInventory(_Player, _Template, _)
THEN
ObjectClearFlag(_Player, "Mimicry_HasMimeAmulet");

IF
ObjectFlagCleared("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
THEN
DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime);

IF
ObjectFlagCleared("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInPartyInventory(_Player, _Template, _Amulet)
THEN
ItemToInventory(_Amulet, _Mime, 1, 1);
PartyClearFlag(_Player, "Mimicry_HasMimeAmulet", 0);

IF
FadeInDone(_,"TUT_LowerDeck_WakeUpAfterWindego")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_TUT_LowerDeck_WindegoKnockedDown(_Mime)
THEN
NOT DB_TUT_LowerDeck_WindegoKnockedDown(_Mime);
//CharacterSetCanTrade(_Mime, 1);
RemoveStatus(_Mime,"KNOCKED_DOWN");
SetCanFight(_Mime,1);
SetCanJoinCombat(_Mime,1);
CharacterSetAnimationOverride(_Mime,"");
CharacterStatusText(_Mime, "LLMIME_StatusText_MimeAmuletShatters");
CharacterSetHitpointsPercentage(_Mime, 100);
GlobalClearFlag("Mimicry_LevelIsOriginsTutorial");

IF
FadeInDone(_,"TUT_LowerDeck_WakeUpAfterWindego")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Amulet)
THEN
ItemDestroy(_Amulet);
ObjectClearFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled");
//END_REGION

/*For disappearing when the player escapes on the boat, and re-appearing on the beach.*/
//REGION TUT_ESCAPE_FTJ_BEACH
IF
GlobalFlagSet("TUT_LowerBoatReadyToLower")
THEN
LLMIME_Follower_Origins_OnTutorialDone();

PROC
PROC_TUT_EndTutorial()
THEN
LLMIME_Follower_Origins_OnTutorialDone();

PROC
LLMIME_Follower_Origins_OnTutorialDone()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "LLMIME_TUT_MimeIsInvisible", 0)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
ObjectSetFlag(_Mime, "LLMIME_TUT_MimeIsInvisible");
PlayEffectAtPosition("RS3_FX_Skills_Rogue_CloakDagger_Cast_01", _x, _y, _z);
PlayEffect(_Mime, "RS3_FX_Skills_Void_Cast_VoidGlide_Overlay_01");
PlayAnimation(_Mime, "Teleport_01_Out", "Mimicry_Events_TUT_SetMimeFollowerToInvisible");

IF
StoryEvent(_Mime, "Mimicry_Events_TUT_SetMimeFollowerToInvisible")
THEN
SetVisible(_Mime, 0);

PROC
Proc_FTJ_StartGame()
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
SetVisible(_Mime, 0);
ObjectSetFlag(_Mime, "LLMIME_FTJ_MimeIsInvisible");


PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"FTJ_WakeUpTimer")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "LLMIME_FTJ_MimeIsInvisible", 1)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
ObjectClearFlag(_Mime, "LLMIME_FTJ_MimeIsInvisible");
PlayEffectAtPosition("RS3_FX_Skills_Rogue_CloakDagger_Cast_01", _x, _y, _z);
PlayEffect(_Mime, "LLMIME_FX_Scripted_Follower_Summon_TeleportOverlay_01");
LeaderLib_StartObjectTimer(_Mime, 500, "LLMIME_Timers_FTJ_MimeAppearTimer_", "Mimicry_Origins_FTJ_MimeFollowerAppearedOnBeach");
CharacterLookAt(_Mime, _Player);

IF
StoryEvent(_Mime, "Mimicry_Origins_FTJ_MimeFollowerAppearedOnBeach")
THEN
PlayAnimation(_Mime, "Teleport_In_01", "");
SetVisible(_Mime, 1);
//END_REGION

//REGION SOURCE
IF
ObjectFlagSet("GLO_Has2MaxMP", (CHARACTERGUID)_Owner, _)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetMaxSourcePoints(_Owner, _Amount)
THEN
CharacterOverrideMaxSourcePoints(_Mime, _Amount);

IF
ObjectFlagSet("GLO_Has3MaxMP", (CHARACTERGUID)_Owner, _)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetMaxSourcePoints(_Owner, _Amount)
THEN
CharacterOverrideMaxSourcePoints(_Mime, _Amount);

IF
CharacterJoinedParty(_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
AND
LeaderLib_Helper_QRY_HasStatus(_Owner, "SOURCE_MUTED")
THEN
CharacterOverrideMaxSourcePoints(_Mime, 0);

IF
CharacterJoinedParty(_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
AND
NOT LeaderLib_Helper_QRY_HasStatus(_Owner, "SOURCE_MUTED")
AND
CharacterGetMaxSourcePoints(_Owner, _Amount)
THEN
CharacterOverrideMaxSourcePoints(_Mime, _Amount);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLMIME_3_1_Follower_Origins__Start"
