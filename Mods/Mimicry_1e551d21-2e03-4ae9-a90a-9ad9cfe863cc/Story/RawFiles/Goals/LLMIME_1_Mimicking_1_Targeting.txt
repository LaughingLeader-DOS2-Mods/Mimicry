Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName)
//DB_LLMIME_Mimic_Temp_MimicTarget(_Character, _Mime, _Target)

//DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
KBSECTION
//REGION CLEAR
PROC
LLMIME_Mime_ClearTargetData((CHARACTERGUID)_Mime, (CHARACTERGUID)_Character, (REAL)_StartX, (REAL)_StartY, (REAL)_StartZ, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target)
THEN
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target);

PROC
LLMIME_Mime_ClearTargetData((CHARACTERGUID)_Mime, (CHARACTERGUID)_Character, (REAL)_StartX, (REAL)_StartY, (REAL)_StartZ, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimicking_Temp_FinalMimicTarget(_Mime, _Character, _Target, _Action, _ActionType, _EventTimerName)
THEN
NOT DB_LLMIME_Mimicking_Temp_FinalMimicTarget(_Mime, _Character, _Target, _Action, _ActionType, _EventTimerName);

PROC
LLMIME_Mime_ClearTargetData((CHARACTERGUID)_Mime, (CHARACTERGUID)_Character, (REAL)_StartX, (REAL)_StartY, (REAL)_StartZ, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
THEN
NOT DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType);

PROC
LLMIME_Mime_ClearTargetData((CHARACTERGUID)_Mime, (CHARACTERGUID)_Character, (REAL)_StartX, (REAL)_StartY, (REAL)_StartZ, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
THEN
NOT DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType);
//END_REGION

//REGION CALCULATE
PROC
LLMIME_Mimic_CalculateTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Character, (REAL)_StartX, (REAL)_StartY, (REAL)_StartZ, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
RealSubtract(_TargetX, _StartX, _xdiff)
AND
RealSubtract(_TargetY, _StartY, _ydiff)
AND
RealSubtract(_TargetZ, _StartZ, _zdiff)
AND
GetPosition(_Mime, _MimeX, _MimeY, _MimeZ)
AND
Integer(_xdiff, _xdiffint)
AND
Integer(_ydiff, _ydiffint)
AND
Integer(_zdiff, _zdiffint)
AND
Real(_xdiffint, _xdiffRounded)
AND
Real(_ydiffint, _ydiffRounded)
AND
Real(_zdiffint, _zdiffRounded)
AND
RealSum(_MimeX, _xdiffRounded, _MimeTargetX)
AND
RealSum(_MimeY, _ydiffRounded, _MimeTargetY)
AND
RealSum(_MimeZ, _zdiffRounded, _MimeTargetZ)
/*
AND
RealSum(_MimeX, _xdiff, _MimeTargetX)
AND
RealSum(_MimeY, _ydiff, _MimeTargetY)
AND
RealSum(_MimeZ, _zdiff, _MimeTargetZ)
*/
THEN
//DB_LLMIME_Mimic_TargetPosition(_Character, _Mime, _MimeTargetX, _MimeTargetY, _MimeTargetZ);
LLMIME_Mimic_GetTargetNearPosition(_Character, _Mime, _MimeTargetX, _MimeTargetY, _MimeTargetZ, _TargetPriority, _Action, _ActionType);
//LLMINE_Debug_TracePositions(_StartX, _StartY, _StartZ, _TargetX, _TargetY, _TargetZ, _MimeX, _MimeY, _MimeZ, _MimeTargetX, _MimeTargetY, _MimeTargetZ);
//END_REGION

//REGION ITERATOR_START
PROC
LLMIME_Mimic_GetTargetNearPosition((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
GetUUID(_Mime, _MimeID)
AND
LeaderLog_QRY_Log("COMBINE","LLMIME_DummyID_", _MimeID, "_", _ActionType, "_", _Action)
AND
DB_LeaderLog_Temp_CombinedString(_DummyTaskID)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_DummyTaskID);
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType);
LLMIME_Dummy_CreateCharacterDummy(_DummyTaskID, _TargetX, _TargetY, _TargetZ);

IF
StoryEvent((CHARACTERGUID)_Dummy, _DummyTaskID) // Fires when the dummy is teleported
AND
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
AND
DB_LLMIME_Dummy_NextCharacterDummy(_DummyTaskID, _Dummy)
THEN
NOT DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType);
NOT DB_LLMIME_Dummy_NextCharacterDummy(_DummyTaskID, _Dummy);
LLMIME_Mimic_Target_StartDummyIteration(_Dummy, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType);

/*
//Is the target outside the bounds of the map?
PROC
LLMIME_Mimic_Target_StartDummyIteration((CHARACTERGUID)_Dummy, (CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
CharacterCanSee(_Mime, _Dummy, 0)
AND
CharacterCanSee(_Character, _Dummy, 0)
THEN
TeleportTo(_Dummy, _Mime);
*/

PROC
LLMIME_Mimic_Target_StartDummyIteration((CHARACTERGUID)_Dummy, (CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (STRING)_Action, (STRING)_ActionType)
AND
NOT DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _, _TargetPriority, _Action, _ActionType)
AND
GetUUID(_Character, _ID1)
AND
GetUUID(_Mime, _ID2)
AND
StringConcatenate(_ID1, "_", _Str2)
AND
StringConcatenate(_Str2, _ID2, _UniqueID)
AND
StringConcatenate("LLMIME_Events_FindMimicTarget_", _UniqueID, _EventName)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Starting dummy character iterator for [",_Action, _ActionType,"].");
DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType);
CharacterLaunchIteratorAroundCharacter(_Dummy, 2.5, _EventName);

IF
StoryEvent((CHARACTERGUID)_CheckChar, _EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
AND
_CheckChar != NULL_00000000-0000-0000-0000-000000000000
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Checking characters around dummy for [",_Action, _ActionType,"].");
LLMIME_Mimic_Target_CheckCharacter(_CheckChar, _EventName);

PROC
LLMIME_Mimic_Target_CheckCharacter((CHARACTERGUID)_CheckChar, (STRING)_EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
AND
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character,_)
AND
_CheckChar != _Dummy
AND
_CheckChar != _Mime
THEN
LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set mimic target for [",_Action, _ActionType,"] to first target found.");
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _CheckChar);

PROC
LLMIME_Mimic_Target_CheckCharacter((CHARACTERGUID)_CheckChar, (STRING)_EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Caster, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
AND
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _OtherTarget)
AND
_CheckChar != _OtherTarget
AND
_CheckChar != _Dummy
AND
_CheckChar != _Mime
AND
LLMIME_QRY_Mimic_Targeting_IsCloserToDummy(_CheckChar, _OtherTarget, _Dummy)
THEN
LLMIME_Mimic_Target_EvaluateTarget(_CheckChar, _OtherTarget, _Mime, _Caster, _Dummy, _TargetPriority);
//END_REGION

//REGION EVALUATE_TARGET
QRY
LLMIME_QRY_Mimic_Targeting_IsCloserToDummy((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Dummy)
AND
GetDistanceTo(_CheckChar, _Dummy, _Dist1)
AND
GetDistanceTo(_OtherTarget, _Dummy, _Dist2)
AND
_Dist1 <= _Dist2
THEN
DB_NOOP(1);

//Prioritize enemies over allies, if the caster targetted an enemy
PROC
LLMIME_Mimic_Target_EvaluateTarget((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (CHARACTERGUID)_Dummy, (INTEGER)_TargetPriority)
AND
_TargetPriority == 1
AND
CharacterIsEnemy(_CheckChar, _Mime, 1)
AND
CharacterIsEnemy(_OtherTarget, _Mime, 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Set mimic target to enemy (priority is 1).");
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _OtherTarget);
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _CheckChar);

//Prioritize allies over enemies, if the caster targetted an ally
PROC
LLMIME_Mimic_Target_EvaluateTarget((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (CHARACTERGUID)_Dummy, (INTEGER)_TargetPriority)
AND
_TargetPriority == 0
AND
CharacterIsEnemy(_CheckChar, _Mime, 0)
AND
LLMIME_QRY_Mimic_Targeting_PrioritizeAlly(_CheckChar, _OtherTarget, _Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Set mimic target to ally (priority is 0).");
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _OtherTarget);
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _CheckChar);

QRY
LLMIME_QRY_Mimic_Targeting_PrioritizeAlly((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime)
AND
CharacterIsAlly(_OtherTarget, _Mime, 0)
AND
CharacterIsAlly(_CheckChar, _Mime, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_Mimic_Targeting_PrioritizeAlly((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime)
AND
CharacterIsAlly(_OtherTarget, _Mime, 1)
AND
CharacterIsAlly(_CheckChar, _Mime, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_Mimic_Targeting_PrioritizeAlly((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime)
AND
CharacterIsAlly(_OtherTarget, _Mime, 0)
AND
CharacterIsAlly(_CheckChar, _Mime, 0)
THEN
DB_NOOP(1);

PROC
LLMIME_Mimic_Target_EvaluateTarget((CHARACTERGUID)_CheckChar, (CHARACTERGUID)_OtherTarget, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (CHARACTERGUID)_Dummy, (INTEGER)_TargetPriority)
AND
_TargetPriority == -1
AND
LLMIME_QRY_Mimic_Targeting_IsCloserToDummy(_CheckChar, _OtherTarget, _Dummy)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Set mimic target to character (priority is -1).");
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _OtherTarget);
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _CheckChar);
//END_REGION

//REGION ITERATOR_END
IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, _EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Caster, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
AND
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster,_)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set mimic target for [",_Action, _ActionType,"] to the target dummy.");
TeleportToPosition(_Dummy, _TargetX, _TargetY, _TargetZ, "", 0);
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _Dummy);

IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, _EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Caster, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
AND
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster,_)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set mimic target for [",_Action, _ActionType,"] to the mime.");
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Caster, _Mime);

IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, _EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Caster, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Cleaning up character iterator for [",_Action, _ActionType,"].");
NOT DB_LLMIME_Mimic_Temp_TargetIterator(_Caster, _Mime, _Dummy, _TargetX, _TargetY, _TargetZ, _EventName, _TargetPriority, _Action, _ActionType);
LLMIME_Mime_TargetSystem_CleanupIterator(_Caster, _Mime, _Dummy, _Action, _ActionType, _TargetPriority);
//END_REGION

//REGION FINALIZING
PROC
LLMIME_Mime_TargetSystem_CleanupIterator((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (INTEGER)_TargetPriority)
AND
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] [ERROR] No target set! How can this be?! [",_Action, _ActionType,"]");

//Use an item for the actual target, since they can be invulnerable and hide combat text
PROC
LLMIME_Mime_TargetSystem_CleanupIterator((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (INTEGER)_TargetPriority)
AND
GlobalGetFlag("LLMIME_MIMIC_PREFER_ITEM_TARGET", 1)
AND
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target)
AND
_Target == _Dummy
AND
GetPosition(_Dummy, _x, _y, _z)
AND
GetVarString(_Dummy, "LLMIME_DummyID", _DummyID)
AND
LLMIME_Dummy_QRY_CreateItemDummy(_DummyID, _x, _y, _z)
AND
DB_LLMIME_Dummy_NextItemDummy(_DummyID, _TargetDummy)
THEN
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target);
NOT DB_LLMIME_Dummy_NextItemDummy(_DummyID, _TargetDummy);
LLMIME_Mime_TargetSystem_SetFinalTarget(_Character, _Mime, _Dummy, _Action, _ActionType, _TargetDummy, _TargetPriority);

PROC
LLMIME_Mime_TargetSystem_CleanupIterator((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (INTEGER)_TargetPriority)
AND
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target)
THEN
NOT DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target);
LLMIME_Mime_TargetSystem_SetFinalTarget(_Character, _Mime, _Dummy, _Action, _ActionType, _Target, _TargetPriority);

PROC
LLMIME_Mime_TargetSystem_SetFinalTarget((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (GUIDSTRING)_Target, (INTEGER)_TargetPriority)
AND
_Target != _Dummy
THEN
LLMIME_Dummy_ResetDummy(_Dummy);

PROC
LLMIME_Mime_TargetSystem_SetFinalTarget((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (GUIDSTRING)_Target, (INTEGER)_TargetPriority)
AND
_Target == _Dummy
AND
_TargetPriority == 0
THEN
SetFaction(_Dummy,"Good NPC");
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set dummy faction to [Good NPC] for next mimic skill [",_Action, _ActionType,"].");

PROC
LLMIME_Mime_TargetSystem_SetFinalTarget((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (GUIDSTRING)_Target, (INTEGER)_TargetPriority)
AND
_Target == _Dummy
AND
_TargetPriority == 1
THEN
SetFaction(_Dummy,"Evil NPC");
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set dummy faction to [Evil NPC] for next mimic skill [",_Action, _ActionType,"].");

PROC
LLMIME_Mime_TargetSystem_SetFinalTarget((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (GUIDSTRING)_Target, (INTEGER)_TargetPriority)
AND
_Target == _Dummy
AND
_TargetPriority == -1
THEN
SetFaction(_Dummy,"Neutral NPC");
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Set dummy faction to [Neutral NPC] for next mimic skill [",_Action, _ActionType,"].");

PROC
LLMIME_Mime_TargetSystem_SetFinalTarget((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime, (CHARACTERGUID)_Dummy, (STRING)_Action, (STRING)_ActionType, (GUIDSTRING)_Target, (INTEGER)_TargetPriority)
AND
GetUUID(_Mime, _MimeID)
AND
String(_Target, _TargetID)
AND
LeaderLog_QRY_Log("COMBINE","LLMIME_EventTimer_TargetAcquired_", _MimeID, "_", _TargetID, "_", _Action, _ActionType)
AND
DB_LeaderLog_Temp_CombinedString(_EventTimerName)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Launching final timer and then event for [",_Action, _ActionType,"].");
NOT DB_LeaderLog_Temp_CombinedString(_EventTimerName);
DB_LLMIME_Mimicking_Temp_FinalMimicTarget(_Mime, _Character, _Target, _Action, _ActionType, _EventTimerName);
TimerLaunch(_EventTimerName, 5); // Frames for the dummy's position to update

IF
TimerFinished(_EventTimerName)
AND
DB_LLMIME_Mimicking_Temp_FinalMimicTarget(_Mime, _Character, _Target, _Action, _ActionType, _EventTimerName)
THEN
//LeaderLog_Log("TRACE", "[LLMIME:Mime:Targeting] Sending final target completion event [",_EventTimerName,"].");
SetStoryEvent(_Mime, _EventTimerName);

//END_REGION

//REGION DEBUG

/*
IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, _EventName)
AND
DB_LLMIME_Mimic_Temp_TargetIterator(_Character, _Mime, _, _, _, _, _EventName, _TargetPriority, _Action, _ActionType)
AND
DB_LLMIME_Mimic_Temp_MimicTarget(_Mime, _Character, _Target)
AND
GetPosition(_Target, _x, _y, _z)
AND
CharacterGetDisplayName(_Target, _, _TargetID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mimic_Targeting] Tracing target: ", _TargetID);
LLMINE_Debug_TraceTarget(_x, _y, _z);
*/

PROC
LLMINE_Debug_TracePositions((REAL)_xr1, (REAL)_yr1, (REAL)_zr1, (REAL)_xr2, (REAL)_yr2, (REAL)_zr2, (REAL)_xr3, (REAL)_yr3, (REAL)_zr3, (REAL)_xr4, (REAL)_yr4, (REAL)_zr4)
AND
Integer(_xr1, _x1)
AND
IntegertoString(_x1, _xstr1)
AND
Integer(_yr1, _y1)
AND
IntegertoString(_y1, _ystr1)
AND
Integer(_zr1, _z1)
AND
IntegertoString(_z1, _zstr1)
AND
Integer(_xr2, _x2)
AND
IntegertoString(_x2, _xstr2)
AND
Integer(_yr2, _y2)
AND
IntegertoString(_y2, _ystr2)
AND
Integer(_zr2, _z2)
AND
IntegertoString(_z2, _zstr2)
AND
Integer(_xr3, _x3)
AND
IntegertoString(_x3, _xstr3)
AND
Integer(_yr3, _y3)
AND
IntegertoString(_y3, _ystr3)
AND
Integer(_zr3, _z3)
AND
IntegertoString(_z3, _zstr3)
AND
Integer(_xr4, _x4)
AND
IntegertoString(_x4, _xstr4)
AND
Integer(_yr4, _y4)
AND
IntegertoString(_y4, _ystr4)
AND
Integer(_zr4, _z4)
AND
IntegertoString(_z4, _zstr4)
AND
LeaderLog_QRY_Log("COMBINE", _xstr1, ",", _zstr1, " => ", _xstr2, ",", _zstr2)
AND
DB_LeaderLog_Temp_CombinedString(_CombinedStr)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_CombinedStr);
LeaderLog_Log("DEBUG", "[LLMIME:Mimic_Targeting] Caster's target: ", _CombinedStr, " | Mime's target: ", _xstr3, ",", _zstr3, " => ", _xstr4, ",", _zstr4);

/*
PROC
LLMINE_Debug_TraceTarget((REAL)_xr1, (REAL)_yr1, (REAL)_zr1)
AND
Integer(_xr1, _x1)
AND
IntegertoString(_x1, _xstr1)
AND
Integer(_yr1, _y1)
AND
IntegertoString(_y1, _ystr1)
AND
Integer(_zr1, _z1)
AND
IntegertoString(_z1, _zstr1)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mimic_Targeting] Target is at position: ", _xstr1, ",",_zstr1);

IF
CharacterUsedSkillAtPosition(_Char, _x, _y, _z, _Skill, _SkillType)
AND
IsTagged(_Char, "LLMIME_MIME", 0)
AND
GetPosition(_Char, _sx, _sy, _sz)
AND
Integer(_sx, _x1)
AND
IntegertoString(_x1, _xstr1)
AND
Integer(_sy, _y1)
AND
IntegertoString(_y1, _ystr1)
AND
Integer(_sz, _z1)
AND
IntegertoString(_z1, _zstr1)
AND
Integer(_x, _x2)
AND
IntegertoString(_x2, _xstr2)
AND
Integer(_y, _y2)
AND
IntegertoString(_y2, _ystr2)
AND
Integer(_z, _z2)
AND
IntegertoString(_z2, _zstr2)
AND
LeaderLog_QRY_Log("COMBINE", "StandingPos[",_xstr1,",",_zstr1,"] TargetPos[",_xstr2, ",", _zstr2,"]")
AND
DB_LeaderLog_Temp_CombinedString(_CombinedStr)
THEN
NOT DB_LeaderLog_Temp_CombinedString(_CombinedStr);
LeaderLog_Log("DEBUG", "[LLMIME:CharacterUsedSkillAtPosition] Skill[",_Skill,"(",_SkillType,")] ", _CombinedStr);
*/
//END_REGION

//REGION TRACE_TARGET_DEBUG
/*
IF
StoryEvent((CHARACTERGUID)_Dummy, _DummyTaskID)
AND
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
AND
GetPosition(_Dummy, _rx, _ry, _rz)
AND
Integer(_rx, _x)
AND
Integer(_ry, _y)
AND
Integer(_rz, _z)
AND
IntegertoString(_x, _DummyX)
AND
IntegertoString(_y, _DummyY)
AND
IntegertoString(_z, _DummyZ)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Dummy teleported to position [",_DummyX,",",_DummyY,",",_DummyZ,"] for [",_Action, _ActionType,"].");

IF
StoryEvent((CHARACTERGUID)_Dummy, _DummyTaskID)
AND
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
AND
GetPosition(_Mime, _rx, _ry, _rz)
AND
Integer(_rx, _x)
AND
Integer(_ry, _y)
AND
Integer(_rz, _z)
AND
IntegertoString(_x, _MimeX)
AND
IntegertoString(_y, _MimeY)
AND
IntegertoString(_z, _MimeZ)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Mime is at position [",_MimeX,",",_MimeY,",",_MimeZ,"] for [",_Action, _ActionType,"].");

IF
StoryEvent((CHARACTERGUID)_Dummy, _DummyTaskID)
AND
DB_LLMIME_Targeting_Temp_NextDummy(_DummyTaskID, _Character, _Mime, _TargetX, _TargetY, _TargetZ, _TargetPriority, _Action, _ActionType)
AND
Integer(_TargetX, _x)
AND
Integer(_TargetY, _y)
AND
Integer(_TargetZ, _z)
AND
IntegertoString(_x, _TraceX)
AND
IntegertoString(_y, _TraceY)
AND
IntegertoString(_z, _TraceZ)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:Targeting] Target position is [",_TraceX,",",_TraceY,",",_TraceZ,"] for [",_Action, _ActionType,"].");
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
