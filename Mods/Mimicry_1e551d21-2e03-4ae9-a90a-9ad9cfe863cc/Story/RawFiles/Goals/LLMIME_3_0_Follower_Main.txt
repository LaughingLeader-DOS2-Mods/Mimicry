Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Follower_InitSettings();
//DB_LLMIME_Follower_RandomAnimation(_Index, _Animation)
//DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
//DB_LLMIME_Follower_Mime(_Mime)
//DB_LLMIME_Follower_Temp_FollowOwner(_Follower, _Owner)
KBSECTION
//REGION SETTINGS
PROC
LLMIME_Follower_InitSettings()
THEN
//LeaderLib_Follower_RegisterPosition((CHARACTERGUID)CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, "TestLevel_LL_Mimicry", 4.92, 0.0, 6.42, "Mimicry_Events_MimeFollowerPositionReset");

LLMIME_Follower_AddRandomAnimation("use_inspect");
LLMIME_Follower_AddRandomAnimation("Teleport_01");
LLMIME_Follower_AddRandomAnimation("Think_01");
LLMIME_Follower_AddRandomAnimation("Think_02");
LLMIME_Follower_AddRandomAnimation("Cheer_01");
LLMIME_Follower_AddRandomAnimation("Cheer_02");
LLMIME_Follower_AddRandomAnimation("Cheer_03");
LLMIME_Follower_AddRandomAnimation("Victory_01");
LLMIME_Follower_AddRandomAnimation("Victory_02");
LLMIME_Follower_AddRandomAnimation("Pray_01");
LLMIME_Follower_AddRandomAnimation("Pickpocket_01");
LLMIME_Follower_AddRandomAnimation("Listen_01");
LLMIME_Follower_AddRandomAnimation("Cower_01");
LLMIME_Follower_AddRandomAnimation("Cower_02");
LLMIME_Follower_AddRandomAnimation("Crying_01");
LLMIME_Follower_AddRandomAnimation("Check_Neck_01");
LLMIME_Follower_AddRandomAnimation("Bored_03");
LLMIME_Follower_AddRandomAnimation("Attention_01");
LLMIME_Follower_AddRandomAnimation("Annoyed_01");
LLMIME_Follower_AddRandomAnimation("BehindBars_01");
LLMIME_Follower_AddRandomAnimation("Clean_Floor_01");
LLMIME_Follower_AddRandomAnimation("Clean_Floor_02");
LLMIME_Follower_AddRandomAnimation("Dust_Off_01");
LLMIME_Follower_AddRandomAnimation("Pst_03");
LLMIME_Follower_AddRandomAnimation("Stand_Read_01");
LLMIME_Follower_AddRandomAnimation("Sigh_01");
LLMIME_Follower_AddRandomAnimation("stillmental");
LLMIME_Follower_AddRandomAnimation("stilldrunk");
LLMIME_Follower_AddRandomAnimation("use_craft");
LLMIME_Follower_AddRandomAnimation("Lie_Tied_Up_01_Loop");
LLMIME_Follower_AddRandomAnimation("Worship_01");

PROC
LLMIME_Follower_AddRandomAnimation((STRING)_Animation)
AND
SysCount("DB_LLMIME_Follower_RandomAnimation", 2, _Index)
THEN
DB_LLMIME_Follower_RandomAnimation(_Index, _Animation);
//END_REGION

//REGION CHARACTER_SETUP
IF
StoryEvent(_, "LeaderLib_Events_OnDefaultEventFlowComplete")
THEN
LLMIME_Follower_RegisterMime();

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_RegisterMimeFollower")
THEN
LLMIME_Follower_Internal_SetMime(_Mime);

IF
StoryEvent(_, "LeaderLib_Initialized")
THEN
LLMIME_Follower_Internal_SetupMask();

IF
StoryEvent(_, "LLMIME_StartSummoningFollower")
THEN
LLMIME_Follower_Internal_SetupMask();

PROC
LLMIME_Follower_Internal_SetupMask()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("Mask_NoVisual", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
AND
NOT CharacterGetEquippedItem(_Mime, "Helmet", _Mask)
THEN
CharacterEquipItem(_Mime, _Mask);

PROC
LLMIME_Follower_Internal_SetupMask()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("Mask_NoVisual", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Mime, _Template, _)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition(_Template, _x, _y, _z, _Mask)
THEN
CharacterEquipItem(_Mime, _Mask);

PROC
LLMIME_Follower_Internal_SetMime((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Follower_Mime(_Mime)
THEN
SysClear("DB_LLMIME_Follower_Mime", 1);
DB_LLMIME_Follower_Mime(_Mime);

PROC
LLMIME_Follower_RegisterMime()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectExists(_Mime, 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:RegisterMime] [ERROR] Follower mime doesn't exist!");
NOT DB_LLMIME_Follower_Mime(_Mime);

PROC
LLMIME_Follower_RegisterMime()
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 1)
THEN
LLMIME_Follower_Internal_SetMime((CHARACTERGUID)CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9);

PROC
LLMIME_Follower_RegisterMime()
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 0)
AND
CharacterGetHostCharacter(_Player)
AND
CharacterCreateOutOfSightToObject("LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", _Player, _Player, 0, "", _Mime)
THEN
LLMIME_Follower_Internal_SetMime(_Mime);
SetOnStage(_Mime, 0);
//END_REGION

//REGION PLAY_RANDOM_ANIM
IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
IsTagged(_Player, "LLMIME_MIME", 0)
AND
ObjectGetFlag(_Mime, "LeaderLib_CharacterIsFollowing", 0) // Don't bother if following, since anims won't play
AND
SysCount("DB_LLMIME_Follower_RandomAnimation", 2, _Total)
AND
IntegerSubtract(_Total, 1, _LastIndex)
AND
Random(_LastIndex, _Index)
AND
DB_LLMIME_Follower_RandomAnimation(_Index, _Animation)
THEN
PlayAnimation(_Mime, _Animation);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Played animation [",_Animation,"].");
//END_REGION

//REGION QUERIES
QRY
LLMIME_Follower_QRY_CanSummon((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 0)
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_HasMusicBox((CHARACTERGUID)_Char)
AND
DB_LLMIME_Templates("MusicBox", _Template)
AND
ItemTemplateIsInUserInventory(_Char, _Template, 0, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_HasMaskBox((CHARACTERGUID)_Char)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Char, _Template, 0, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

QRY
LLMIME_Follower_QRY_CanResurrectMime((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0) // In lieue of resurrect scrolls not working
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Char, _Template, 1, _Count)
AND
_Count > 0

THEN
DB_NOOP(1);
//END_REGION

//REGION MUSICBOX_RESPONSES
IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
GetTemplate(_Item, "UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb")
AND
CharacterIsInCombat(_Char, 1)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlockedInCombat");

//Reset dismissal timer
IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
GetTemplate(_Item, "UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 10000);

IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
GetTemplate(_Item, "UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsRunningAway", 0)
THEN
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);
//END_REGION

//REGION MUSICBOX_SUMMONING
PROC
LLMIME_Follower_ReturnIfNotInCombat((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsInCombat(_Mime, 0)
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist >= 30
THEN
CharacterUseSkill(_Mime, "Shout_LLMIME_ReturnToOwner", _Char, 1, 1);

IF
CharacterUsedItem(_Char, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
THEN
LLMIME_Follower_StartSummoning(_Char, _Item);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist <= 30
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 0)
THEN
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist <= 30
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Resurrected", 0, "", 1, 5000.0, 0, 0);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist > 30
AND
GetPosition(_Mime, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Rogue_CloakDagger_Cast_01", _x, _y, _z);
PlayEffect(_Mime, "LLMIME_FX_Scripted_Follower_Summon_TeleportOverlay_01");
LeaderLib_StartCharacterItemTimer(_Char, _Item, 100, "LLMIME_Timers_FollowerSummonTimer_", "LLMIME_StartSummoningFollower");

IF
CharacterItemEvent(_Char, _Item, "LLMIME_StartSummoningFollower")
THEN
LLMIME_Follower_SummonFollower(_Char, _Item);

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerPause(_TimerName);

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerUnpause(_TimerName);

PROC
LLMIME_Follower_SummonFollower((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
ItemIsInInventory(_Item, 0)
AND
GetPosition(_Item, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Void_Cast_VoidGlide_Root_01", _x, _y, _z);
TeleportToPosition(_Mime, _x, _y, _z, "Mimicry_Follower_MimeSummoned_Item", 0);
SetOnStage(_Mime, 1);
CharacterCharacterSetEvent(_Char, _Mime, "Mimicry_MimeSummoned");

PROC
LLMIME_Follower_SummonFollower((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
ItemIsInInventory(_Item, 1)
THEN
TeleportTo(_Mime, _Char, "Mimicry_Follower_MimeSummoned_Char", 0);
SetOnStage(_Mime, 1);
CharacterCharacterSetEvent(_Char, _Mime, "Mimicry_MimeSummoned");

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_Follower_MimeSummoned_Char")
AND
GetPosition(_Mime, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_Skills_Void_Cast_VoidGlide_Root_01", _x, _y, _z);

IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 0)
THEN
ObjectSetFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Summoner, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);

IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Summoner, "LLMIME_MimeCompanion_Resurrected", 0, "", 1, 5000.0, 0, 0);

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollowerJustSummoned", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
//END_REGION

//REGION RECRUITMENT_EVENTS
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_FollowerIsLeaving");
CharacterEnableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 1);
SetFaction(_Mime,"Good NPC");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage);
TimerCancel(_TimerName);
//END_REGION

//REGION DISMISSAL
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
CharacterDisableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 0);
SetFaction(_Mime,"Neutral NPC");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsDead(_Mime, 0)
THEN
ObjectSetFlag(_Mime, "Mimicry_FollowerIsLeaving");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _, _)
AND
GetUUID(_Mime, _MimeID)
AND
StringConcatenate("LLMIME_Timers_ResetPositionTimer_", _MimeID, _TimerName)
THEN
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1);
TimerLaunch(_TimerName, 10000);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Started position reset timer.");

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1);
//ApplyStatus(_Mime, "INVISIBLE", 3.0, 1);
TimerLaunch(_TimerName, 1000);
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
AND
GetVarObject(_Mime, "LeaderLib_Follower_LastOwner", _Owner)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
ObjectSetFlag(_Mime, "Mimicry_FollowerIsRunningAway");
CharacterDisappearOutOfSightToObject(_Mime, _Owner, 1, "LLMIME_Events_MimeFollowerDisappeared", 1);

IF
StoryEvent((CHARACTERGUID)_Mime, "LLMIME_Events_MimeFollowerDisappeared")
THEN
//LLLIB_Follower_ResetPosition(_Mime);
//LeaderLog_Log("DEBUG", "[LLMIME:Follower] Reset follower's position to the recruit position.");
Poof(_Mime);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Poofed the Mime follower.");
ObjectClearFlag(_Mime, "Mimicry_FollowerIsLeaving");
ObjectClearFlag(_Mime, "Mimicry_FollowerIsRunningAway");

/*
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_Events_MimeFollowerPositionReset")
THEN
SetOnStage(_Mime, 1);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Set Mime follower on stage.");
*/

//No last owner found
IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
//LLLIB_Follower_ResetPosition(_Mime, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
//LeaderLog_Log("DEBUG", "[LLMIME:Follower] Reset follower's position to the recruit position.");
Poof(_Mime);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Poofed the Mime follower.");
//END_REGION

//REGION NON_RECRUITED_COMBAT
IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
THEN
CharacterEnableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 1);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
IntegertoString(_Percentage, _HpStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Debug] Mime follower's health is: [",_HpStr,"%]");

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
_Percentage <= 75
THEN
CharacterAddAttitudeTowardsPlayer(_Mime, _Owner, -5);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
_Percentage <= 40
THEN
EnterCombat(_Mime, _Owner);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsInCombat(_Mime, 0)
AND
CharacterIsPlayer(_Owner, 0)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
THEN
ApplyStatus(_Mime, "INVISIBLE", 90.0, 1);

IF
ObjectEnteredCombat((CHARACTERGUID)_Mime, _CombatID)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
ObjectGetFlag(_Mime, "LeaderLib_IsFollower", 0)
THEN
CharacterAddActionPoints(_Mime, 2);
CharacterUseSkill(_Mime, "Shout_LLMIME_BrawlerStance", _Mime, 1, 1);
//END_REGION

//REGION RETURN_SKILL
IF
CharacterTeleportToFleeWaypoint(_Owner, _)
AND
DB_LLLIB_PartyFollower(_Owner, _Follower)
AND
IsTagged(_Follower, "LLMIME_MimeFollower", 1)
THEN
RemoveStatus(_Follower, "LLMIME_MIMICKING");
ApplyStatus(_Follower, "INVISIBLE", 9.0, 1);
LeaveCombat(_Follower);
CharacterUseSkill(_Follower, "Shout_LLMIME_ReturnToOwner", _Follower, 1, 1);

IF
SkillCast(_Follower, "Shout_LLMIME_ReturnToOwner", _)
THEN
LeaderLib_StartObjectTimer(_Follower, 250, "LLMIME_Timers_ReturnDelay_", "LLMIME_Events_StartTeleport");

IF
StoryEvent((CHARACTERGUID)_Follower, "LLMIME_Events_StartTeleport")
AND
DB_LLLIB_PartyFollower(_Owner, _Follower)
THEN
TeleportTo(_Follower, _Owner, "Shout_LLMIME_ReturnToOwner", 1);

IF
StoryEvent((CHARACTERGUID)_Follower, "Shout_LLMIME_ReturnToOwner")
THEN
LeaderLib_StartObjectTimer(_Follower, 500, "LLMIME_Timers_ReturnEffectDelay_", "LLMIME_Events_PlayReturnEffect");

IF
StoryEvent((CHARACTERGUID)_Follower, "LLMIME_Events_PlayReturnEffect")
THEN
PlayEffect(_Follower, "RS3_FX_Skills_Void_Cast_VoidGlide_Root_01");
//END_REGION

//REGION LEVEL_UP
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
IntegertoString(_Level, _LevelStr)
AND
IntegertoString(_MimeLevel, _MimeLevelStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:FollowerRecruited_Debug] Level: Mime[",_MimeLevelStr,"] Owner[",_LevelStr,"]");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_PlayerIsRecruitingCharacter")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
_MimeLevel < _Level
THEN
CharacterLevelUpTo(_Mime, _Level);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:FollowerRecruited] Mime joined party and is a lower level than the recruiting character. Leveling up.");

IF
CharacterLeveledUp(_Owner)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
_MimeLevel < _Level
THEN
CharacterRemoveFromPlayerCharacter(_Mime, _Owner);
LeaderLib_StartCharacterCharacterTimer(_Owner, _Mime, 100, "Mimicry_Timers_Follower_LevelUpDelay_", "Mimicry_Follower_LevelUpAndAttachFollower");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "Mimicry_Follower_LevelUpAndAttachFollower")
AND
CharacterGetLevel(_Owner, _Level)
THEN
CharacterLevelUpTo(_Mime, _Level);
CharacterAddToPlayerCharacter(_Mime, _Owner);
//END_REGION

//REGION GHOST
QRY
LLMIME_Follower_QRY_GhostExists()
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
AND
ObjectExists(_MimeGhost, 1)
THEN
DB_NOOP(1);

IF
GameStarted(_,_)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 1)
AND
NOT LLMIME_Follower_QRY_GhostExists()
THEN
LeaderLib_StartObjectTimer(_Mime, 500, "Mimicry_Timers_Follower_SpawnGhost_", "Mimicry_Follower_SpawnGhost");

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_Follower_SpawnGhost")
THEN
LLMIME_Follower_SpawnGhost(_Mime);

IF
CharacterDied(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
LLMIME_Follower_SpawnGhost(_Mime);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
AND
ObjectExists((CHARACTERGUID)_MimeGhost, 0)
THEN
NOT DB_LLMIME_FollowerGhost(_MimeGhost);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
AND
ObjectExists((CHARACTERGUID)_MimeGhost, 1)
AND
CharacterIsDead(_MimeGhost, 0)
THEN
TeleportTo(_MimeGhost, _Mime, "Mimicry_MimeGhostAppeared", 0);

IF
StoryEvent(_MimeGhost, "Mimicry_MimeGhostAppeared")
THEN
SetOnStage(_MimeGhost, 1);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_FollowerGhost(_)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
CharacterGetItemTemplateCount(_Mime, _Template, _Count)
AND
_Count > 0
AND
GetPosition(_Mime, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, "S_LLMIME_Ghost_Mime_d699d0e5-250d-4d4d-bacf-96e19e60235c", 1, _MimeGhost)
THEN
DB_LLMIME_FollowerGhost(_MimeGhost); 
DB_Dialogs(_MimeGhost, "LLMIME_MimeCompanion_Ghost");
CharacterLinkGhost(_Mime, _MimeGhost);
CharacterSetImmortal(_MimeGhost,1);
ProcSetInvulnerable(_MimeGhost,1);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
AND
DB_LLLIB_PartyFollower(_Owner, _Mime)
THEN
ObjectSetFlag(_MimeGhost,"GLO_Ghost_Visible");

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
THEN
SetOnStage(_MimeGhost, 0);

IF
RegionEnded(_)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
THEN
SetOnStage(_MimeGhost, 0);
//DestroyGhost(_MimeGhost);
NOT DB_LLMIME_FollowerGhost(_MimeGhost);
//END_REGION

//REGION GHOST_MASK
IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Ghost)
AND
GetPosition(_Ghost, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_GhostDissipate_01", _x, _y, _z);


IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Ghost)
AND
DB_LLMIME_Follower_Mime(_Mime)
//AND
//DB_LLMIME_FollowerGhost(_Ghost)
THEN
CharacterTransform(_Mime, "LLMIME_Elves_Male_Mime_NoMask_8b69a5c0-1d6a-403c-ae38-e4a8d4297ceb", 0, 0, 0, 0, 0, 0, 0);
CharacterUnlinkGhost(_Mime, _Ghost);
//DestroyGhost(_Ghost);
CharacterSetImmortal(_Ghost,0);
ProcSetInvulnerable(_Ghost,0);
CharacterDie(_Ghost, 0, "", _Player);
LeaderLib_Follower_DismissCharacter(_Mime, "", 0);
SetTag(_Mime, "BLOCK_RESURRECTION");

//For testing (since the Mime should be dead normally)
IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
THEN
CharacterDie(_Mime, 0, "None", _Player);

IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Mime, _Template, _)
THEN
ItemTemplateAddTo(_Template, _Player, 1);

IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
THEN
//CharacterUnequipItem(_Mime, _Mask);
ItemToInventory(_Mask, _Player, 1, 1, 1);

IF
CharacterDied(_MimeGhost)
AND
DB_LLMIME_FollowerGhost(_MimeGhost)
THEN
SetOnStage(_MimeGhost, 0);
NOT DB_LLMIME_FollowerGhost(_MimeGhost);
//END_REGION

//REGION RESURRECTION_EVENT
IF
DialogEnded("LLMIME_MusicBox_Resurrection", _Instance)
AND
GlobalGetFlag("Mimicry_EventFlag_ReviveMimeFollower", 1)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (ITEMGUID)_MusicBox)
THEN
LLMIME_Follower_BeginResurrection(_Player, _MusicBox);
GlobalClearFlag("Mimicry_EventFlag_ReviveMimeFollower");

PROC
LLMIME_Follower_BeginResurrection((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
ObjectSetFlag(_Player, "Mimicry_PlayerRevivedMimeFollower");
SetOnStage(_Mime, 1);
CharacterResurrectCustom(_Mime, "resurrect");
ObjectSetFlag(_Mime, "Mimicry_WasResurrectedByMask");
LeaderLib_StartCharacterItemTimer(_Player, _MusicBox, 2500, "LLMIME_Timers_FollowerrTimer_", "Mimicry_MimeFollowerResurrected");
DB_LLMIME_Follower_RevivedBy(_Player);

PROC
LLMIME_Follower_BeginResurrection((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
GetTemplate(_MusicBox, "UNIQUE_LLMIME_MusicBox_Cracked_467b732e-c599-49b1-beb8-06e55f1dd140")
THEN
ItemDestroy(_MusicBox);
CharacterStatusText(_Player, "LLMIME_StatusText_MusicBoxShatters");

PROC
LLMIME_Follower_BeginResurrection((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
GetTemplate(_MusicBox, "UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb")
THEN
Transform(_MusicBox, "UNIQUE_LLMIME_MusicBox_Cracked_467b732e-c599-49b1-beb8-06e55f1dd140", 1, 0, 1);
ObjectSetFlag(_MusicBox, "Mimicry_CrackedMusicBox");

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
SetStoryEvent(_Mime, "LLMIME_MimeFollowerRevived");

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_RevivedBy(_Player)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Player, _Template, 1, _Count)
AND
_Count > 0
AND
GetItemForItemTemplateInInventory(_Player, _Template, _Mask)
THEN
CharacterTransform(_Mime, "LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", 0, 0, 0, 0, 0, 0, 0);
ItemToInventory(_Mask, _Mime);
//CharacterEquipItem(_Mime, _Mask);
ChangeAttitude(_Mime, _Player, 0);

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_RevivedBy(_Player)
THEN
NOT DB_LLMIME_Follower_RevivedBy(_Player);

IF
CharacterItemEvent(_Player, _MusicBox, "Mimicry_MimeFollowerResurrected")
THEN
LLMIME_Follower_StartSummoning(_Player, _MusicBox);

IF
DialogEnded("LLMIME_MimeCompanion_Resurrected", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
THEN
Proc_StartDialog(0, "LLMIME_MimeCompanion_Default", _Mime, _Player);
ObjectClearFlag(_Player, "Mimicry_MaskTakenFromFollower");
//END_REGION

//REGION TUTORIAL_IMMUNITY
IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 0)
THEN
GlobalSetFlag("Mimicry_LevelIsOriginsTutorial");
DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime); // To avoid dying from the source blast

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Mime, _Template, _)
THEN
ItemTemplateAddTo(_Template, _Player, 1);

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
THEN
NOT DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime);

IF
ObjectFlagSet("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Amulet)
THEN
ItemToInventory(_Amulet, _Player, 1, 1);

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 1)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInPartyInventory(_Player, _Template, _Amulet)
THEN
ObjectSetFlag(_Player, "Mimicry_HasMimeAmulet");

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "Mimicry_HasMimeAmulet", 1)
THEN
ObjectClearFlag(_Player, "Mimicry_HasMimeAmulet");

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_CurrentLevel("TUT_Tutorial_A")
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
ObjectGetFlag(_Player, "Mimicry_HasMimeAmulet", 1)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled", 1)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
NOT GetItemForItemTemplateInPartyInventory(_Player, _Template, _)
THEN
ObjectClearFlag(_Player, "Mimicry_HasMimeAmulet");

IF
ObjectFlagCleared("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
THEN
DB_TUT_LowerDeck_WindegoSpareCharacters(_Mime);

IF
ObjectFlagCleared("Mimicry_MimeFollower_SourceBlastImmunityDisabled", (CHARACTERGUID)_Mime, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInPartyInventory(_Player, _Template, _Amulet)
THEN
ItemToInventory(_Amulet, _Mime, 1, 1);
PartyClearFlag(_Player, "Mimicry_HasMimeAmulet", 0);

IF
FadeInDone(_,"TUT_LowerDeck_WakeUpAfterWindego")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_TUT_LowerDeck_WindegoKnockedDown(_Mime)
THEN
NOT DB_TUT_LowerDeck_WindegoKnockedDown(_Mime);
//CharacterSetCanTrade(_Mime, 1);
RemoveStatus(_Mime,"KNOCKED_DOWN");
SetCanFight(_Mime,1);
SetCanJoinCombat(_Mime,1);
CharacterSetAnimationOverride(_Mime,"");
CharacterStatusText(_Mime, "LLMIME_StatusText_MimeAmuletShatters");
CharacterSetHitpointsPercentage(_Mime, 100);
GlobalClearFlag("Mimicry_LevelIsOriginsTutorial");

IF
FadeInDone(_,"TUT_LowerDeck_WakeUpAfterWindego")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("MimeAmulet", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Amulet)
THEN
ItemDestroy(_Amulet);
ObjectClearFlag(_Mime, "Mimicry_MimeFollower_SourceBlastImmunityDisabled");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
