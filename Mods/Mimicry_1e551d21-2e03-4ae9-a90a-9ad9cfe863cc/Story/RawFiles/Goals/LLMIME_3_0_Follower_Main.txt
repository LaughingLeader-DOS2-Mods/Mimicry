Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Follower_InitSettings();
//DB_LLMIME_Follower_RandomAnimation(_Index, _Animation)
//DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
//DB_LLMIME_Follower_Mime(_Mime)
/*Temporary*/
//DB_LLMIME_Follower_Temp_FollowOwner(_Follower, _Owner)
//DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime)
//DB_LLMIME_Follower_Temp_SpawnedGhostAfterCombat(_CombatID, _Mime)
//DB_LLMIME_Follower_Temp_GhostEffect(_Ghost, _Handle)
KBSECTION
//REGION SETTINGS
PROC
LLMIME_Follower_InitSettings()
THEN
//LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 1, "Target_EnemyCripplingBlow", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 2, "MultiStrike_LLMIME_MimeVault_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 3, "Target_LLMIME_Wish_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 4, "Target_LLMIME_DisarmingBlow_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 6, "Projectile_LLMIME_Decoy_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 7, "Target_LLMIME_SealEvil_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 8, "Target_LLMIME_VitalInfluence_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 9, "Shout_LLMIME_QuakeSlam_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 10, "Target_LLMIME_KneeBreaker_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 11, "Shout_LLMIME_EvasiveManeuver_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 12, "Target_LLMIME_SilencingStare_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 13, "Projectile_LLMIME_FinalWish_Follower", "Tag", "LLMIME_MimeFollower");
LeaderLib_Growth_Characters_Register_Skill("Mimicry.MimeFollower", 15, "Target_LLMIME_Fatality_Follower", "Tag", "LLMIME_MimeFollower");
//END_REGION

//REGION SETTINGS_ANIMATIONS
PROC
LLMIME_Follower_InitSettings()
AND
NOT DB_LLMIME_Follower_RandomAnimation(_,_)
THEN
LLMIME_Follower_AddRandomAnimation("use_inspect");
LLMIME_Follower_AddRandomAnimation("Teleport_01");
LLMIME_Follower_AddRandomAnimation("Think_01");
LLMIME_Follower_AddRandomAnimation("Think_02");
LLMIME_Follower_AddRandomAnimation("Cheer_01");
LLMIME_Follower_AddRandomAnimation("Cheer_02");
LLMIME_Follower_AddRandomAnimation("Cheer_03");
LLMIME_Follower_AddRandomAnimation("Victory_01");
LLMIME_Follower_AddRandomAnimation("Victory_02");
LLMIME_Follower_AddRandomAnimation("Pray_01");
LLMIME_Follower_AddRandomAnimation("Pickpocket_01");
LLMIME_Follower_AddRandomAnimation("Listen_01");
LLMIME_Follower_AddRandomAnimation("Cower_01");
LLMIME_Follower_AddRandomAnimation("Cower_02");
LLMIME_Follower_AddRandomAnimation("Crying_01");
LLMIME_Follower_AddRandomAnimation("Check_Neck_01");
LLMIME_Follower_AddRandomAnimation("Bored_03");
LLMIME_Follower_AddRandomAnimation("Attention_01");
LLMIME_Follower_AddRandomAnimation("Annoyed_01");
LLMIME_Follower_AddRandomAnimation("BehindBars_01");
LLMIME_Follower_AddRandomAnimation("Clean_Floor_01");
LLMIME_Follower_AddRandomAnimation("Clean_Floor_02");
LLMIME_Follower_AddRandomAnimation("Dust_Off_01");
LLMIME_Follower_AddRandomAnimation("Pst_03");
LLMIME_Follower_AddRandomAnimation("Stand_Read_01");
LLMIME_Follower_AddRandomAnimation("Sigh_01");
LLMIME_Follower_AddRandomAnimation("stillmental");
LLMIME_Follower_AddRandomAnimation("stilldrunk");
LLMIME_Follower_AddRandomAnimation("use_craft");
LLMIME_Follower_AddRandomAnimation("Lie_Tied_Up_01_Loop");
LLMIME_Follower_AddRandomAnimation("Worship_01");

PROC
LLMIME_Follower_AddRandomAnimation((STRING)_Animation)
AND
SysCount("DB_LLMIME_Follower_RandomAnimation", 2, _Index)
THEN
DB_LLMIME_Follower_RandomAnimation(_Index, _Animation);
//END_REGION

//REGION UPDATING
QRY
LLMIME_Follower_Internal_QRY_VersionNeedsUpdating("0.9.7.0", (STRING)_NewVersion)
THEN
DB_NOOP(1);

PROC
LeaderUpdater_ModUpdated("Mimicry", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
LLMIME_Follower_Internal_QRY_VersionNeedsUpdating(_PastVersion, _NewVersion)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Updating settings from version [",_PastVersion,"]");
LLMIME_Follower_InitSettings();
//END_REGION

//REGION CHARACTER_SETUP
QRY
LLMIME_Follower_QRY_CreateMimeIfNeeded((CHARACTERGUID)_Summoner)
THEN
LLMIME_Follower_Internal_CreateMimeIfNeeded(_Summoner);

PROC
LLMIME_Follower_Internal_CreateMimeIfNeeded((CHARACTERGUID)_Summoner)
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 1)
THEN
DB_LLMIME_Follower_Mime((CHARACTERGUID)CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:CreateMimeIfNeeded] Set DB_LLMIME_Follower_Mime to global mime follower.");

/*
IF
DB_LLMIME_Follower_Mime(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9)
AND
ObjectIsOnStage(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:CreateMimeIfNeeded] Setting global mime follower on stage.");
ApplyStatus(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, "INVISIBLE", 18.0, 1);
SetOnStage(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 1);
*/

PROC
LLMIME_Follower_Internal_CreateMimeIfNeeded((CHARACTERGUID)_Summoner)
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 0)
AND
NOT GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
AND
CharacterCreateOutOfSightToObject("CHARACTERGUID_LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", _Summoner,_Summoner, 1, "", _Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:CreateMimeIfNeeded] Global mime follower does not exist. Creating new mime follower.");
LLMIME_Follower_Internal_SetMime(_Mime);

IF
StoryEvent(_, "LeaderLib_Events_OnDefaultEventFlowComplete")
THEN
LLMIME_Follower_RegisterMime();

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_RegisterMimeFollower")
THEN
LLMIME_Follower_Internal_SetMime(_Mime);

PROC
LLMIME_Follower_Internal_SetMime((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Follower_Mime(_Mime)
THEN
SysClear("DB_LLMIME_Follower_Mime", 1);
DB_LLMIME_Follower_Mime(_Mime);

PROC
LLMIME_Follower_RegisterMime()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectExists(_Mime, 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:RegisterMime] [ERROR] Follower mime doesn't exist!");
NOT DB_LLMIME_Follower_Mime(_Mime);

PROC
LLMIME_Follower_RegisterMime()
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 1)
THEN
LLMIME_Follower_Internal_SetMime((CHARACTERGUID)CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9);

PROC
LLMIME_Follower_RegisterMime()
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
ObjectExists(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, 0)
AND
CharacterGetHostCharacter(_Player)
AND
CharacterCreateOutOfSightToObject("LLMIME_Elves_Male_Mime_d8b7efbb-6e60-4700-af8a-4295b0c1ed41", _Player, _Player, 0, "", _Mime)
THEN
LLMIME_Follower_Internal_SetMime(_Mime);
SetOnStage(_Mime, 0);
//END_REGION

//REGION MASK_FIX
IF
GameStarted(_Level,_)
AND
IsGameLevel(_Level, 1)
THEN
LLMIME_Follower_Internal_SetupMask();

IF
StoryEvent(_, "LLMIME_StartSummoningFollower")
THEN
LLMIME_Follower_Internal_SetupMask();

PROC
LLMIME_Follower_Internal_SetupMask()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("Mask_A_NoVisual", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
AND
NOT CharacterGetEquippedItem(_Mime, "Helmet", _Mask)
THEN
CharacterEquipItem(_Mime, _Mask);

PROC
LLMIME_Follower_Internal_SetupMask()
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
DB_LLMIME_Templates("Mask_A_NoVisual", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Mime, _Template, _)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition(_Template, _x, _y, _z, _Mask)
THEN
CharacterEquipItem(_Mime, _Mask);

/*
IF
GameStarted(_,_)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
NOT LLMIME_QRY_HasMaskEquipped(_Mime)
AND
DB_LLMIME_Templates("Mask_A_NoVisual", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
THEN
CharacterEquipItem(_Mime, _Mask);
*/
//END_REGION

//REGION PLAY_RANDOM_ANIM
IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Player)
AND
ObjectGetFlag(_Mime, "LeaderLib_CharacterIsFollowing", 0) // Don't bother if following, since anims won't play
AND
SysCount("DB_LLMIME_Follower_RandomAnimation", 2, _Total)
AND
IntegerSubtract(_Total, 1, _LastIndex)
AND
Random(_LastIndex, _Index)
AND
DB_LLMIME_Follower_RandomAnimation(_Index, _Animation)
THEN
PlayAnimation(_Mime, _Animation);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Played animation [",_Animation,"].");
//END_REGION

//REGION QUERIES
QRY
LLMIME_Follower_QRY_CanSummon((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsRunningAway", 0)
THEN
DB_NOOP(1);

QRY
LLMIME_Follower_QRY_CanSummon((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
NOT DB_LLMIME_Follower_Mime(_)
AND
LLMIME_Follower_QRY_CreateMimeIfNeeded(_Char)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsRunningAway", 0)
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_HasMusicBox((CHARACTERGUID)_Char)
AND
CharacterFindTaggedItem(_Char, "LLMIME_MimeSummonTool", _MusicBox)
THEN
DB_NOOP(1);

QRY
LLMIME_QRY_HasMaskBox((CHARACTERGUID)_Char)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Char, _Template, 0, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);

QRY
LLMIME_Follower_QRY_CanResurrectMime((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Char, _Template, 1, _Count)
AND
_Count > 0
THEN
DB_NOOP(1);
//END_REGION

//REGION MUSICBOX_RESPONSES
IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
CharacterIsInCombat(_Char, 1)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MusicBoxBlockedInCombat");

//Reset dismissal timer
IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 10000);

IF
DB_CustomUseItemResponse(_Char, _Item, 0)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsRunningAway", 0)
THEN
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);
//END_REGION

//REGION MUSICBOX_SUMMONING
IF
CharacterUsedItem(_Char, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
PartyGetFlag(_Char, "Mimicry_PlayedMusicBoxIntro", 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Playing movie 'Mimicry_MusicBoxIntro'.");
DB_LLMIME_Follower_Temp_MusicBoxUser(_Char, _Item);
ProcStartMovie("Mimicry_MusicBoxIntro");
TimerLaunch("Mimicry_Timers_MusicBoxVideoFailsafe", 2000);

IF
MovieFinished("Mimicry_MusicBoxIntro")
AND
DB_LLMIME_Follower_Temp_MusicBoxUser(_Char, _Item)
THEN
TimerCancel("Mimicry_Timers_MusicBoxVideoFailsafe");
LeaderLog_Log("DEBUG", "[LLMIME:Follower:MovieFinished] Movie 'Mimicry_MusicBoxIntro' finished. Starting summoning.");
PartySetFlag(_Char, "Mimicry_PlayedMusicBoxIntro");
NOT DB_LLMIME_Follower_Temp_MusicBoxUser(_Char, _Item);
LLMIME_Follower_StartSummoning(_Char, _Item);

IF
CharacterUsedItem(_Char, _Item)
AND
IsTagged(_Item, "LLMIME_MimeSummonTool", 1)
AND
PartyGetFlag(_Char, "Mimicry_PlayedMusicBoxIntro", 1)
THEN
LLMIME_Follower_StartSummoning(_Char, _Item);

QRY
LLMIME_Follower_QRY_WithinTalkingDistance((CHARACTERGUID)_Char, (CHARACTERGUID)_Mime)
AND
ObjectIsOnStage(_Mime, 1)
AND
LLMIME_QRY_CharacterIsAMime(_Mime) // Initialized
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist <= 30
AND
CharacterCanSee(_Char, _Mime, 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
NOT DB_LLMIME_Follower_Mime(_)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StartSummoning] No registered mime followers. Creating if needed.");
LLMIME_Follower_Internal_CreateMimeIfNeeded(_Char);

PROC
LLMIME_Follower_TeleportIfOffStage((CHARACTERGUID)_Mime, (CHARACTERGUID)_Char)
AND
ObjectIsOnStage(_Mime, 0)
THEN
TeleportTo(_Mime, _Char, "", 0);
SetOnStage(_Mime, 1);

/*
IF
CharacterItemEvent((CHARACTERGUID)_Mime, (ITEMGUID)_Mask, "Mimicry_MaskRefreshed")
*/

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
LLMIME_Follower_QRY_WithinTalkingDistance(_Char, _Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 0)
THEN
LLMIME_Follower_TeleportIfOffStage(_Mime, _Char);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StartSummoning] Mime follower is within range of talking. Running [ProcCharacterMoveToAndTalk].");
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
LLMIME_Follower_QRY_WithinTalkingDistance(_Char, _Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 1)
THEN
LLMIME_Follower_TeleportIfOffStage(_Mime, _Char);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StartSummoning] Mime follower was just resurrected, and is within range of talking.");
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Char, "LLMIME_MimeCompanion_Resurrected", 0, "", 1, 5000.0, 0, 0);

QRY
LLMIME_Follower_QRY_IsInitialized((CHARACTERGUID)_Mime)
AND
ObjectIsOnStage(_Mime, 1)
AND
LLMIME_QRY_CharacterIsAMime(_Mime)
AND
GetVarInteger(_Mime, "LLMIME_Follower_Initialized", 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
NOT LLMIME_Follower_QRY_WithinTalkingDistance(_Char, _Mime)
AND
LLMIME_Follower_QRY_IsInitialized(_Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StartSummoning] Teleporting mime to summoner after timer.");
SetStoryEvent(_Mime, "Mimicry_Events_Follower_SummoningTeleportStarted");
LeaderLib_StartCharacterItemTimer(_Char, _Item, 100, "LLMIME_Timers_FollowerSummonTimer_", "LLMIME_StartSummoningFollower");

PROC
LLMIME_Follower_StartSummoning((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
NOT LLMIME_Follower_QRY_WithinTalkingDistance(_Char, _Mime)
AND
NOT LLMIME_Follower_QRY_IsInitialized(_Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StartSummoning] Mime follower is off stage, or not initialized. Teleporting to summoner directly.");
LLMIME_Follower_Internal_RefreshIfNeeded(_Mime);
LLMIME_Follower_SummonFollower(_Char, _Item);

PROC
LLMIME_Follower_Internal_RefreshIfNeeded((CHARACTERGUID)_Mime)
AND
GetVarInteger(_Mime, "LLMIME_Follower_InitialRefresh", 1)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Mime)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Internal:RefreshIfNeeded] Mime follower does not have the [LLMIME_MIME] tag. Refreshing mask.");
LLMIME_RefreshMask(_Mime, 50);

PROC
LLMIME_Follower_Internal_RefreshIfNeeded((CHARACTERGUID)_Mime)
AND
NOT GetVarInteger(_Mime, "LLMIME_Follower_InitialRefresh", 1)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Internal:RefreshIfNeeded] Mime follower is not initialized. Re-initializing");
SetStoryEvent(_Mime, "Mimicry_Follower_ReInitialize");

IF
CharacterItemEvent(_Char, _Item, "LLMIME_StartSummoningFollower")
THEN
LLMIME_Follower_SummonFollower(_Char, _Item);

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerPause(_TimerName);

IF
DialogStarted("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
AND
DB_CharacterDisappearedOutOfSight(_Mime,_,1)
THEN
DB_StoppedOutOfSight(_Mime);
CharacterPurgeQueue(_Mime);

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
TimerUnpause(_TimerName);
ProcResumeDisappearOutOfSight(_Mime);

PROC
LLMIME_Follower_SummonFollower((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
ItemIsInInventory(_Item, 0)
AND
GetPosition(_Item, _x, _y, _z)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:SummonFollower] Teleporting Mime to summoner's position.");
TeleportToPosition(_Mime, _x, _y, _z, "Mimicry_Events_Follower_OnTeleported", 0);
SetOnStage(_Mime, 1);
CharacterCharacterSetEvent(_Char, _Mime, "Mimicry_MimeSummoned");

PROC
LLMIME_Follower_SummonFollower((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
ItemIsInInventory(_Item, 1)
THEN
TeleportTo(_Mime, _Char, "Mimicry_Events_Follower_OnTeleported", 0);
SetOnStage(_Mime, 1);
CharacterCharacterSetEvent(_Char, _Mime, "Mimicry_MimeSummoned");

PROC
LLMIME_Follower_SummonFollower((CHARACTERGUID)_Char, (ITEMGUID)_Item)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 1)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:SummonFollower] Mime follower is already recruited [LeaderLib_IsFollower].");

IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 0)
THEN
ObjectSetFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Summoner, "LLMIME_MimeCompanion_Default", 0, "", 1, 5000.0, 0, 0);

IF
CharacterCharacterEvent(_Summoner, _Mime, "Mimicry_MimeSummoned")
AND
ObjectGetFlag(_Mime, "Mimicry_WasResurrectedByMask", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
ProcCharacterMoveToAndTalk(_Mime, _Summoner, "LLMIME_MimeCompanion_Resurrected", 0, "", 1, 5000.0, 0, 0);

IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_MimeFollowerJustSummoned", 1)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerJustSummoned");
//END_REGION

//REGION RECRUITMENT_EVENTS
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
GetFaction(_Owner, _Faction)
THEN
ObjectClearFlag(_Mime, "Mimicry_FollowerIsLeaving");
CharacterEnableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 1);
SetFaction(_Mime, _Faction);

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _Stage);
TimerCancel(_TimerName);
//END_REGION

//REGION DISMISSAL
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
CharacterDisableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 0);
SetFaction(_Mime,"Neutral NPC");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsDead(_Mime, 0)
THEN
ObjectSetFlag(_Mime, "Mimicry_FollowerIsLeaving");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerDismissed")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
LLMIME_Follower_StartLeaving(_Mime);

//Summoned, but not recruited
IF
DialogEnded("LLMIME_MimeCompanion_Default", _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 0)
THEN
ObjectSetFlag(_Mime, "Mimicry_FollowerIsLeaving");
LLMIME_Follower_StartLeaving(_Mime);

PROC
LLMIME_Follower_StartLeaving((CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_FollowerIsLeaving", 1)
AND
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _, _)
AND
GetUUID(_Mime, _MimeID)
AND
StringConcatenate("LLMIME_Timers_ResetPositionTimer_", _MimeID, _TimerName)
THEN
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1);
TimerLaunch(_TimerName, 10000);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:ResetPositionTimer] Started position reset timer.");

IF
CharacterDied(_Mime)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _State)
THEN
TimerCancel(_TimerName);
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, _State);
ObjectClearFlag(_Mime, "Mimicry_FollowerIsLeaving");
ObjectClearFlag(_Mime, "Mimicry_FollowerIsRunningAway");

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1)
AND
CharacterIsDead(_Mime, 0)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:ResetPositionTimer] Started second phase of position reset timer.");
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 1);
//ApplyStatus(_Mime, "INVISIBLE", 3.0, 1);
TimerLaunch(_TimerName, 1000);
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
AND
NOT LLMIME_Follower_QRY_NoLastOwnerSet(_Mime)
AND
GetVarObject(_Mime, "LeaderLib_Follower_LastOwner", _Owner)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:ResetPositionTimer] Running away from owner player.");
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
ObjectSetFlag(_Mime, "Mimicry_FollowerIsRunningAway");
ProcCharacterDisappearOutOfSightToObject(_Mime, _Owner, 1, "LLMIME_Events_MimeFollowerDisappeared", 1);

QRY
LLMIME_Follower_QRY_NoLastOwnerSet((CHARACTERGUID)_Mime)
AND
NOT GetVarObject(_Mime, "LeaderLib_Follower_LastOwner", _)
THEN
DB_NOOP(1);

QRY
LLMIME_Follower_QRY_NoLastOwnerSet((CHARACTERGUID)_Mime)
AND
GetVarObject(_Mime, "LeaderLib_Follower_LastOwner", NULL_00000000-0000-0000-0000-000000000000)
THEN
DB_NOOP(1);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
AND
LLMIME_Follower_QRY_NoLastOwnerSet(_Mime)
AND
GetClosestAlivePlayer(_Mime, _Player, _Dist)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:ResetPositionTimer] Running away from nearest player.");
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
ObjectSetFlag(_Mime, "Mimicry_FollowerIsRunningAway");
ProcCharacterDisappearOutOfSightToObject(_Mime, _Player, 1, "LLMIME_Events_MimeFollowerDisappeared", 1);
//CharacterDisappearOutOfSightToObject(_Mime, _Player, 1, "LLMIME_Events_MimeFollowerDisappeared", 1);

//Nothing to run away from
IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
SetStoryEvent(_Mime, "LLMIME_Events_MimeFollowerDisappeared");

IF
StoryEvent((CHARACTERGUID)_Mime, "LLMIME_Events_MimeFollowerDisappeared")
THEN
//LeaderLib_Follower_ResetPosition(_Mime);
//LeaderLog_Log("DEBUG", "[LLMIME:Follower] Reset follower's position to the recruit position.");
Poof(_Mime);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Poofed the Mime follower.");
ObjectClearFlag(_Mime, "Mimicry_FollowerIsLeaving");
ObjectClearFlag(_Mime, "Mimicry_FollowerIsRunningAway");

/*
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_Events_MimeFollowerPositionReset")
THEN
SetOnStage(_Mime, 1);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Set Mime follower on stage.");
*/

//No last owner found
IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2)
THEN
NOT DB_LLMIME_Follower_ResetPositionTimer(_Mime, _TimerName, 2);
//LeaderLib_Follower_ResetPosition(_Mime, "RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01");
//LeaderLog_Log("DEBUG", "[LLMIME:Follower] Reset follower's position to the recruit position.");
Poof(_Mime);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Poofed the Mime follower.");
//END_REGION

//REGION NON_RECRUITED_COMBAT
IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
THEN
CharacterEnableAllCrimes(_Mime);
SetCanJoinCombat(_Mime, 1);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
Integer(_Percentage, _PercInt)
AND
IntegertoString(_PercInt, _HpStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Debug] Mime follower's health is: [",_HpStr,"%]");

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
_Percentage <= 75.0
THEN
CharacterAddAttitudeTowardsPlayer(_Mime, _Owner, -5);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsPlayer(_Owner, 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
CharacterGetHitpointsPercentage(_Mime, _Percentage)
AND
_Percentage <= 40.0
THEN
EnterCombat(_Mime, _Owner);

IF
AttackedByObject((CHARACTERGUID)_Mime, (CHARACTERGUID)_Owner, _Attacker, _, _)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterIsInCombat(_Mime, 0)
AND
CharacterIsPlayer(_Owner, 0)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
THEN
ApplyStatus(_Mime, "INVISIBLE", 90.0, 1);

IF
ObjectEnteredCombat((CHARACTERGUID)_Mime, _CombatID)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
AND
NOT LLMIME_Brawler_QRY_StanceIsActive(_Mime)
THEN
CharacterUseSkill(_Mime, "Shout_LLMIME_BrawlerStance", _Mime, 1, 1);
//END_REGION

//REGION RETURN_SKILL
QRY
LLMIME_Follower_QRY_CanReturnToOwner((CHARACTERGUID)_Char, (CHARACTERGUID)_Mime)
AND
CharacterIsInCombat(_Mime, 0)
AND
CharacterCanSee(_Char, _Mime, 0)
THEN
DB_NOOP(1);

QRY
LLMIME_Follower_QRY_CanReturnToOwner((CHARACTERGUID)_Char, (CHARACTERGUID)_Mime)
AND
CharacterIsInCombat(_Mime, 0)
AND
GetDistanceTo(_Char, _Mime, _Dist)
AND
_Dist >= 30
THEN
DB_NOOP(1);

PROC
LLMIME_Follower_ReturnIfNotInCombat((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
LLMIME_Follower_QRY_CanReturnToOwner(_Char, _Mime)
THEN
CharacterStatusText(_Char, "LLMIME_StatusText_MimeFollowerRecalled");
CharacterUseSkill(_Mime, "Shout_LLMIME_ReturnToOwner", _Char, 1, 1);

PROC
LLMIME_Follower_ReturnIfNotInCombat((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char, 0)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
NOT LLMIME_Follower_QRY_CanReturnToOwner(_Char, _Mime)
THEN
DisplayText(_Mime, "LLMIME_StatusText_MimeFollowerRecallFailed");

IF
CharacterTeleportToFleeWaypoint(_Owner, _)
AND
DB_LeaderLib_PartyFollower(_Owner, _Follower)
AND
IsTagged(_Follower, "LLMIME_MimeFollower", 1)
THEN
LLMIME_Mimicking_RemoveMimicking(_Follower);
//ApplyStatus(_Follower, "INVISIBLE", 9.0, 1);
//CharacterUseSkill(_Follower, "Shout_LLMIME_ReturnToOwner", _Follower, 1, 1);
TeleportTo(_Follower, _Owner, "Mimicry_Events_Follower_ReturnToOwner_Teleported", 1, 0);
LeaveCombat(_Follower);

IF
CharacterTeleportToWaypoint(_Owner, _)
AND
DB_LeaderLib_PartyFollower(_Owner, _Follower)
AND
IsTagged(_Follower, "LLMIME_MimeFollower", 1)
THEN
TeleportTo(_Follower, _Owner, "", 1, 0);

IF
SkillCast(_Follower, "Shout_LLMIME_ReturnToOwner", _, _)
THEN
LeaderLib_StartObjectTimer(_Follower, 250, "Mimicry_Timers_ReturnToOwner_ReturnDelay_", "Mimicry_Events_Follower_ReturnToOwner_StartTeleport");

IF
StoryEvent((CHARACTERGUID)_Follower, "Mimicry_Events_Follower_ReturnToOwner_StartTeleport")
AND
DB_LeaderLib_PartyFollower(_Owner, _Follower)
THEN
TeleportTo(_Follower, _Owner, "Mimicry_Events_Follower_ReturnToOwner_Teleported", 1);

IF
StoryEvent((CHARACTERGUID)_Follower, "Mimicry_Events_Follower_ReturnToOwner_Teleported")
THEN
LeaderLib_StartObjectTimer(_Follower, 500, "Mimicry_Timers_ReturnToOwner_ReturnEffectDelay_", "Mimicry_Events_Follower_OnTeleported");
//END_REGION

//REGION LEVEL_UP
/*
IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_Events_FollowerRecruited")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
IntegertoString(_Level, _LevelStr)
AND
IntegertoString(_MimeLevel, _MimeLevelStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Follower:FollowerRecruited_Debug] Level: Mime[",_MimeLevelStr,"] Owner[",_LevelStr,"]");
*/

IF
CharacterCharacterEvent((CHARACTERGUID)_Summoner, (CHARACTERGUID)_Mime, "Mimicry_MimeSummoned")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Summoner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
_MimeLevel < _Level
THEN
CharacterLevelUpTo(_Mime, _Level);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Mimicry_MimeSummoned] Mime follower was summoned. Leveling up.");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "LeaderLib_PlayerIsRecruitingCharacter")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
_MimeLevel < _Level
THEN
CharacterLevelUpTo(_Mime, _Level);
LeaderLog_Log("DEBUG", "[LLMIME:Follower:FollowerRecruited] Mime joined party and is a lower level than the recruiting character. Leveling up.");

IF
CharacterLeveledUp(_Owner)
AND
DB_LeaderLib_PartyFollower(_Owner, _Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
CharacterGetLevel(_Owner, _Level)
AND
CharacterGetLevel(_Mime, _MimeLevel)
AND
_MimeLevel < _Level
THEN
CharacterRemoveFromPlayerCharacter(_Mime, _Owner);
LeaderLib_StartCharacterCharacterTimer(_Owner, _Mime, 100, "Mimicry_Timers_Follower_LevelUpDelay_", "Mimicry_Follower_LevelUpAndAttachFollower");

IF
CharacterCharacterEvent((CHARACTERGUID)_Owner, (CHARACTERGUID)_Mime, "Mimicry_Follower_LevelUpAndAttachFollower")
AND
CharacterGetLevel(_Owner, _Level)
THEN
CharacterLevelUpTo(_Mime, _Level);
CharacterAddToPlayerCharacter(_Mime, _Owner);
//END_REGION

//REGION SOURCE_MIMICKING_UNLOCK
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_MimeInitialized")
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
LLMIME_Follower_Internal_AddSourceMimicking(_Mime);

IF
CharacterLeveledUp(_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
THEN
LLMIME_Follower_Internal_AddSourceMimicking(_Mime);

PROC
LLMIME_Follower_Internal_AddSourceMimicking((CHARACTERGUID)_Mime)
AND
CharacterGetLevel(_Mime, _Level)
AND
_Level >= 10
AND
CharacterHasSkill(_Mime, "Shout_LLMIME_SourceMimicking_Follower", 0)
THEN
CharacterAddSkill(_Mime, "Shout_LLMIME_SourceMimicking_Follower");

IF
CharacterJoinedParty(_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
NOT DB_CurrentGameMode("Campaign")
AND
DB_LeaderLib_PartyFollower(_Owner, _Mime)
AND
CharacterGetMaxSourcePoints(_Owner, _Amount)
THEN
CharacterOverrideMaxSourcePoints(_Mime, _Amount);
//END_REGION

//REGION GHOST_SPAWNING_START
IF
CharacterDied(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
LLMIME_Follower_Internal_StartSpawningGhost(_Mime);

IF
CharacterDied(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 1)
THEN
CharacterSetCorpseLootable(_Mime, 0);

/*
IF
CharacterDied(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 0)
THEN
CharacterSetCorpseLootable(_Mime, 1);
*/

/*
IF
ObjectLostTag((CHARACTERGUID)_Mime, "LeaderLib_IsFollower")
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
CharacterSetCorpseLootable(_Mime, 1);
*/

IF
CombatEnded(_CombatID)
AND
DB_LLMIME_Follower_Temp_SpawnedGhostAfterCombat(_CombatID, _Mime)
AND
CharacterIsDead(_Mime, 1)
THEN
LLMIME_Follower_Internal_StartSpawningGhost(_Mime);

IF
CombatEnded(_CombatID)
AND
DB_LLMIME_Follower_Temp_SpawnedGhostAfterCombat(_CombatID, _Mime)
THEN
NOT DB_LLMIME_Follower_Temp_SpawnedGhostAfterCombat(_CombatID, _Mime);
//END_REGION

//REGION GHOST_SPAWN
QRY
LLMIME_Follower_QRY_GhostExists()
AND
DB_LLMIME_FollowerGhost(_Ghost)
AND
ObjectExists(_Ghost, 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Follower_Internal_StartSpawningGhost((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime)
AND
NOT GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
AND
NOT IsTagged(_Mime, "LeaderLib_IsFollower", 1)
THEN
LLMIME_Follower_SpawnGhost(_Mime);
DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime);

PROC
LLMIME_Follower_Internal_StartSpawningGhost((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime)
AND
NOT GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 1)
AND
DB_LeaderLib_PartyFollower(_Owner, _Mime)
AND
CharacterIsInCombat(_Owner, 0)
THEN
LLMIME_Follower_SpawnGhost(_Mime);
DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime);

PROC
LLMIME_Follower_Internal_StartSpawningGhost((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime)
AND
NOT GlobalGetFlag("Mimicry_MusicBoxShattered", 1)
AND
IsTagged(_Mime, "LeaderLib_IsFollower", 1)
AND
DB_LeaderLib_PartyFollower(_Owner, _Mime)
AND
CharacterIsInCombat(_Owner, 1)
AND
CombatGetIDForCharacter(_Owner, _CombatID)
THEN
DB_LLMIME_Follower_Temp_SpawnedGhostAfterCombat(_CombatID, _Mime);

PROC
LLMIME_Follower_Internal_StartSpawningGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime)
THEN
NOT DB_LLMIME_Follower_Temp_SpawnedGhost(_Mime);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_FollowerGhost(_Ghost)
AND
ObjectExists((CHARACTERGUID)_Ghost, 0)
THEN
NOT DB_LLMIME_FollowerGhost(_Ghost);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
DB_LLMIME_FollowerGhost(_Ghost)
AND
ObjectExists((CHARACTERGUID)_Ghost, 1)
AND
CharacterIsDead(_Ghost, 0)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
SetStoryEvent(_Ghost, "Mimicry_Events_MimeFollower_InitGhost");
TeleportToPosition(_Ghost, _x, _y, _z, "", 0);
LLMIME_Follower_Internal_OnGhostAppeared(_Ghost);

PROC
LLMIME_Follower_SpawnGhost((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_FollowerGhost(_)
AND
GetPosition(_Mime, _x, _y, _z)
AND
DB_LLMIME_Templates("MimeFollower_Ghost", _Template)
AND
NOT DB_LLMIME_FollowerGhost(_) // Safety
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, _Template, 0, _Ghost)
THEN
DB_LLMIME_FollowerGhost(_Ghost);
LLMIME_Follower_Internal_OnGhostAppeared(_Ghost);

PROC
LLMIME_Follower_Internal_OnGhostAppeared((CHARACTERGUID)_Ghost)
THEN
DB_Dialogs(_Ghost, "LLMIME_MimeCompanion_Ghost");
//SetInvulnerable_UseProcSetInvulnerable(_Ghost,1);
ObjectSetFlag(_Ghost, "GLO_Ghost_Visible");
SetStoryEvent(_Ghost, "Mimicry_Events_MimeFollower_GhostAppeared");

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_FollowerGhost(_Ghost)
THEN
SetStoryEvent(_Ghost, "Mimicry_Events_MimeFollower_Resurrected");

IF
RegionEnded(_)
AND
DB_LLMIME_FollowerGhost(_Ghost)
THEN
SetOnStage(_Ghost, 0);
NOT DB_LLMIME_FollowerGhost(_Ghost);

IF
CharacterDied(_Ghost)
AND
DB_LLMIME_FollowerGhost(_Ghost)
THEN
NOT DB_LLMIME_FollowerGhost(_Ghost);
LeaderLog_Log("DEBUG", "[LLMIME:Follower] Mime ghost died.");
//END_REGION

//REGION GHOST_FOLLOWER_DEATH
IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Ghost)
AND
GetPosition(_Ghost, _x, _y, _z)
THEN
LLMIME_Follower_Internal_KillGhost(_Ghost);

IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
CharacterCharacterSetEvent(_Mime, _Player, "Mimicry_Events_Follower_OnMaskStolen");
LeaderLib_Follower_DismissCharacter(_Mime, "", 0);
SetTag(_Mime, "BLOCK_RESURRECTION");

IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition("CONT_Humans_Backpack_Merchant_A_360e3e11-c7f8-4281-848a-596e37df884b", _x, _y, _z, _Backpack)
THEN
LeaderLib_StartCharacterCharacterTimer(_Mime, _Player, 750, "LLMIME_Timers_TreasureEffectsTimer", "Mimicry_Events_MimeFollowerLootSpawned");
ObjectSetFlag(_Mime, "Mimicry_MimeFollowerMaskTaken", 0);
SetTag(_Mime, "LLMIME_MimeFollower_TagTreasure");
GenerateTreasure(_Backpack, "LLMIME_MimeFollower_Treasure", -1, _Player);
MoveAllItemsTo(_Backpack, _Mime, 1, 1, 1);
ItemRemove(_Backpack);
LeaderLib_StartObjectTimer(_Mime, 500, "LLMIME_Timers_StopTaggingMimeFollowerTreasure_", "LLMIME_Events_StopTaggingMimeFollowerTreasure");
LeaderLog_Log("DEBUG", "[LLMIME:Follower:Mimicry_MaskTakenFromFollower] Generated mime follower treasure and moved to corpse.");

IF
CharacterCharacterEvent(_Mime, _Player, "Mimicry_Events_MimeFollowerLootSpawned")
THEN
CharacterSetCorpseLootable(_Mime, 1);

IF
ItemAddedToCharacter(_Item, _Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower_TagTreasure", 1)
THEN
SetTag(_Item, "LLMIME_MimeFollowerTreasure");
LeaderLog_Log("DEBUG", "[LLMIME:Follower:ItemAddedToCharacter] Tagged mime follower treasure.");

IF
StoryEvent((CHARACTERGUID)_Mime, "LLMIME_Events_StopTaggingMimeFollowerTreasure")
THEN
ClearTag(_Mime, "LLMIME_MimeFollower_TagTreasure");
LeaderLog_Log("DEBUG", "[LLMIME:Follower:StopTaggingMimeFollowerTreasure] Stopped tagging generated treasure.");

//For testing (since the Mime should be dead normally)
IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
CharacterIsDead(_Mime, 0)
THEN
CharacterDie(_Mime, 1, "KnockedDown", _Player);

IF
ObjectFlagSet("Mimicry_MaskTakenFromFollower", (CHARACTERGUID)_Player, _Instance)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Templates("Mask_A", _Template)
THEN
ItemTemplateAddTo(_Template, _Player, 1, 1);

PROC
LLMIME_Follower_Internal_KillGhost((CHARACTERGUID)_Ghost)
AND
GetPosition(_Ghost, _x, _y, _z)
THEN
NOT DB_LLMIME_FollowerGhost(_Ghost);
SetStoryEvent(_Ghost, "Mimicry_Events_Follower_OnGhostKilled");
CharacterSetAnimationOverride(_Ghost, "");
PlayAnimation(_Ghost, "deathfigure_end", "LLMIME_Events_Follower_KillGhost");

/*
IF
StoryEvent((CHARACTERGUID)_Ghost, "LLMIME_Events_Follower_KillGhost")
AND
DB_LLMIME_Follower_Temp_GhostEffect(_Ghost, _Handle)
THEN
StopLoopEffect(_Handle);
NOT DB_LLMIME_Follower_Temp_GhostEffect(_Ghost, _Handle);
*/

IF
StoryEvent((CHARACTERGUID)_Ghost, "LLMIME_Events_Follower_KillGhost")
THEN
CharacterSetImmortal(_Ghost,0);
SetInvulnerable_UseProcSetInvulnerable(_Ghost,0);
SetOnStage(_Ghost, 0);
CharacterDie(_Ghost, 0, "None");
//END_REGION

//REGION RESURRECTION_EVENT
IF
DialogEnded("LLMIME_MusicBox_Resurrection", _Instance)
AND
GlobalGetFlag("Mimicry_EventFlag_ReviveMimeFollower", 1)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (ITEMGUID)_MusicBox)
THEN
LLMIME_Follower_BeginResurrection(_Player, _MusicBox);
LLMIME_Items_TransformMusicBox(_Player, _MusicBox);
GlobalClearFlag("Mimicry_EventFlag_ReviveMimeFollower");

PROC
LLMIME_Follower_BeginResurrection((CHARACTERGUID)_Player, (ITEMGUID)_MusicBox)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
SetStoryEvent(_Mime, "Mimicry_Events_Follower_OnResurrected");
ClearTag(_Mime, "BLOCK_RESURRECTION");
ObjectSetFlag(_Player, "Mimicry_PlayerRevivedMimeFollower");
ObjectClearFlag(_Mime, "Mimicry_MimeFollowerMaskTaken");
SetOnStage(_Mime, 1);
CharacterResurrectCustom(_Mime, "resurrect");
ObjectSetFlag(_Mime, "Mimicry_WasResurrectedByMask");
LeaderLib_StartCharacterItemTimer(_Player, _MusicBox, 2500, "LLMIME_Timers_FollowerrTimer_", "Mimicry_MimeFollowerResurrected");
DB_LLMIME_Follower_RevivedBy(_Player);

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
THEN
SetStoryEvent(_Mime, "Mimicry_Events_OnMimeFollowerRevived");

IF
ObjectFlagSet("Mimicry_ResurrectionEvent_RemoveAndDestroyMask", (CHARACTERGUID)_Player, _Instance)
AND
NOT DB_LLMIME_Follower_Temp_RemovedMask(_Player)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
ItemTemplateIsInUserInventory(_Player, _Template, 1, _Count)
AND
_Count > 0
AND
GetItemForItemTemplateInInventory(_Player, _Template, _Mask)
THEN
CharacterUnequipItem(_Player, _Mask);
ItemRemove(_Mask);
DB_LLMIME_Follower_Temp_RemovedMask(_Player);

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_RevivedBy(_Player)
AND
DB_LLMIME_Templates("MimeFollower", _MimeTemplate)
THEN
//CharacterTransform(_Mime, _MimeTemplate, 0, 0, 0, 0, 0, 0, 0);
ChangeAttitude(_Mime, _Player, 0);

IF
CharacterResurrected(_Mime)
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
DB_LLMIME_Follower_RevivedBy(_Player)
THEN
NOT DB_LLMIME_Follower_RevivedBy(_Player);
NOT DB_LLMIME_Follower_Temp_RemovedMask(_Player);

IF
CharacterItemEvent(_Player, _MusicBox, "Mimicry_MimeFollowerResurrected")
THEN
LLMIME_Follower_StartSummoning(_Player, _MusicBox);

IF
DialogEnded("LLMIME_MimeCompanion_Resurrected", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_Instance, 1, (CHARACTERGUID)_Mime)
THEN
Proc_StartDialog(0, "LLMIME_MimeCompanion_Default", _Mime, _Player);
ObjectClearFlag(_Player, "Mimicry_MaskTakenFromFollower");
//END_REGION

//REGION TREASURE_REMOVAL
IF
StoryEvent((ITEMGUID)_Item, "LLMIME_Events_OnMimeFollowerTreasureRemoved")
AND
NOT ItemGetOwner(_Item, _)
AND
ItemIsInInventory(_Item, 0)
THEN
DisplayText(_Item, "LLMIME_StatusText_MimeFollowerTreasureRemoved");

IF
StoryEvent((ITEMGUID)_Item, "LLMIME_Events_OnMimeFollowerTreasureRemoved")
AND
ItemGetOwner(_Item, _Player)
AND
CharacterIsPlayer(_Player, 1)
AND
NOT DB_LLMIME_Follower_Temp_RemoveMessageDisplayed(_Player)
THEN
CharacterStatusText(_Player, "LLMIME_StatusText_MimeFollowerTreasureRemoved");
LeaderLog_SetOneshotTarget(_Player);
LeaderLog_Log("COMBAT", "LLMIME_StatusText_MimeFollowerTreasureRemoved");
DB_LLMIME_Follower_Temp_RemoveMessageDisplayed(_Player);
LeaderLib_StartObjectTimer(_Player, 1000, "LLMIME_Timers_ResetFollowerMessageRemovedDB_", "LLMIME_Events_Internal_RemoveMessageDisplayed");

IF
StoryEvent((CHARACTERGUID)_Player, "LLMIME_Events_Internal_RemoveMessageDisplayed")
AND
DB_LLMIME_Follower_Temp_RemoveMessageDisplayed(_Player)
THEN
NOT DB_LLMIME_Follower_Temp_RemoveMessageDisplayed(_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
