Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_WaypointInfo((ITEMGUID)S_LLMIME_TestLevelWaypointShrine_9955a6a5-c7ca-40aa-8fa8-9c3699d65032,(TRIGGERGUID)S_LLMIME_Debug_Waypoint_e81af9c7-8f99-4050-8ab8-1101954a97af,"WAYP_LLMIME_TestLevel");

Proc_LLMIME_IterateGameAlreadyStarted();
KBSECTION
IF
GameStarted("TestLevel_LL_Mimicry",1)
THEN
ProcDoWaypointUnlock(ITEMGUID_S_LLMIME_TestLevelWaypointShrine_9955a6a5-c7ca-40aa-8fa8-9c3699d65032,NULL_00000000-0000-0000-0000-000000000000);
DB_LLMIME_Debug_GameStarted(1);
TimerLaunch("LLMIME_Debug_Start", 100);
TimerLaunch("LLMIME_Debug_Test", 5000);

IF
TimerFinished("LLMIME_Debug_Start")
THEN
LeaderLib_Math_Sine(131.0);
LeaderLib_Math_Cosine(131.0);

IF
TimerFinished("LLMIME_Debug_Test")
AND
DB_IsPlayer(_Player)
THEN
LLMIME_Debug_TracePartyCount(_Player);

IF
CharacterJoinedParty(_)
AND
DB_IsPlayer(_Player)
THEN
LLMIME_Debug_TracePartyCount(_Player);

IF
CharacterLeftParty(_)
AND
DB_IsPlayer(_Player)
THEN
LLMIME_Debug_TracePartyCount(_Player);

PROC
LLMIME_Debug_TracePartyCount((CHARACTERGUID)_Player)
AND
GetVarInteger(_Player, "LeaderLib_PartyCount", _Count)
AND
IntegertoString(_Count, _CountStr)
AND
CharacterGetDisplayName(_Player, _, _Name)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:PartyCount] [",_Name,"] party count is [",_CountStr,"].");


IF
DB_LeaderLib_Math_Sine(131.0, _Result)
AND
CharacterGetHostCharacter(_Char)
THEN
LeaderLog_RealToString(_Char, _Result, "LLMIME_Debug_SineResultConverted");

IF
StoryEvent(_Char, "LLMIME_Debug_SineResultConverted")
AND
GetVarString(_Char, "LeaderLog_RealString", _RealStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:Math] Sine | Angle [131] = [",_RealStr,"]");

IF
StoryEvent((CHARACTERGUID)_Char, "LLMIME_Debug_SineResultConverted")
AND
DB_LeaderLib_Math_Cosine(131.0, _Result)
THEN
LeaderLog_RealToString(_Char, _Result, "LLMIME_Debug_CosineResultConverted");

IF
StoryEvent(_Char, "LLMIME_Debug_CosineResultConverted")
AND
GetVarString(_Char, "LeaderLog_RealString", _RealStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:Math] Cosine | Angle [131] = [",_RealStr,"]");

/*
IF
RegionEnded("TestLevel_LL_Mimicry")
AND
DB_IsPlayer(_Player)
THEN
LLMIME_RemoveAllModStatuses(_Player);
*/

IF
RegionEnded("TestLevel_LL_Mimicry")
THEN
NOT DB_LLMIME_Debug_GameStarted(1);
NOT DB_LLMIME_Debug_IteratingPlayers(1);
GoalCompleted;
SysActivateGoal("LLMIME_8_0_DebugLevel");

IF
TimerFinished("LLMIME_Debug_Start")
AND
NOT DB_LLMIME_Debug_IteratingPlayers(_)
THEN
DB_LLMIME_Debug_IteratingPlayers(1);
CharacterLaunchIterator("LLMIME_Events_Debug_IterateCharacters");

IF
StoryEvent((CHARACTERGUID)_Character, "LLMIME_Events_Debug_IterateCharacters")
AND
IsTagged(_Character, "COMPANION", 1)
THEN
CharacterSetHitpointsPercentage(_Character, 50);
DB_Dialogs(_Character, "LLMIME_Debug_RecruitCharacter");

IF
StoryEvent((CHARACTERGUID)_Player, "LLMIME_Events_Debug_IterateCharacters")
AND
IsTagged(_Player, "COMPANION", 0)
AND
CharacterIsPlayer(_Player, 1)
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LOOT_BackPack_A_6c70c298-aa29-418f-a659-f8e0b5f5fa60", _x, _y, _z, _Backpack)
THEN
SetTag(_Player, "AVATAR");
SetTag(_Backpack, "LLMIME_DEBUG_DELETEALL");
MoveAllItemsTo(_Player, _Backpack, 1, 1, 1);
//SetStoryEvent(_Player, "Mimicry_InitMimeJob");
//CharacterTransformAppearanceToWithEquipmentSet(_Player, _Player, "LLMIME_Class_Mime_Start", 0);
DB_LLMIME_Debug_HostPlayer(_Player);
DB_LLMIME_Debug_DeleteBackpack(_Backpack);

IF
ObjectFlagSet("LLMIME_IsCompanion", (CHARACTERGUID)_Character, _Instance)
THEN
CharacterMakePlayer(_Character);
CharacterStopFollow(_Character);
CharacterDetachFromGroup(_Character);
DB_IsPlayer(_Character);

IF
ObjectFlagCleared("LLMIME_IsCompanion", (CHARACTERGUID)_Character, _Instance)
THEN
CharacterDetachFromGroup(_Character);
CharacterMakeNPC(_Character);
NOT DB_IsPlayer(_Character);

/*
IF
StoryEvent((CHARACTERGUID)_Character, "LLMIME_Events_Debug_IterateCharacters")
AND
IsTagged(_Character, "COMPANION", 1)
AND
CharacterGetHostCharacter(_Player)
AND
_Character != _Player
AND
IsTagged(_Player, "COMPANION", 0)
THEN
CharacterRecruitCharacter(_Character, _Player);
CharacterStopFollow(_Character);
*/

IF
StoryEvent(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, "LLMIME_Events_Debug_IterateCharacters")
THEN
SetTag(CHARACTERGUID_LLMIME_MimeCompanion_ElfMale_3f625a37-59d0-41b5-81df-695abd2975e9, "LeaderLib_Follower_DisableAutoResetPosition");

IF
StoryEvent((CHARACTERGUID)_Character, "LLMIME_Events_Debug_IterateCharacters")
AND
CharacterGetHostCharacter(_Host)
AND
CharacterIsEnemy(_Character, _Host, 0)
THEN
CharacterEnableWaypointUsage(_Character, 1);
SetHomesteadKeyState(1, _Character);
CharacterAddAttribute(_Character, "Finesse", 1);
CharacterAddAttribute(_Character, "Strength", 1);
CharacterAddAttribute(_Character, "Wits", 1);
CharacterAddAttribute(_Character, "Intelligence", 1);
CharacterAddAttribute(_Character, "Constitution", 1);
//CharacterLevelUpTo(_Character, 10);
CharacterOverrideMaxSourcePoints(_Character, 10);
CharacterAddSourcePoints(_Character, 10);

IF
StoryEvent((CHARACTERGUID)_Character, "LLMIME_Events_Debug_IterateCharacters")
THEN
CharacterAddSkill(_Character, "Target_LLMIME_Wish", 0);

/*
IF
StoryEvent((CHARACTERGUID)_Character, "LLMIME_Events_Debug_IterateCharacters")
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
NOT GetItemForItemTemplateInInventory(_Character, _Template, _)
THEN
ItemTemplateAddTo(_Template, _Character, 1);
CharacterAddSkill(_Character, "Target_LLMIME_Wish", 0);
*/

/*
IF
StoryEvent(CHARACTERGUID_LLMIME_Debug_PartyMember3_9c02760e-befa-4026-a214-ecbeaa7de64a, "LLMIME_Events_Debug_IterateCharacters")
THEN
SetStoryEvent(CHARACTERGUID_LLMIME_Debug_PartyMember3_9c02760e-befa-4026-a214-ecbeaa7de64a, "Mimicry_InitMimeJob");
*/

IF
StoryEvent(NULL_00000000-0000-0000-0000-000000000000, "LLMIME_Events_Debug_IterateCharacters")
THEN
NOT DB_LLMIME_Debug_IteratingPlayers(1);
//TimerLaunch("LLMINE_Events_UnequipDummy", 300);
TimerLaunch("LLMINE_Events_Debug_DeleteBackpacks", 500);
IterateParties("LLMIME_Events_Debug_AddPlayers");

IF
TimerFinished("LLMINE_Events_UnequipDummy")
AND
DB_LLMIME_Debug_HostPlayer(_Player)
THEN
LLMIME_Debug_UnequipInSlot(_Player, "Weapon");
LLMIME_Debug_UnequipInSlot(_Player, "Helmet");

PROC
LLMIME_Debug_UnequipInSlot((CHARACTERGUID)_Player, (STRING)_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
AND
DB_LLMIME_Debug_DeleteBackpack(_Backpack)
THEN
CharacterUnequipItem(_Player, _Item);
ItemToInventory(_Item, _Backpack);

IF
TimerFinished("LLMINE_Events_UnequipDummy")
THEN
TimerLaunch("LLMINE_Events_Debug_DeleteBackpacks", 500);

IF
ItemAddedToContainer(_Item, _Backpack)
AND
IsTagged(_Backpack, "LLMIME_DEBUG_DELETEALL", 1)
THEN
ItemDestroy(_Item);

IF
TimerFinished("LLMINE_Events_Debug_DeleteBackpacks")
AND
DB_LLMIME_Debug_DeleteBackpack(_Backpack)
THEN
ItemDestroy(_Backpack);

PROC
Proc_LLMIME_IterateGameAlreadyStarted()
AND
DB_LLMIME_Debug_GameStarted(1)
THEN
TimerLaunch("LLMIME_Debug_Start", 100);

IF
StoryEvent((CHARACTERGUID)_Player, "LLMIME_Events_Debug_AddPlayers")
AND
NOT DB_IsPlayer(_Player)
THEN
DB_IsPlayer(_Player);

IF
CharacterUsedSkill(_Character,_,_)
THEN
CharacterResetCooldowns(_Character);

IF
CharacterUsedItem(_Character, ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
AND
NOT DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
AND
GlobalGetFlag("Mimicry_Mimic_CombatOnlyDisabled", 0)
THEN
GlobalSetFlag("Mimicry_Mimic_CombatOnlyDisabled");
DisplayText(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83, "Mimic Enabled Outside of Combat");
DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83);

IF
CharacterUsedItem(_Character, ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
AND
NOT DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
AND
GlobalGetFlag("Mimicry_Mimic_CombatOnlyDisabled", 1)
THEN
GlobalClearFlag("Mimicry_Mimic_CombatOnlyDisabled");
DisplayText(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83, "Mimic Disabled Outside of Combat");
DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83);

IF
CharacterUsedItem(_Character, ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
AND
DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83)
THEN
NOT DB_LLMIME_Debug_LeverToggle(ITEMGUID_LLMIME_TestLevel_Lever_ToggleGlobalFlags_32ee1fc8-3a1f-4293-85eb-45c59dc25d83);

//REGION TEXT_EVENTS
/*
IF
TextEventSet("mimicdummy")
AND
DB_LLMIME_MimicNextAction(_Mime)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LLMIME_TargetDummy_7384531c-c7ba-45df-ad84-909fa0208047", _x, _y, _z, _Dummy)
//CreateItemTemplateAtPosition("LLMIME_CONT_Barrel_A_Water_a2573a2d-5556-4c82-b004-aa7d5130b1e6", _x, _y, _z, _Dummy)
THEN
DebugBreak("[LLMIME:Position] Spawned skill dummy.");
*/

IF
TextEventSet("mimicdupe")
AND
GlobalGetFlag("LLMIME_DUPLICATE_ITEMS", _Val)
AND
IntegerSubtract(1, _Val, _NextVal)
THEN
LLMIME_Debug_SetGlobalFlag("LLMIME_DUPLICATE_ITEMS", _NextVal);

PROC
LLMIME_Debug_SetGlobalFlag((STRING)_Flag, 0)
AND
StringConcatenate(_Flag, " cleared.", _Message)
THEN
DebugBreak(_Message);
GlobalClearFlag(_Flag);

PROC
LLMIME_Debug_SetGlobalFlag((STRING)_Flag, 1)
AND
StringConcatenate(_Flag, " set.", _Message)
THEN
DebugBreak(_Message);
GlobalSetFlag(_Flag);
//END_REGION

IF
DB_LLMIME_Debug_HostPlayer(_Player)
THEN
LeaveCombat(_Player);

IF
CharacterTeleportToFleeWaypoint(_Player, _)
AND
DB_LeaderLib_Combat_Objects(_CombatID, _Player)
AND
DB_LeaderLib_Combat_Objects(_CombatID, _Object)
AND
CharacterIsEnemy((CHARACTERGUID)_Object, _Player, 1)
THEN
LeaveCombat(_Object);

IF
CharacterTeleportToFleeWaypoint(_Player, _)
THEN
LeaveCombat(_Player);

//REGION TRACING
IF
AttackedByObject(_Target, _Owner, _Attacker, _DamageType, _DamageSource)
AND
GetTemplate(_Attacker, _AttackerID)
AND
GetTemplate(_Owner, _OwnerID)
AND
GetTemplate(_Target, _TargetID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:AttackedByObject] [",_AttackerID,"](",_OwnerID,") attacked [",_TargetID,"]. DamageType[",_DamageType,"] DamageSource[",_DamageSource,"].");

IF
CharacterReceivedDamage(_Target, _Percentage, _Attacker)
AND
GetTemplate(_Attacker, _AttackerID)
AND
GetTemplate(_Target, _TargetID)
AND
IntegertoString(_Percentage, _DamageStr)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterReceivedDamage] [",_AttackerID,"] did [",_DamageStr,"]% damage to [",_TargetID,"].");

IF
CharacterTeleported(_Player, _, _, _, _, _, _, _, _Skill)
AND
GetTemplate(_Player, _PlayerID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterTeleported] [",_PlayerID,"] teleported with skill [",_Skill,"].");

IF
CharacterTeleportToWaypoint(_Player, _Trigger)
AND
GetTemplate(_Player, _PlayerID)
AND
GetTemplate(_Trigger, _TriggerID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterTeleportToWaypoint] [",_PlayerID,"] teleported to waypoint [",_TriggerID,"].");

IF
CharacterTeleportToFleeWaypoint(_Player, _Trigger)
AND
GetTemplate(_Player, _PlayerID)
AND
GetTemplate(_Trigger, _TriggerID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Debug:CharacterTeleportToFleeWaypoint] [",_PlayerID,"] fled to waypoint [",_TriggerID,"].");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLMIME_8_0_DebugLevel"
