Version 1
SubGoalCombiner SGC_AND
INITSECTION
/*
DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ)

*/
KBSECTION
//REGION ATTACK_TRACKING
IF
CharacterStartAttackObject(_Target, _Owner, _Caster)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
_Caster != _Target
AND
LLMIME_Mimicking_QRY_GetTargetPriority(_Caster, _Target, 2)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, "", "")
AND
GetPosition(_Target, _TargetX, _TargetY, _TargetZ)
AND
GetUUID(_Caster, _CasterID)
AND
GetUUID(_Target, _TargetID)
AND
StringConcatenate(_CasterID, _TargetID, _Action)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterStartAttackObject] Queueing up mimic weapon attack on target.");
//ObjectSetFlag(_Mime, "LLMIME_MIMIC_PREFER_ITEM_TARGET"); // Prefer the item dummy, since all weapons can attack items
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Action, "Attack", _TargetX, _TargetZ, _TargetPriority, 1);

IF
CharacterStartAttackObject(_Target, _Owner, _Caster)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
THEN
NOT DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority);

//Delay by a few frames so the target event sticks first, if possible
IF
CharacterStartAttackPosition(_TargetX, _, _TargetZ, _Owner, _Caster)
AND
NOT DB_LLMIME_Timers_AttackAtPositionTimer(_, _Caster, _TargetX, _TargetZ)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
AND
GetUUID(_Caster, _CasterID)
AND
StringConcatenate("LLMIME_Timers_AttackAtPositionTimer_", _CasterID, _TimerName)
THEN
DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ);
TimerLaunch(_TimerName,5);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ)
AND
NOT LLMIME_Mimicking_QRY_ActionTypeQueued(_Caster, "Attack")
THEN
LLMIME_Mimicking_OnAttackAtPosition(_Caster, _TargetX, _TargetZ);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ)
THEN
NOT DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ);

PROC
LLMIME_Mimicking_OnAttackAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetZ)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime)
AND
GetUUID(_Caster, _CasterID)
AND
LeaderLog_QRY_RealToIntToString(_CasterID, _TargetX, _TargetZ)
AND
DB_LeaderLog_Temp_RealString(_CasterID, _xs, _zs)
AND
StringConcatenate(_xs, _zs, _Str)
AND
StringConcatenate(_CasterID, _Str, _Action)
THEN
NOT DB_LeaderLog_Temp_RealString(_CasterID, _xs, _zs);
LeaderLog_Log("DEBUG", "[LLMIME:Mime:OnAttackAtPosition] Queuing up next mimic attack position.");
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Action, "Attack", _TargetX, _TargetZ, 2);
LLMIME_Mimicking_StartNextMimic(_Caster, _Action, "Attack", 1000);

IF
CharacterStartAttackObject(_Target, _Owner, _Caster)
AND
LLMIME_Mimicking_QRY_ActionTypeQueued(_Caster, "Attack")
AND
GetUUID(_Caster, _CasterID)
AND
GetUUID(_Target, _TargetID)
AND
StringConcatenate(_CasterID, _TargetID, _Action)
THEN
LLMIME_Mimicking_StartNextMimic(_Caster, _Action, "Attack", 1000);
//END_REGION

//REGION ATTACK_MIMIC
PROC
LLMIME_Mimicking_MimicAction((CHARACTERGUID)_Caster, (STRING)_Action, "Attack")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Action, "Attack")
THEN
//CharacterAttack(_Mime, _Mime);
PlayAnimation(_Mime, "attack1");
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Action, "Attack");

QRY
LLMIME_Mimicking_QRY_HasEquippedWeaponType((CHARACTERGUID)_Mime, (STRING)_Type)
AND
LLMIME_Mimicking_QRY_Debug_EquippedWeaponType(_Mime, _Type)
AND
CharacterGetEquippedItem(_Mime, "Weapon", _Weapon)
AND
LLMIME_Mimicking_QRY_Internal_WeaponStringMatch(_Weapon, _Type)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_HasEquippedWeaponType((CHARACTERGUID)_Mime, (STRING)_Type)
AND
CharacterGetEquippedItem(_Mime, "Shield", _Weapon)
AND
LLMIME_Mimicking_QRY_Internal_WeaponStringMatch(_Weapon, _Type)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_Internal_WeaponStringMatch((GUIDSTRING)_Weapon, (STRING)_Type)
AND
GetStatString(_Weapon, _Stat)
AND
StringContains(_Stat, _Type, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_Internal_WeaponStringMatch((GUIDSTRING)_Weapon, (STRING)_Type)
AND
GetTemplate(_Weapon, _Template)
AND
StringContains(_Template, _Type, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_Debug_EquippedWeaponType((CHARACTERGUID)_Mime, (STRING)_Type)
AND
CharacterGetEquippedItem(_Mime, "Weapon", _Weapon)
AND
GetStatString(_Weapon, _Stat)
AND
GetTemplate(_Weapon, _ID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:HasEquippedWeaponType] Equipped weapon [",_ID,"] stat string is [",_Stat,"].");

QRY
LLMIME_Mimicking_QRY_Debug_EquippedWeaponType((CHARACTERGUID)_Mime, (STRING)_Type)
AND
CharacterGetEquippedItem(_Mime, "Weapon", _Weapon)
AND
LLMIME_Mimicking_QRY_Internal_WeaponStringMatch(_Weapon, _Type)
AND
GetStatString(_Weapon, _Stat)
AND
GetTemplate(_Weapon, _ID)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:HasEquippedWeaponType] Equipped weapon [",_ID,"][",_Stat,"] contains [",_Type,"].");

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Action, "Attack")
AND
NOT DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime)
AND
LLMIME_Mimicking_QRY_HasEquippedWeaponType(_Mime, "Crossbow")
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
CharacterUseSkill(_Mime, "Projectile_LLMIME_MimicCrossbowAttack", _Target, 1, 1, 1);
LLMIME_Mimicking_Attack_StartSuccessTimer(_Mime, _Target, _Action);
DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Action, "Attack")
AND
NOT DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime)
AND
LLMIME_Mimicking_QRY_HasEquippedWeaponType(_Mime, "Bow")
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
CharacterUseSkill(_Mime, "Projectile_LLMIME_MimicBowAttack", _Target, 1, 1, 1);
LLMIME_Mimicking_Attack_StartSuccessTimer(_Mime, _Target, _Action);
DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Action, "Attack")
AND
NOT DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
CharacterUseSkill(_Mime, "Target_LLMIME_MimicAttack", _Target, 1, 1, 1);
LLMIME_Mimicking_Attack_StartSuccessTimer(_Mime, _Target, _Action);
DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Action, "Attack")
AND
NOT DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime)
AND
CharacterCanSee(_Mime, _Target, 1)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
CharacterAttack(_Mime, _Target);
//SetVarObject(_Mime, "AttackOfOpportunity", _Target); // Call CharacterAttackWithoutMove in behavior with Base.charScript
LLMIME_Mimicking_Attack_StartSuccessTimer(_Mime, _Target, _Action);
DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Action, "Attack")
AND
DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime)
THEN
NOT DB_LLMIME_Mimicking_Temp_JustAttacked(_Mime);

PROC
LLMIME_Mimicking_Attack_StartSuccessTimer((CHARACTERGUID)_Mime, (GUIDSTRING)_Target, (STRING)_Action)
AND
DB_LLMIME_Mimicking_MimicFailTimer(_TimerName, _Mime, _Caster, _Action, _ActionType)
THEN
NOT DB_LLMIME_Mimicking_MimicFailTimer(_TimerName, _Mime, _Caster, _Action, _ActionType);
TimerCancel(_TimerName);

PROC
LLMIME_Mimicking_Attack_StartSuccessTimer((CHARACTERGUID)_Mime, (GUIDSTRING)_Target, (STRING)_Action)
AND
GetUUID(_Mime, _MimeID)
AND
StringConcatenate("LLMIME_Timers_AttackSuccessTimer_", _MimeID, _TimerName)
THEN
DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action);
TimerLaunch(_TimerName, 3000);

IF
SkillCast(_Mime, _Skill, _)
AND
DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action)
THEN
TimerCancel(_TimerName);
LLMIME_Mimicking_Attack_OnSuccess(_Mime);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action)
THEN
LLMIME_Mimicking_Attack_OnSuccess(_Mime);

PROC
LLMIME_Mimicking_Attack_OnSuccess((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action)
AND
DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Action, "Attack")
THEN
NOT DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Action, "Attack");
LLMIME_Dummy_ResetDummyAfterDelay((GUIDSTRING)_Target, 1000);

PROC
LLMIME_Mimicking_Attack_OnSuccess((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action)
THEN
NOT DB_LLMIME_Timers_AttackSuccessTimer(_TimerName, _Mime, _Target, _Action);
LLMIME_Mimicking_OnMimicSuccess(_Mime, _Action, "Attack");
//END_REGION

//REGION CLEAR_MIME
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_ClearMimeBonuses")
AND
DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ)
THEN
NOT DB_LLMIME_Timers_AttackAtPositionTimer(_TimerName, _Caster, _TargetX, _TargetZ);
TimerCancel(_TimerName);
//END_REGION

//REGION DEBUG
//query GetRotation([in](GUIDSTRING)_Target, [out](REAL)_X, [out](REAL)_Y, [out](REAL)_Z)
IF
CharacterStartAttackPosition(_txf, _tyf, _tzf, _Owner, _Caster)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugTargetPosition", _txf, _tyf, _tzf)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugTargetPosition", _txstr, _tystr, _tzstr)
AND
GetPosition(_Caster, _px, _py, _pz)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugAttackerPosition", _px, _py, _pz)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugAttackerPosition", _pxstr, _pystr, _pzstr)
AND
GetRotation(_Caster, _rx, _ry, _rz)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugRotation", _rx, _ry, _rz)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugRotation", _rxstr, _rystr, _rzstr)
AND
CharacterGetDisplayName(_Caster, _, _Name)
THEN
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugTargetPosition", _txstr, _tystr, _tzstr);
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugRotation", _rxstr, _rystr, _rzstr);
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugAttackerPosition", _pxstr, _pystr, _pzstr);
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackPosition] **************************************");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackPosition] Character [",_Name,"] made an attack:");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackPosition] Position[",_pxstr,",",_pystr,",",_pzstr,"] | Target: [",_txstr,",",_tystr,",",_tzstr,"]");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackPosition] Rotation: [",_rxstr,",",_rystr,",",_rzstr,"]");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackPosition] **************************************");

IF
CharacterStartAttackObject(_Defender, _Owner, _Caster)
AND
GetPosition(_Defender, _txf, _tyf, _tzf)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugTargetPosition", _txf, _tyf, _tzf)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugTargetPosition", _txstr, _tystr, _tzstr)
AND
GetPosition(_Caster, _px, _py, _pz)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugAttackerPosition", _px, _py, _pz)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugAttackerPosition", _pxstr, _pystr, _pzstr)
AND
GetRotation(_Caster, _rx, _ry, _rz)
AND
LeaderLog_QRY_RealToIntToString("MimicryAttackDebugRotation", _rx, _ry, _rz)
AND
DB_LeaderLog_Temp_RealString("MimicryAttackDebugRotation", _rxstr, _rystr, _rzstr)
AND
CharacterGetDisplayName(_Caster, _, _Name)
THEN
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugTargetPosition", _txstr, _tystr, _tzstr);
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugRotation", _rxstr, _rystr, _rzstr);
NOT DB_LeaderLog_Temp_RealString("MimicryAttackDebugAttackerPosition", _pxstr, _pystr, _pzstr);
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackObject] **************************************");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackObject] Character [",_Name,"] made an attack:");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackObject] Position[",_pxstr,",",_pystr,",",_pzstr,"] | Target: [",_txstr,",",_tystr,",",_tzstr,"]");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackObject] Rotation: [",_rxstr,",",_rystr,",",_rzstr,"]");
LeaderLog_Log("DEBUG", "[LLMIME:AttackDebug:CharacterStartAttackObject] **************************************");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
