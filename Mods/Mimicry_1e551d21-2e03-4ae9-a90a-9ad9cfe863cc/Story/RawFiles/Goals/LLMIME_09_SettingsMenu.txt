Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Settings_InitSettings();

KBSECTION
//REGION SETTINGS
PROC
LLMIME_Settings_InitSettings()
AND
NOT DB_LLBARTER_DialogBlacklist("Mimicry", _)
THEN
DB_LLBARTER_DialogBlacklist("Mimicry", "LLMIME_MimeVendor");
DB_LLBARTER_DialogBlacklist("Mimicry", "LLMIME_SettingsMenu");

PROC
LeaderUpdater_ModUpdated("Mimicry", "LaughingLeader", (STRING)_PastVersion, (STRING)_NewVersion)
AND
NOT DB_LLBARTER_DialogBlacklist("Mimicry", _)
THEN
LLMIME_Settings_InitSettings();
//END_REGION

//REGION MENU_SKILL
/*
IF
CharacterUsedSkill(_Mime, "Shout_LLMIME_OpenMenu", _)
AND
CharacterIsInCombat(_Mime, 0)
AND
DB_LLMIME_Templates("Mask_A", _Template)
AND
GetItemForItemTemplateInInventory(_Mime, _Template, _Mask)
AND
NOT DB_LLMIME_Settings_Temp_StartedDialog(_Mime)
THEN
Proc_StartDialog(0, "LLMIME_SettingsMenu", _Mask, _Mime);
DB_LLMIME_Settings_Temp_StartedDialog(_Mime);
*/

IF
CharacterUsedSkill(_Mime, "Shout_LLMIME_OpenMenu", _,_)
AND
CharacterIsInCombat(_Mime, 0)
THEN
Proc_StartDialog(0, "LLMIME_SettingsMenu", _Mime, _Mime);
//END_REGION

//REGION DIALOG_EVENTS
IF
DialogStarted("LLMIME_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Mime)
THEN
CharacterSetAnimationOverride(_Mime, "Kneel_01_Loop");
LLMIME_Settings_SetDialogVariables(_Mime, _Instance);

IF
DialogEnded("LLMIME_SettingsMenu", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Mime)
AND
NOT ObjectGetFlag(_Mime, "Mimicry_MimeVendorReady", 1)
THEN
CharacterSetAnimationOverride(_Mime, "");
//END_REGION

//REGION DIALOG_VARS
PROC
LLMIME_Settings_SetDialogVariables((CHARACTERGUID)_Mime, (INTEGER)_Instance)
THEN
DB_NOOP(1);
//END_REGION

//REGION DEBUG_FLAGS
IF
ObjectFlagSet("Mimicry_Debug_RemoveAllStatuses", (CHARACTERGUID)_Mime, _Instance)
AND
LeaderLib_Helper_QRY_ClearObjectFlag(_Mime, "Mimicry_Debug_RemoveAllStatuses")
THEN
LLMIME_RemoveMask(_Mime);
LLMIME_RemoveAllMimicryStatuses(_Mime);

IF
ObjectFlagSet("Mimicry_Debug_RefreshMask", (CHARACTERGUID)_Mime, _Instance)
AND
LeaderLib_Helper_QRY_ClearObjectFlag(_Mime, "Mimicry_Debug_RefreshMask")
THEN
LLMIME_RefreshMask(_Mime);
LLMIME_RemoveAllMimicryStatuses(_Mime);

IF
ObjectFlagSet("Mimicry_Commands_ResetMimeEquipment", (CHARACTERGUID)_Mime, _Instance)
AND
LeaderLib_Helper_QRY_ClearObjectFlag(_Mime, "Mimicry_Commands_ResetMimeEquipment")
THEN
LLMIME_ItemSystem_ResetMimeEquipment();
//END_REGION

//REGION MIME_VENDOR
IF
GameStarted(_Level,_)
AND
IsGameLevel(_Level, 1)
AND
NOT DB_LLMIME_Settings_MimeVendor(_)
THEN
LLMIME_Settings_Internal_InitMimeVendor();

PROC
LLMIME_Settings_Internal_InitMimeVendor()
AND
ObjectExists((CHARACTERGUID)CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516, 1)
AND
NOT ObjectGetFlag(CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516, "Mimicry_MimeVendorInitialized", 1)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Settings:InitMimeVendor] Global mime vendor found.");
LeaderLib_Helper_SetupDummy(CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516);
ObjectSetFlag(CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516, "Mimicry_MimeVendorInitialized");
SysClear("DB_LLMIME_Settings_MimeVendor", 1);
DB_LLMIME_Settings_MimeVendor((CHARACTERGUID)CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516);

PROC
LLMIME_Settings_Internal_InitMimeVendor()
AND
NOT DB_LLMIME_Settings_MimeVendor(_)
AND
ObjectExists((CHARACTERGUID)CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516, 0)
AND
CharacterGetHostCharacter(_Host)
AND
GetPosition(_Host, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, "LLMIME_MimeVendor_6c698e8f-9149-4933-95c0-da6e37df8968", 0, _MimeVendor)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Settings:InitMimeVendor] Global mime vendor not found. Creating new mime vendor.");
LeaderLib_Helper_SetupDummy(_MimeVendor);
ObjectSetFlag(_MimeVendor, "Mimicry_MimeVendorInitialized");
DB_LLMIME_Settings_MimeVendor(_MimeVendor);

//Why is it dead?
IF
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
CharacterIsDead(_MimeVendor, 1)
THEN
CharacterResurrectCustom(_MimeVendor, "");

//Non-global vendor resetting
IF
RegionEnded(_Level)
AND
IsGameLevel(_Level, 1)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
ObjectIsGlobal(_MimeVendor, 0)
THEN
NOT DB_LLMIME_Settings_MimeVendor(_MimeVendor);
SetOnStage(_MimeVendor, 0);
CharacterDieImmediate(_MimeVendor, 0, "", _MimeVendor);

IF
RegionEnded(_Level)
AND
IsGameLevel(_Level, 1)
AND
DB_LLMIME_Settings_MimeVendor(CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516)
THEN
ObjectClearFlag(CHARACTERGUID_S_LLMIME_MimeVendor_3744df74-87c5-49c0-9eb4-b3c4528dd516, "Mimicry_MimeVendorInitialized");

IF
ObjectFlagSet("Mimicry_PrepareMimeVendor", (CHARACTERGUID)_Mime, _Instance)
AND
NOT LLMIME_Settings_QRY_MimeVendorIsBusy()
AND
LLMIME_Settings_QRY_PrepareMimeVendor(_Mime)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
THEN
DialogAddActorAt(_Instance, _MimeVendor, 3);
LeaderLog_Log("DEBUG", "[Mimicry:Settings:PrepareMimeVendor] Mime vendor is ready.");

QRY
LLMIME_Settings_QRY_MimeVendorIsBusy()
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
DB_DialogNPCs(_, _MimeVendor, _)
THEN
DB_NOOP(1);

IF
ObjectFlagSet("Mimicry_PrepareMimeVendor", (CHARACTERGUID)_Mime, _Instance)
THEN
ObjectClearFlag(_Mime, "Mimicry_PrepareMimeVendor");

IF
DialogEnded("LLMIME_MimeVendor", _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, (CHARACTERGUID)_Mime)
THEN
ObjectClearFlag(_Mime, "Mimicry_MimeVendorReady");

IF
DialogEnded("LLMIME_MimeVendor", _Instance)
AND
DB_DialogName("LLMIME_SettingsMenu", _MainInstance)
AND
DB_DialogNPCs(_MainInstance, _MimeVendor, _)
AND
DB_LLMIME_Settings_MimeVendor((CHARACTERGUID)_MimeVendor)
AND
DialogRemoveActorFromDialog(_MainInstance, _MimeVendor, _Success)
AND
IntegertoString(_Success, _SuccessStr)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Settings:DialogEnded(LLMIME_MimeVendor)] Attempted to remove mime vendor from dialog. Success[",_SuccessStr,"].");

QRY
LLMIME_Settings_QRY_PrepareMimeVendor((CHARACTERGUID)_Mime)
THEN
LLMIME_Settings_Internal_PrepareMimeVendor(_Mime);

PROC
LLMIME_Settings_Internal_PrepareMimeVendor((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
ObjectIsOnStage(_MimeVendor, 0)
THEN
SetOnStage(_MimeVendor, 1);

//Safety
PROC
LLMIME_Settings_Internal_PrepareMimeVendor((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
CharacterIsDead(_MimeVendor, 1)
THEN
CharacterResurrectCustom(_MimeVendor, "");

//Safety 2
PROC
LLMIME_Settings_Internal_PrepareMimeVendor((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Settings_MimeVendor(_)
THEN
LLMIME_Settings_Internal_InitMimeVendor();

PROC
LLMIME_Settings_Internal_PrepareMimeVendor((CHARACTERGUID)_Mime)
AND
LLMIME_QRY_CharacterIsAMime(_Mime)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
CharacterGetAttitudeTowardsPlayer(_MimeVendor, _Mime, _Attitude)
AND
_Attitude != 100
THEN
CharacterAddAttitudeTowardsPlayer(_MimeVendor, _Mime, 200);

PROC
LLMIME_Settings_Internal_PrepareMimeVendor((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
ObjectSetFlag(_Mime, "Mimicry_MimeVendorReady");
TeleportTo(_MimeVendor, _Mime, "", 0);
DB_LLMIME_Settings_MimeVendorTarget(_MimeVendor, _Mime);
PROC_Helper_SafeTeleportTo(_MimeVendor, _Mime, "Mimicry_Settings_MoveMimeVendorToPlayer", 0);
//TeleportToPosition(_MimeVendor, _x, _y, _z, "Mimicry_Settings_MoveMimeVendorToPlayer", 0, 1);

IF
StoryEvent((CHARACTERGUID)_MimeVendor, "Mimicry_Settings_MoveMimeVendorToPlayer")
AND
DB_GlobalFlag("LeaderLib_IsEditorMode")
AND
GetPosition(_MimeVendor, _x, _y, _z)
THEN
PlayEffectAtPosition("RS3_FX_GP_ScriptedEvent_Teleport_GenericSmoke_01", _x, _y, _z);

IF
StoryEvent((CHARACTERGUID)_MimeVendor, "Mimicry_Settings_MoveMimeVendorToPlayer")
AND
DB_LLMIME_Settings_MimeVendorTarget(_MimeVendor, _Mime)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
NOT DB_LLMIME_Settings_MimeVendorTarget(_MimeVendor, _Mime);
CharacterMoveToPosition(_MimeVendor, _x, _y, _z, 1, "");

//"Warping" items.
IF
HappyWithDeal(_Mime, _MimeVendor, _ValuePlayer, _ValueNpc)
AND
_ValuePlayer >= _ValueNpc
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
AND
LLMIME_QRY_CharacterIsAMime(_Mime)
THEN
PlayEffect(_Mime, "LLMIME_FX_Mime_Mask_TradeTransaction_Overlay_01");
//END_REGION

//REGION MIME_VENDOR_LIQUIDATE
IF
ObjectFlagSet("Mimicry_LiquidateMimeVendorInventory", (CHARACTERGUID)_MimeVendor, _Instance)
AND
DialogGetInvolvedPlayer(_Instance, 1, _Player)
THEN
PlayEffect(_Player, "LLMIME_FX_Mime_Mask_Liquidation_Overlay_01");

IF
ObjectFlagSet("Mimicry_LiquidateMimeVendorInventory", (CHARACTERGUID)_MimeVendor, _Instance)
AND
GetPosition(_MimeVendor, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LOOT_LeaderLib_BackPack_Invisible_98fa7688-0810-4113-ba94-9a8c8463f830", _x, _y, _z, _Backpack)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Settings:Mimicry_LiquidateMimeVendorInventory] Starting item liquidation for Mime Vendor.");
SetVisible(_Backpack, 0);
ItemSetOwner(_Backpack, _MimeVendor);
MoveAllItemsTo(_MimeVendor, _Backpack, 0, 0, 0);
ProcObjectTimer(_Backpack, "Mimicry_Timers_StartLiquidationIterator", 100);
ObjectClearFlag(_MimeVendor, "Mimicry_LiquidateMimeVendorInventory");

PROC
ProcObjectTimerFinished((ITEMGUID)_Backpack, "Mimicry_Timers_StartLiquidationIterator")
AND
GetUUID(_Backpack, _UUID)
AND
StringConcatenate("Mimicy_Iterator_BackpackFinished_", _UUID, _GlobalFlag)
THEN
DB_LLMIME_Settings_BackpackIterator(_Backpack, _GlobalFlag);
InventoryLaunchIterator(_Backpack, "Mimicy_Iterator_RemoveNonMimeItems", _GlobalFlag);
ProcObjectTimer(_Backpack, "Mimicry_OnMimeVendorLiquidationComplete", 1000);

IF
GlobalFlagSet(_GlobalFlag)
AND
DB_LLMIME_Settings_BackpackIterator(_Backpack, _GlobalFlag)
THEN
NOT DB_LLMIME_Settings_BackpackIterator(_Backpack, _GlobalFlag);
ProcObjectTimerCancel(_Backpack, "Mimicry_OnMimeVendorLiquidationComplete");
GlobalClearFlag(_GlobalFlag);
LeaderLog_Log("DEBUG", "[Mimicry:Settings:Mimicy_Iterator_BackpackFinished] InventoryLaunchIterator set global flag [",_GlobalFlag,"].");
SetStoryEvent(_Backpack, "Mimicry_Settings_OnBackpackIteratorFinished");

IF
StoryEvent((ITEMGUID)_Item, "Mimicy_Iterator_RemoveNonMimeItems")
AND
IsTagged(_Item, "Mimicry_MimeItem", 0)
AND
ItemIsStoryItem(_Item, 0)
AND
ItemGetGoldValue(_Item, _Value)
AND
DB_LLMIME_Settings_MimeVendor(_MimeVendor)
THEN
ItemRemove(_Item);
CharacterAddGold(_MimeVendor, _Value);

PROC
ProcObjectTimerFinished((ITEMGUID)_Backpack, "Mimicry_OnMimeVendorLiquidationComplete")
AND
DB_LLMIME_Settings_BackpackIterator(_Backpack, _GlobalFlag)
THEN
NOT DB_LLMIME_Settings_BackpackIterator(_Backpack, _GlobalFlag);
LeaderLog_Log("DEBUG", "[Mimicry:Settings:Mimicry_OnMimeVendorLiquidationComplete] Object timer finished before the global flag was set. Manually completing.");
SetStoryEvent(_Backpack, "Mimicry_Settings_OnBackpackIteratorFinished");

IF
StoryEvent((ITEMGUID)_Backpack, "Mimicry_Settings_OnBackpackIteratorFinished")
AND
ItemGetOwner(_Backpack, _MimeVendor)
THEN
MoveAllItemsTo(_Backpack, _MimeVendor, 0, 0, 0);
ProcObjectTimer(_Backpack, "Mimicry_Timers_RemoveIteratorBackpack", 500);
LeaderLog_Log("DEBUG", "[Mimicry:Settings:OnMimeVendorLiquidationComplete] Mime vendor liquidated all non-mime items.");

PROC
ProcObjectTimerFinished((ITEMGUID)_Backpack, "Mimicry_Timers_RemoveIteratorBackpack")
THEN
ItemRemove(_Backpack);
//END_REGION

//REGION DAMAGE_DEBUFF_VARIABLE
IF
GlobalFlagSet("Mimicry_MimickingDamageDebuff1_Enabled")
THEN
DialogSetVariableString("LLMIME_SettingsMenu", "LLMIME_Settings_MimickingDamagePercentage_f980d09f-aedb-45cc-bab8-b725834ce6f3", "90% (-10%)");

IF
GlobalFlagSet("Mimicry_MimickingDamageDebuff2_Enabled")
THEN
DialogSetVariableString("LLMIME_SettingsMenu", "LLMIME_Settings_MimickingDamagePercentage_f980d09f-aedb-45cc-bab8-b725834ce6f3", "75% (-25%)");

IF
GlobalFlagSet("Mimicry_MimickingDamageDebuff3_Enabled")
THEN
DialogSetVariableString("LLMIME_SettingsMenu", "LLMIME_Settings_MimickingDamagePercentage_f980d09f-aedb-45cc-bab8-b725834ce6f3", "50% (-50%)");

IF
GlobalFlagCleared("Mimicry_MimickingDamageDebuff1_Enabled")
THEN
LLMIME_Settings_Internal_UpdateMimickingDamageVariable();

IF
GlobalFlagCleared("Mimicry_MimickingDamageDebuff2_Enabled")
THEN
LLMIME_Settings_Internal_UpdateMimickingDamageVariable();

IF
GlobalFlagCleared("Mimicry_MimickingDamageDebuff3_Enabled")
THEN
LLMIME_Settings_Internal_UpdateMimickingDamageVariable();

PROC
LLMIME_Settings_Internal_UpdateMimickingDamageVariable()
AND
NOT LLMIME_QRY_DamageDebuffSet()
THEN
DialogSetVariableString("LLMIME_SettingsMenu", "LLMIME_Settings_MimickingDamagePercentage_f980d09f-aedb-45cc-bab8-b725834ce6f3", "100%");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
