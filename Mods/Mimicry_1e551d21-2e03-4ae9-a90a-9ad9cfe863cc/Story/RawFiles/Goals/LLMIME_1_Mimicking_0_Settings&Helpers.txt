Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLMIME_Mimicking_InitSettings();

//DB_LLMIME_Mimicking_Blacklist_SkillTypes(_SkillType, _GlobalFlag, _EnabledState)
//DB_LLMIME_Mimicking_Blacklist_Skills(_Skill)
//DB_LLMIME_Mimicking_Blacklist_SkillElements(_SkillType, _SkillElement)
//DB_LLMIME_Mimicking_Blacklist_SkillPrefixes(_Prefix)
KBSECTION
//REGION SETTINGS
PROC
LLMIME_Mimicking_ClearSettings()
THEN
SysClear("DB_LLMIME_Mimicking_Blacklist_SkillTypes", 3);
SysClear("DB_LLMIME_Mimicking_Blacklist_SkillElements", 2);
SysClear("DB_LLMIME_Mimicking_Blacklist_Skills", 1);
SysClear("DB_LLMIME_Mimicking_Blacklist_SkillPrefixes", 1);
SysClear("DB_LLMIME_Mimicking_Whitelist_Skills", 1);
SysClear("DB_LLMIME_Mimicking_Whitelist_ItemTags", 1);
SysClear("DB_LLMIME_Mimicking_Blacklist_ItemTemplates", 1);
SysClear("DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes", 1);
SysClear("DB_LLMIME_Mimicking_Whitelist_WeaponCopy_Skills", 1);
SysClear("DB_LLMIME_Mimicking_Blacklist_WeaponCopy_IgnoreSkillsWith", 1);

PROC
LLMIME_Mimicking_InitSettings()
THEN
DB_LLMIME_Mimicking_Blacklist_SkillTypes("Path", "Mimicry_Mimic_SkillType_Path_Enabled", 1);
DB_LLMIME_Mimicking_Blacklist_SkillTypes("Summon", "Mimicry_Mimic_SkillType_Summons_Enabled", 1);
DB_LLMIME_Mimicking_Blacklist_SkillElements("Shout", ""); // Special type shout skills, typically used for toggle abilities in mods
DB_LLMIME_Mimicking_Blacklist_Skills("Shout_FavourableWind");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_LLMIME_MimicAttack");
DB_LLMIME_Mimicking_Blacklist_Skills("Shout_LLMIME_ReturnToOwner");
DB_LLMIME_Mimicking_Blacklist_Skills("Shout_LLMIME_BrawlerStance");
//Source skills
DB_LLMIME_Mimicking_Blacklist_Skills("Target_Challenge");
//Summon Infusions
DB_LLMIME_Mimicking_Blacklist_Skills("Target_ElectricInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_IceInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_FireInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_WarpInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_ShadowInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_PowerInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_RangedInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_PoisonInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_NecrofireInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_WaterInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_AcidInfusion");
DB_LLMIME_Mimicking_Blacklist_Skills("Target_CursedElectricInfusion");
//Prefixes (i.e. mod skills)
DB_LLMIME_Mimicking_Blacklist_SkillPrefixes("_CA_");
//Special skills
DB_LLMIME_Mimicking_Whitelist_Skills("Shout_RecoverArmour");
DB_LLMIME_Mimicking_Whitelist_Skills("Shout_EnemyOverthrow");

DB_LLMIME_Mimicking_Whitelist_ItemTags("Potion");
DB_LLMIME_Mimicking_Whitelist_ItemTags("POTIONS");
DB_LLMIME_Mimicking_Whitelist_ItemTags("DRINK");
DB_LLMIME_Mimicking_Whitelist_ItemTags("FOOD");

//DB_LLMIME_Mimicking_Blacklist_ItemTags("");
DB_LLMIME_Mimicking_Blacklist_ItemTemplates("CON_Potion_BloodRose_8c89072f-820b-46cb-83af-757b3d469c93");
DB_LLMIME_Mimicking_Blacklist_ItemTemplates("CON_Potion_Source_A_23cdecb1-06a4-4290-bb2c-de300cba5dd0");
DB_LLMIME_Mimicking_Blacklist_ItemTemplates("LOOT_Source_Orb_106a7107-cf36-4331-b5b5-6de71969f370");

//Skill types with the "UseWeaponDamage" property. Checking the skill name itself.
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Target_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Shout_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Projectile_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("ProjectileStrike_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Cone_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Zone_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("MultiStrike_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Rush_");
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes("Rain_");

DB_LLMIME_Mimicking_Whitelist_WeaponCopy_Skills("Shout_RecoverArmour");

DB_LLMIME_Mimicking_Blacklist_WeaponCopy_IgnoreSkillsWith("_Grenade_");
//END_REGION

//REGION UPDATING
QRY
LLMIME_Mimicking_Internal_QRY_VersionNeedsUpdating((STRING)_Version)
AND
_Version == "0.9.0.0"
THEN
DB_NOOP(1);

IF
StoryEvent(_, "LeaderUpdater_ModVersionsChanged")
AND
DB_LeaderUpdater_Temp_PastRegistered("Mimicry", "LaughingLeader", _Version)
AND
LLMIME_Mimicking_Internal_QRY_VersionNeedsUpdating(_Version)
AND
NOT DB_LLMIME_Mimicking_UpdatingVersion(_)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:System] Updating settings from version [",_Version,"]");
DB_LLMIME_Mimicking_UpdatingVersion(1);
LLMIME_Mimicking_ClearSettings();
LLMIME_Mimicking_InitSettings();

IF
StoryEvent(_, "LeaderUpdater_ModVersionsChanged")
AND
DB_LLMIME_Mimicking_UpdatingVersion(_Val)
THEN
NOT DB_LLMIME_Mimicking_UpdatingVersion(_Val);
//END_REGION

//REGION COPY_ATTRIBUTES
PROC
LLMIME_Mimicking_CopyAttributes((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster)
AND
DB_LLLIB_Attributes(_Attribute)
AND
CharacterGetAttribute(_Caster, _Attribute, _Val)
AND
CharacterGetAttribute(_Mime, _Attribute, _CurrentVal)
AND
IntegerSubtract(_Val, _CurrentVal, _AddVal)
AND
_AddVal > 0
THEN
CharacterAddAttribute(_Mime, _Attribute, _AddVal);
DB_LLMIME_Mimicking_MimickedAttribute(_Mime, _Attribute, _AddVal);

PROC
LLMIME_Mimicking_ClearCopiedAttributes((CHARACTERGUID)_Mime)
AND
DB_LLMIME_Mimicking_MimickedAttribute(_Mime, _Attribute, _AddVal)
THEN
CharacterRemoveAttribute(_Mime, _Attribute, _AddVal);
NOT DB_LLMIME_Mimicking_MimickedAttribute(_Mime, _Attribute, _AddVal);
//END_REGION

//REGION BLACKLIST_CHECK_ITEMS
QRY
LLMIME_Mimicking_QRY_ItemIsAllowed((ITEMGUID)_Item)
AND
GetTemplate(_Item, _Template)
AND
NOT DB_LLMIME_Mimicking_Blacklist_ItemTemplates(_Template)
AND
DB_LLMIME_Mimicking_Whitelist_ItemTags(_Tag)
AND
IsTagged(_Item, _Tag, 1)
THEN
DB_NOOP(1);
//END_REGION

//REGION BLACKLIST_CHECK_SKILLS
QRY
LLMIME_Mimicking_QRY_Internal_BlockedSkillTypeIsEnabled((STRING)_SkillType)
AND
DB_LLMIME_Mimicking_Blacklist_SkillTypes(_SkillType, _GlobalFlag, _EnabledState)
AND
GlobalGetFlag(_GlobalFlag, _EnabledState)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_Internal_BlockedSkillTypeIsEnabled((STRING)_SkillType)
AND
NOT DB_LLMIME_Mimicking_Blacklist_SkillTypes(_SkillType, _, _)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_Internal_SkillNameMatchesBlocked((STRING)_Skill)
AND
DB_LLMIME_Mimicking_Blacklist_SkillPrefixes(_Prefix)
AND
StringContains(_Skill, _Prefix, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSourceSkill((STRING)_Skill, (CHARACTERGUID)_Mime)
AND
IsSourceSkill(_Skill, 0)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSourceSkill((STRING)_Skill, (CHARACTERGUID)_Mime)
AND
IsSourceSkill(_Skill, 1)
AND
GlobalGetFlag("Mimicry_Mimic_CopySourceSkills_Enabled", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSourceSkill((STRING)_Skill, (CHARACTERGUID)_Mime)
AND
IsSourceSkill(_Skill, 1)
AND
NOT GlobalGetFlag("Mimicry_Mimic_CopySourceSkills_Enabled", 1)
AND
HasActiveStatus(_Mime, "LLMIME_MIMICKING_SOURCE", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSkill((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
//AND
//LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill)
AND
LLMIME_Mimicking_QRY_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_SkillIsAllowed((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
DB_LLMIME_Mimicking_Whitelist_Skills(_Skill)
THEN
DB_NOOP(1);

/*
QRY
LLMIME_Mimicking_QRY_SkillIsAllowed((STRING)_Skill)
AND
NOT DB_LLMIME_Mimicking_Blacklist_Skills(_Skill)
AND
NOT DB_LLMIME_SkillData(_Skill, _, _)
THEN
DB_NOOP(1);
*/

QRY
LLMIME_Mimicking_QRY_SkillIsAllowed((STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Mimicking_Blacklist_Skills(_Skill)
AND
//LeaderLog_QRY_Log("DEBUG", "[LLMIME:QRY_SkillIsAllowed] Checking skill: Name[",_Skill,"] Type[",_SkillType,"] Element[",_SkillElement,"]")
//AND
LLMIME_Mimicking_QRY_Internal_BlockedSkillTypeIsEnabled(_SkillType)
AND
NOT DB_LLMIME_Mimicking_Blacklist_SkillElements(_SkillType, _SkillElement)
AND
NOT LLMIME_Mimicking_QRY_Internal_SkillNameMatchesBlocked(_Skill)
THEN
DB_NOOP(1);
//END_REGION

//REGION WEAPON_SKILLTYPE
//Only bother with skill types that allow weapon requirements
QRY
LLMIME_Mimicking_Skills_QRY_WeaponSkillTypeMatch((STRING)_Skill)
AND
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_SkillTypes(_SkillType)
AND
StringContains(_Skill, _SkillType, 1)
AND
NOT LLMIME_Mimicking_Skills_QRY_IgnoredSkillStringMatch(_Skill)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_Skills_QRY_IgnoredSkillStringMatch((STRING)_Skill)
AND
DB_LLMIME_Mimicking_Blacklist_WeaponCopy_IgnoreSkillsWith(_Str)
AND
StringContains(_Skill, _Str, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_Skills_QRY_WeaponSkillTypeMatch(_Skill)
AND
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_Skills(_Skill)
THEN
DB_NOOP(1);
//END_REGION

//REGION CHECK_QUERIES
QRY
LLMIME_Mimicking_QRY_CanMimicCharacter((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
NOT LLMIME_Mimicking_QRY_MimeIsCurrentlyMimicking(_Mime)
AND
_Character != _Mime
AND
LLMIME_Mimicking_QRY_CanMimicMime(_Character, _Mime)
AND
LLMIME_Mimicking_QRY_FollowerOwnerMatch(_Character, _Mime)
AND
QRY_CharacterIsNotDisabled(_Mime)
AND
LLMIME_Mimicking_QRY_CombatConditionsMet(_Character, _Mime)
AND
LLMIME_Mimicking_QRY_PartyConditionsMet(_Character, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSummon(_Character)
THEN
DB_NOOP(1);

//Mime followers can only mimic their owners
QRY
LLMIME_Mimicking_QRY_FollowerOwnerMatch((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 1)
AND
DB_LLLIB_PartyFollower(_Character, _Mime)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_FollowerOwnerMatch((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
IsTagged(_Mime, "LLMIME_MimeFollower", 0)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicMime((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Character)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicMime((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
LLMIME_QRY_CharacterIsAMime(_Character)
AND
NOT LLMIME_Mimicking_QRY_IsMimicking(_Character)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CombatConditionsMet((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
GlobalGetFlag("Mimicry_Mimic_CombatOnlyDisabled", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CombatConditionsMet((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
NOT GlobalGetFlag("Mimicry_Mimic_CombatOnlyDisabled", 1)
AND
DB_LeaderLib_Combat_Objects(_CombatID, _Character)
AND
DB_LeaderLib_Combat_Objects(_CombatID, _Mime) // Same combat as the target to copy
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_PartyConditionsMet((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
GlobalGetFlag("Mimicry_Mimic_PartyOnlyDisabled", 1)
AND
CharacterIsAlly(_Character, _Mime, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_PartyConditionsMet((CHARACTERGUID)_Character, (CHARACTERGUID)_Mime)
AND
NOT GlobalGetFlag("Mimicry_Mimic_PartyOnlyDisabled", 1)
AND
CharacterIsInPartyWith(_Character, _Mime, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSummon((CHARACTERGUID)_Character)
AND
CharacterIsSummon(_Character, 0)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CanMimicSummon((CHARACTERGUID)_Character)
AND
CharacterIsSummon(_Character, 1)
AND
GlobalGetFlag("Mimicry_Mimic_CopySummonedCreatureSkills_Enabled", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CharacterIsMimickable((CHARACTERGUID)_Character)
AND
CharacterIsPlayer(_Character, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CharacterIsMimickable((CHARACTERGUID)_Character)
AND
CharacterIsPlayer(_Character, 0)
AND
CharacterIsSummon(_Character, 1)
AND
GlobalGetFlag("Mimicry_Mimic_CopySummonedCreatureSkills_Enabled", 1)
AND
CharacterGetOwner(_Character, _Owner)
AND
CharacterIsPlayer(_Owner, 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_MimesAvailable()
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
//AND
//NOT LLMIME_Mimicking_QRY_MimeIsCurrentlyMimicking(_Mime)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CasterAndTargetMatch((CHARACTERGUID)_Character, (GUIDSTRING)_Target, (STRING)_SkillType)
AND
_SkillType == "Shout"
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_CasterAndTargetMatch((CHARACTERGUID)_Character, (GUIDSTRING)_Target, (STRING)_SkillType)
AND
(GUIDSTRING)_Character == _Target
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_IsMimicking((CHARACTERGUID)_Mime)
AND
ObjectGetFlag(_Mime, "Mimicry_IsMimicking", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_IsMimicking((CHARACTERGUID)_Mime)
AND
HasActiveStatus(_Mime, "LLMIME_MIMICKING", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_QRY_IsMimicking((CHARACTERGUID)_Mime)
AND
HasActiveStatus(_Mime, "LLMIME_MIMICKING_FOLLOWER", 1)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
