Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLMIME_Mimicking_Temp_CasterWeapons(_Caster, _MainHand, _Offhand)
//DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType)
//DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
KBSECTION
//REGION SKILL_TRACKING
//Give time for the CharacterUsedSkill event to fire, for tracking the element and type
IF
CharacterUsedSkillOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
THEN
DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType);

IF
CharacterUsedSkillOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
THEN
NOT DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority);

IF
CharacterUsedSkill(_Caster, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType)
THEN
NOT DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType);
LLMIME_Mimicking_OnSkillCastOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement);

PROC
LLMIME_Mimicking_OnSkillCastOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
_Caster != _Target
AND
LLMIME_Mimicking_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_GetTargetPriority(_Caster, _Target)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
AND
GetPosition(_Target, _TargetX, _TargetY, _TargetZ)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterUsedSkillOnTarget] Queueing up next mimic skill [",_Skill,"] on target.");
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, _TargetPriority, 1);

PROC
LLMIME_Mimicking_OnSkillCastOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
_Caster == _Target
AND
LLMIME_Mimicking_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterUsedSkillOnTarget] Queueing up next mimic self-target skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextSelfAction(_Mime, _Caster, _Skill, "Skill");

//Delay by a frame or two so target events will fire first
IF
CharacterUsedSkillAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
THEN
DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);

IF
CharacterUsedSkill(_Caster, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
THEN
NOT DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);
LLMIME_Mimicking_StartOnSkillCastOnPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType, _SkillElement);

PROC
LLMIME_Mimicking_StartOnSkillCastOnPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
LLMIME_Mimicking_QRY_CanMimicSkill(_Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
NOT DB_LLMIME_Timers_SkillCastAtPosition(_, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
GetUUID(_Caster, _CasterID)
AND
StringConcatenate("LLMIME_Events_SkillCastAtPosition_", _CasterID, _TimerName)
THEN
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);
TimerLaunch(_TimerName,5);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
AND
NOT LLMIME_Mimicking_QRY_SelfActionIsQueued(_Caster, _Skill, "Skill")
AND
NOT LLMIME_Mimicking_QRY_TargetActionIsQueued(_Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, 1)
THEN
LLMIME_Mimicking_OnSkillCastAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
THEN
NOT DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);

PROC
LLMIME_Mimicking_OnSkillCastAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, "shout")
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, "shout")
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:OnSkillCastAtPosition] Queuing up next mimic shout skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextSelfAction(_Mime, _Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_OnSkillCastAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType)
AND
_SkillType != "shout"
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mime:CharacterUsedSkillAtPosition] Queuing up next mimic positional skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, -1);

IF
CharacterUsedSkill(_Caster, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_ActionQueued(_Caster, _Skill, "Skill")
AND
GetUUID(_Caster, _ID)
AND
StringConcatenate(_Skill, _ID, _Str)
AND
StringConcatenate("LLMIME_Timers_CastFailTimer_", _Str, _TimerName)
THEN
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill);
TimerLaunch(_TimerName, 2000);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill)
AND
LLMIME_Mimicking_QRY_ActionQueued(_Caster, _Skill, "Skill")
THEN
LLMIME_Mimicking_StartNextMimic(_Caster, _Skill, "Skill");

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill);

IF
SkillCast(_Caster, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_ActionQueued(_Caster, _Skill, "Skill")
THEN
LLMIME_Mimicking_StartNextMimic(_Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_StartNextMimic((CHARACTERGUID)_Caster, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Action)
THEN
NOT DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Action);
TimerCancel(_TimerName);
//END_REGION

//REGION SKILL_MIMIC
QRY
LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (STRING)_Skill)
AND
NOT DB_LLMIME_Mimicking_Whitelist_WeaponCopy_Skills(_Skill)
THEN
DB_NOOP(1);

QRY
LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (STRING)_Skill)
AND
DB_LLMIME_Mimicking_Whitelist_WeaponCopy_Skills(_Skill)
AND
LLMIME_Mimicking_QRY_CasterAndMimeUnarmed(_Mime, _Caster, "Skill")
THEN
DB_NOOP(1);

PROC
LLMIME_Mimicking_MimicAction((CHARACTERGUID)_Caster, (STRING)_Skill, "Skill")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill")
AND
NOT LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking(_Mime, _Caster, _Skill)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill);
LLMIME_Mimicking_Internal_StartWeaponMimicking(_Mime, _Caster, "Skill");

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_WeaponMimickingStarted")
AND
DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill)
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill")
THEN
NOT DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill);
LLMIME_Mimicking_Internal_MimicWeapons(_Mime, _Caster, _Skill, "Skill");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Mime, 1, 1, 1);
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_MimicAction((CHARACTERGUID)_Caster, (STRING)_Skill, "Skill")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill")
AND
LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking(_Mime, _Caster, _Skill)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Mime, 1, 1, 1);
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Target, 1, 1, 1);

IF
CharacterUsedSkill(_Mime, _Skill, _SkillType, _SkillElement)
AND
ObjectGetFlag(_Mime, "Mimicry_IsMimicking", 1)
AND
IsSourceSkill(_Skill, 1)
AND
HasActiveStatus(_Mime, "LLMIME_MIMICKING_SOURCE", 1)
THEN
RemoveStatus(_Mime, "LLMIME_MIMICKING_SOURCE");
//END_REGION

//REGION SUCCESS
IF
CharacterUsedSkill(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:Skills:CharacterUsedSkill] Mimicked skill [",_Skill,"] was used. Starting cast fail timer.");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 3000);
ProcObjectTimer(_Mime, "LLMIME_Timers_CastFailTimer", 1500);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Mime, "LLMIME_Timers_CastFailTimer")
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:Skills:ProcObjectTimerFinished(LLMIME_Timers_CastFailTimer)] Mimicked skill [",_Skill,"] never fired the SkillCast event. Completing.");
LLMIME_Mimicking_OnMimicSuccess(_Mime, _Skill, "Skill");

IF
SkillCast(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Skill, "Skill")
THEN
NOT DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Skill, "Skill");
LLMIME_Dummy_ResetDummyAfterDelay((GUIDSTRING)_Target, 2000);

IF
SkillCast(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:Skills:SkillCast] Mimicked skill [",_Skill,"] was cast.");
ProcObjectTimerCancel(_Mime, "LLMIME_Timers_CastFailTimer");
LLMIME_Mimicking_OnMimicSuccess(_Mime, _Skill, "Skill");
//END_REGION

//REGION CLEAR_MIME
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_ClearMimeBonuses")
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
