Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION UNDEAD_SAFETY
QRY
LLMIME_Mimicking_QRY_SkillIsSafeForRace((CHARACTERGUID)_Caster, (CHARACTERGUID)_Mime, (STRING)_Skill)
AND
ObjectGetFlag(_Mime, "Mimicry_SkillMimickingSafetyDisabled", 1)
THEN
DB_NOOP(1);

//Mimic if both races aren't undead
QRY
LLMIME_Mimicking_QRY_SkillIsSafeForRace((CHARACTERGUID)_Caster, (CHARACTERGUID)_Mime, (STRING)_Skill)
AND
NOT LLMIME_QRY_IsUndead(_Mime)
AND
NOT LLMIME_QRY_IsUndead(_Caster)
THEN
DB_NOOP(1);

//Mimic if both races are undead
QRY
LLMIME_Mimicking_QRY_SkillIsSafeForRace((CHARACTERGUID)_Caster, (CHARACTERGUID)_Mime, (STRING)_Skill)
AND
LLMIME_QRY_IsUndead(_Mime)
AND
LLMIME_QRY_IsUndead(_Caster)
THEN
DB_NOOP(1);

//Mimic if an undead casts a healing spell on themselves
QRY
LLMIME_Mimicking_QRY_SkillIsSafeForRace((CHARACTERGUID)_Caster, (CHARACTERGUID)_Mime, (STRING)_Skill)
AND
DB_LLMIME_Mimicking_HarmfulHealingSkills(_Skill)
AND
NOT LLMIME_QRY_IsUndead(_Mime)
AND
LLMIME_QRY_IsUndead(_Caster)
THEN
DB_NOOP(1);

//Mimic if the skill is safe for undead
QRY
LLMIME_Mimicking_QRY_SkillIsSafeForRace((CHARACTERGUID)_Caster, (CHARACTERGUID)_Mime, (STRING)_Skill)
AND
NOT DB_LLMIME_Mimicking_HarmfulHealingSkills(_Skill)
THEN
DB_NOOP(1);
//END_REGION

//REGION SKILL_TRACKING
PROC
LLMIME_Mimicking_OnSkillUsedOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill)
AND
DB_LLMIME_Mimicking_SkillSwap(_Skill, _ResultSkill, _ResultSkillType, _ResultSkillElement)
THEN
DB_LLMIME_Skills_Temp_SkillSwapped(_Caster, _Skill, _ResultSkill);
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:Skills:OnSkillUsedOnTarget] Swapped skill [",_Skill,"][",_SkillType,"][",_SkillElement,"] for [",_ResultSkill,"][",_ResultSkillType,"][",_ResultSkillElement,"].");
LLMIME_Mimicking_OnSkillCastOnTarget(_Caster, _Target, _ResultSkill, _ResultSkillType, _ResultSkillElement);
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill);

PROC
LLMIME_Mimicking_OnSkillUsedOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill)
THEN
LLMIME_Mimicking_OnSkillCastOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement);
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill);

PROC
LLMIME_Mimicking_OnSkillUsedOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill)
THEN
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _Target, _Skill);

PROC
LLMIME_Mimicking_OnSkillCastOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
_Caster != _Target
AND
LLMIME_Mimicking_QRY_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_GetTargetPriority(_Caster, _Target)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
AND
GetPosition(_Target, _TargetX, _TargetY, _TargetZ)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:CharacterUsedSkillOnTarget] Queueing up next mimic skill [",_Skill,"] on target.");
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, _TargetPriority, 1);

PROC
LLMIME_Mimicking_OnSkillCastOnTarget((CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
AND
_Caster == _Target
AND
LLMIME_Mimicking_QRY_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
AND
LLMIME_Mimicking_QRY_SkillIsSafeForRace(_Caster, _Mime, _Skill)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:CharacterUsedSkillOnTarget] Queueing up next mimic self-target skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextSelfAction(_Mime, _Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_OnSkillUsedAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill)
AND
DB_LLMIME_Mimicking_SkillSwap(_Skill, _ResultSkill, _ResultSkillType, _ResultSkillElement)
THEN
DB_LLMIME_Skills_Temp_SkillSwapped(_Caster, _Skill, _ResultSkill);
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:Skills:OnSkillUsedAtPosition] Swapped skill [",_Skill,"][",_SkillType,"][",_SkillElement,"] for [",_ResultSkill,"][",_ResultSkillType,"][",_ResultSkillElement,"].");
LLMIME_Mimicking_StartOnSkillCastAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _ResultSkill, _ResultSkillType, _ResultSkillElement);
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill);

PROC
LLMIME_Mimicking_OnSkillUsedAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill)
THEN
LLMIME_Mimicking_StartOnSkillCastAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType, _SkillElement);
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill);

PROC
LLMIME_Mimicking_OnSkillUsedAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill)
THEN
NOT DB_LLMIME_Skills_Temp_SkillResolved(_Caster, _TargetX, _TargetY, _TargetZ, _Skill);

//Delay by a frame or two so target events will fire first
PROC
LLMIME_Mimicking_StartOnSkillCastAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType, (STRING)_SkillElement)
AND
LLMIME_Mimicking_QRY_SkillIsAllowed(_Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
NOT DB_LLMIME_Timers_SkillCastAtPosition(_, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
GetUUID(_Caster, _CasterID)
AND
StringConcatenate("LLMIME_Events_SkillCastAtPosition_", _CasterID, _TimerName)
THEN
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);
TimerLaunch(_TimerName,5);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
AND
NOT LLMIME_Mimicking_QRY_SelfActionIsQueued(_Caster, _Skill, "Skill")
AND
NOT LLMIME_Mimicking_QRY_PositionActionIsQueued(_Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, 1)
THEN
LLMIME_Mimicking_OnSkillCastAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
THEN
NOT DB_LLMIME_Timers_SkillCastAtPosition(_TimerName, _Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType);

PROC
LLMIME_Mimicking_OnSkillCastAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, "shout")
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, "shout")
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:OnSkillCastAtPosition] Queuing up next mimic shout skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextSelfAction(_Mime, _Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_OnSkillCastAtPosition((CHARACTERGUID)_Caster, (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (STRING)_Skill, (STRING)_SkillType)
AND
_SkillType != "shout"
AND
DB_LLMIME_Mimicking_MimicNextAction(_Mime)
AND
LLMIME_Mimicking_QRY_CanMimicSourceSkill(_Skill, _Mime)
AND
LLMIME_Mimicking_QRY_CanMimicCharacter(_Caster, _Mime, _Skill, _SkillType)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:CharacterUsedSkillAtPosition] Queuing up next mimic positional skill [",_Skill,"].");
LLMIME_Mimicking_QueueNextAction(_Mime, _Caster, _Skill, "Skill", _TargetX, _TargetY, _TargetZ, -1);

//Some skills may not fire a "cast" event if the animation doesn't have a textkey for it
PROC
LLMIME_Mimicking_QueueNextAction((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (STRING)_Skill, "Skill", (REAL)_TargetX, (REAL)_TargetY, (REAL)_TargetZ, (INTEGER)_TargetPriority, (INTEGER)_IsTarget)
AND
NOT DB_LLMIME_Mimicking_CastFallbackTimer(_, _Caster, _Skill)
AND
GetUUID(_Caster, _ID)
AND
StringConcatenate(_Skill, _ID, _Str)
AND
StringConcatenate("LLMIME_Timers_CastFailTimer_", _Str, _TimerName)
THEN
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill);
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 2000);

IF
TimerFinished(_TimerName)
AND
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Skill);
LLMIME_Mimicking_Skills_OnSkillCast(_Caster, _Skill);

PROC
LLMIME_Mimicking_Skills_OnSkillCast((CHARACTERGUID)_Caster, (STRING)_Skill)
AND
DB_LLMIME_Skills_Temp_SkillSwapped(_Caster, _Skill, _ResultSkill)
AND
LLMIME_Mimicking_QRY_ActionIsQueued(_Caster, _ResultSkill, "Skill")
THEN
NOT DB_LLMIME_Skills_Temp_SkillSwapped(_Caster, _Skill, _ResultSkill);
DB_LLMIME_Skills_Temp_SkillCastResolved(_Caster, _Skill);
LLMIME_Mimicking_StartNextMimic(_Caster, _ResultSkill, "Skill");

PROC
LLMIME_Mimicking_Skills_OnSkillCast((CHARACTERGUID)_Caster, (STRING)_Skill)
AND
NOT DB_LLMIME_Skills_Temp_SkillCastResolved(_Caster, _Skill)
AND
LLMIME_Mimicking_QRY_ActionIsQueued(_Caster, _Skill, "Skill")
THEN
DB_LLMIME_Skills_Temp_SkillCastResolved(_Caster, _Skill);
LLMIME_Mimicking_StartNextMimic(_Caster, _Skill, "Skill");

PROC
LLMIME_Mimicking_Skills_OnSkillCast((CHARACTERGUID)_Caster, (STRING)_Skill)
AND
DB_LLMIME_Skills_Temp_SkillCastResolved(_Caster, _Skill)
THEN
NOT DB_LLMIME_Skills_Temp_SkillCastResolved(_Caster, _Skill);

PROC
LLMIME_Mimicking_StartNextMimic((CHARACTERGUID)_Caster, (STRING)_Action, (STRING)_ActionType)
AND
DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Action)
THEN
NOT DB_LLMIME_Mimicking_CastFallbackTimer(_TimerName, _Caster, _Action);
TimerCancel(_TimerName);
//END_REGION

//REGION SKILL_MIMIC
PROC
LLMIME_Mimicking_MimicAction((CHARACTERGUID)_Caster, (STRING)_Skill, "Skill")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill")
AND
LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking(_Mime, _Caster, _Skill, "Skill")
THEN
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill");
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:MimicAction] Skipping weapon mimicking for self-targeting skill [",_Skill,"].");
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Mime, 1, 1, 1);

PROC
LLMIME_Mimicking_MimicAction((CHARACTERGUID)_Caster, (STRING)_Skill, "Skill")
AND
DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill")
AND
ObjectGetFlag(_Mime, "Mimicry_IsMimicking", 0)
AND
NOT LLMIME_Mimicking_Skills_QRY_SkipWeaponMimicking(_Mime, _Caster, _Skill, "Skill")
THEN
NOT DB_LLMIME_Mimicking_MimicQueue_Self(_Mime, _Caster, _Skill, "Skill");
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:MimicAction] Mimicking weapons for self-targeting skill [",_Skill,"].");
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill);
LLMIME_Mimicking_Internal_StartWeaponMimicking(_Mime, _Caster, _Skill, "Skill");

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_WeaponMimickingStarted")
AND
DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Temp_CastSelfSkill(_Mime, _Caster, _Skill);
LLMIME_Mimicking_Internal_MimicWeapons(_Mime, _Caster, _Mime, _Skill, "Skill");

PROC
LLMIME_Mimicking_WeaponMimickingDone((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, (STRING)_ActionType)
AND
_Target == _Mime
THEN
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Mime, 1, 1, 1);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
AND
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
//AND
//NOT LLMIME_Mimicking_QRY_IsMovementSkill(_Skill)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Target, 1, 1, 1);

/*
PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
AND
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
AND
LLMIME_Mimicking_QRY_IsMovementSkill(_Skill)
AND
GetPosition(_Target, _x, _y, _z)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkillAtPosition(_Mime, _Skill, _x, _y, _z, 0, 1);
*/
//END_REGION

//REGION MIMIC_RUSH
/*
//Prevent rushing through walls
PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
AND
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
AND
StringContains(_Skill, "Rush_", 1)
AND
HasLineOfSight(_Mime, _Target, 1)
THEN
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Target, 1, 1, 1);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
AND
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
AND
StringContains(_Skill, "Rush_", 1)
AND
HasLineOfSight(_Mime, _Target, 0)
AND
IsTagged(_Target, "LLMIME_Dummy", 1)
AND
ObjectIsCharacter(_Target, 1)
THEN
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, (GUIDSTRING)_Target, _Skill);
LeaderLib_Timers_StartCharacterCharacterTimer(_Mime, (CHARACTERGUID)_Target, 25, "LLMIME_Timers_Skills_CheckRushLineOfSight_", "LLMIME_Internal_CheckRushLineOfSight");
CharacterMoveTo((CHARACTERGUID)_Target, _Mime, 0, "LLMIME_Mimicking_SkillDummyArrived", 0);

IF
CharacterCharacterEvent(_Mime, _Target, "LLMIME_Internal_CheckRushLineOfSight")
AND
HasLineOfSight(_Mime, _Target, 1)
AND
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill);
CharacterPurgeQueue(_Target);
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Target, 1, 1, 1);

PROC
LLMIME_Mimicking_MimicActionOnTarget((CHARACTERGUID)_Mime, (CHARACTERGUID)_Caster, (GUIDSTRING)_Target, (STRING)_Skill, "Skill")
AND
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
AND
StringContains(_Skill, "Rush_", 1)
AND
HasLineOfSight(_Mime, _Target, 0)
AND
IsTagged(_Target, "LLMIME_Dummy", 1)
AND
ObjectIsItem(_Target, 1)
AND
GetPosition(_Mime, _x, _y, _z)
THEN
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, (GUIDSTRING)_Target, _Skill);
LeaderLib_Timers_StartCharacterItemTimer(_Mime, (ITEMGUID)_Target, 25, "LLMIME_Timers_Skills_CheckRushLineOfSight_", "LLMIME_Internal_CheckRushLineOfSight");
ItemMoveToPosition((ITEMGUID)_Target, _x, _y, _z, 6.0, 0.0, "LLMIME_Mimicking_SkillDummyArrived", 0);

IF
CharacterItemEvent(_Mime, _Target, "LLMIME_Internal_CheckRushLineOfSight")
AND
HasLineOfSight(_Mime, _Target, 1)
AND
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill)
AND
GetPosition(_Target, _x, _y, _z)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill);
ItemMoveToPosition(_Target, _x, _y, _z, 6.0, 0.0, "", 0);
ObjectSetFlag(_Mime, "Mimicry_IsMimicking");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 2500);
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
CharacterUseSkill(_Mime, _Skill, _Target, 1, 1, 1);

IF
CharacterItemEvent(_Mime, _Target, "LLMIME_Internal_CheckRushLineOfSight")
AND
HasLineOfSight(_Mime, _Target, 0)
AND
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill)
THEN
LeaderLib_Timers_StartCharacterItemTimer(_Mime, _Target, 25, "LLMIME_Timers_Skills_CheckRushLineOfSight_", "LLMIME_Internal_CheckRushLineOfSight");

IF
StoryEvent(_Target, "LLMIME_Mimicking_SkillDummyArrived")
AND
DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_RushLineOfSightCheck(_Mime, _Caster, _Target, _Skill);
*/
//END_REGION

//REGION SUCCESS
PROC
ProcObjectTimerFinished((CHARACTERGUID)_Mime, "LLMIME_Timers_CastFailTimer")
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
LeaderLog_Log("DEBUG", "[Mimicry:Mimicking:Skills:ProcObjectTimerFinished(LLMIME_Timers_CastFailTimer)] Mimicked skill [",_Skill,"] never fired the SkillCast event. Completing.");
LLMIME_Mimicking_OnMimicSuccess(_Mime, _Caster, _Skill, "Skill");
//END_REGION

//REGION CLEAR_MIME
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_ClearMimeBonuses")
THEN
DB_NOOP(1);
//END_REGION

//REGION SCRIPT_DISABLED
PROC
LLMIME_Mimicking_OnScriptsCompleting()
THEN
//SysClear("DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget", 4);
//SysClear("DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition", 6);
SysClear("DB_LLMIME_Mimicking_Temp_TargetPriority", 3);
SysClear("DB_LLMIME_Timers_SkillCastAtPosition", 7);
SysClear("DB_LLMIME_Mimicking_CastFallbackTimer", 3);
SysClear("DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess", 3);
SysClear("DB_LLMIME_Mimicking_Temp_CastSelfSkill", 3);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"