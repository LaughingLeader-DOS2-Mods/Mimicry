Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
PROC
LeaderLib_Debug_RegisterDebugPlayer()
THEN
DebugBreak("[LLMIME_08_0_DebugLevel:] LeaderLib_Debug_RegisterDebugPlayer] Adding Mime to [DB_Origins].");
//SysClear("DB_Origins", 1);

IF
GameModeStarted(_,1)
THEN
SysActivateGoal("LLMIME_08_1_DebugLevel_Main");
SysActivateGoal("LLMIME_02_Passives_TS_StatusEvents");

IF
GameStarted(_,1)
THEN
SysActivateGoal("LLMIME_08_1_DebugLevel_Main");
SysActivateGoal("LLMIME_02_Passives_TS_StatusEvents");

IF
GameStarted("LeaderLib_GreenScreen",1)
THEN
TimerLaunch("LLMIME_Debug_Start", 500);

IF
TimerFinished("LLMIME_Debug_Start")
AND
CharacterGetHostCharacter(_Player)
AND
NOT GetItemForItemTemplateInInventory(_Player, "UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb", _)
THEN
ItemTemplateAddTo("UNIQUE_LLMIME_MusicBox_fd03b00c-5366-40e2-a8af-1d9f6a904ecb", _Player, 1);

//REGION TEXT_EVENTS
IF
TextEventSet("llmime_addskills")
AND
DB_LLMIME_Mime_LearnableSkills(_Skill)
AND
DB_IsPlayer(_Player)
THEN
CharacterAddSkill(_Player, _Skill, 1);

IF
TextEventSet("llmime_Tagtest")
AND
CharacterGetHostCharacter(_Host)
AND
GetPosition(_Host, _x, _y, _z)
AND
TemporaryCharacterCreateAtPosition(_x, _y, _z, "LLMIME_Dummy_Character_e2f3f6f7-c069-4928-830e-f1d46e2c898c", 0, _Dummy)
AND
IsTagged(_Dummy, "LeaderLib_Dummy", _IsDummy)
THEN
LeaderLog_LogInt("DEBUG", "[LLMIME_08_1_DebugLevel_Main:TagTest] Before transforming, dummy tag is [", _IsDummy, "].");
ObjectSetFlag(_Dummy, "LEADERLIB_IGNORE", 0);
CharacterTransformFromCharacter(_Dummy, _Host, 0, 0, 0, 1, 0, 0, 1);
ProcObjectTimer(_Dummy, "LLMIME_Timers_Debug_TagTest", 250);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Dummy, "LLMIME_Timers_Debug_TagTest")
AND
IsTagged(_Dummy, "LeaderLib_Dummy", _IsDummy)
AND
ObjectGetFlag(_Dummy, "LEADERLIB_IGNORE", _Ignored)
THEN
LeaderLog_LogInt("DEBUG", "[LLMIME_08_1_DebugLevel_Main:TagTest] After transforming, dummy tag is [", _IsDummy, "], and flag [LEADERLIB_IGNORE] is [",_Ignored,"].");
RemoveTemporaryCharacter(_Dummy);

/*
IF
TextEventSet("mimicdummy")
AND
DB_LLMIME_MimicNextAction(_Mime)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition("LLMIME_TargetDummy_7384531c-c7ba-45df-ad84-909fa0208047", _x, _y, _z, _Dummy)
//CreateItemTemplateAtPosition("LLMIME_CONT_Barrel_A_Water_a2573a2d-5556-4c82-b004-aa7d5130b1e6", _x, _y, _z, _Dummy)
THEN
DebugBreak("[LLMIME:Position] Spawned skill dummy.");
*/

IF
TextEventSet("llmime_setup")
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
THEN
CharacterAddAttribute(_Player, "Memory", 10);

IF
TextEventSet("llmime_setup")
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
AND
DB_LLMIME_Mime_Skills(_Skill)
THEN
CharacterAddSkill(_Player, _Skill, 0);

IF
TextEventSet("llmime_setup")
AND
DB_IsPlayer(_Player)
AND
CharacterIsControlled(_Player, 1)
AND
DB_LLMIME_Mime_LearnableSkills(_Skill)
THEN
CharacterAddSkill(_Player, _Skill, 0);

IF
TextEventSet("mimicdupe")
AND
GlobalGetFlag("LLMIME_DUPLICATE_ITEMS", _Val)
AND
IntegerSubtract(1, _Val, _NextVal)
THEN
LLMIME_Debug_SetGlobalFlag("LLMIME_DUPLICATE_ITEMS", _NextVal);

PROC
LLMIME_Debug_SetGlobalFlag((STRING)_Flag, 0)
AND
StringConcatenate(_Flag, " cleared.", _Message)
THEN
DebugBreak(_Message);
GlobalClearFlag(_Flag);

PROC
LLMIME_Debug_SetGlobalFlag((STRING)_Flag, 1)
AND
StringConcatenate(_Flag, " set.", _Message)
THEN
DebugBreak(_Message);
GlobalSetFlag(_Flag);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
