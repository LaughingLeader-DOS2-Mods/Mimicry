Version 1
SubGoalCombiner SGC_AND
INITSECTION

LLMIME_ToggleScripts_InitSettings();
KBSECTION
/*
Some of the mimicking events can be quite broad, so this script is used to disable them when nobody is mimicking.
*/

PROC
LLMIME_ToggleScripts_InitSettings()
THEN
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Skills_01_MimeSkills", "LLMIME_TS_GameHasActiveMime");
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_PassiveStatuses", "LLMIME_TS_GameHasActiveMime");
//This needs to be active independent of the mimicking scripts, as it applies/removes mimicking
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Mimicking_01_Start", "LLMIME_TS_GameHasActiveMime");

//Mimicking scripts
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Mimicking_02_Main", "LLMIME_TS_MimeIsMimicking");
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Mimicking_03_Skills", "LLMIME_TS_MimeIsMimicking");
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Mimicking_04_Attacking", "LLMIME_TS_MimeIsMimicking");
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Mimicking_05_Items", "LLMIME_TS_MimeIsMimicking");

//Mime follower script
LeaderLib_ToggleScripts_Register_Script("LLMIME_10_TS_Follower_01_Recruited", "LLMIME_TS_MimeFollowerInParty");

//REGION SCRIPT_TOGGLING
PROC
LLMIME_Mimicking_OnMimickingEnabled((CHARACTERGUID)_Mime)
THEN
TimerCancel("LLMIME_Timers_DisableMimickingScripts");
LLMIME_ScriptToggler_ToggleScriptsOn();

PROC
LLMIME_ScriptToggler_ToggleScriptsOn()
AND
NOT DB_GlobalFlag("LLMIME_TS_MimeIsMimicking")
THEN
GlobalSetFlag("LLMIME_TS_MimeIsMimicking");
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:ToggleScriptsOn] Enabled mimicking scripts via global flag [LLMIME_TS_MimeIsMimicking].");

//Delay disabling scripts so they have time to clear data after this event
IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_ClearMimeBonuses")
AND
DB_GlobalFlag("LLMIME_TS_MimeIsMimicking")
AND
NOT DB_LLMIME_Mimicking_MimicNextAction(_)
THEN
DB_LLMIME_ScriptToggler_Temp_BlockScriptDisable(1);
TimerCancel("LLMIME_Timers_ResetScriptToggleBlocker");
TimerLaunch("LLMIME_Timers_ResetScriptToggleBlocker", 500);

IF
TimerFinished("LLMIME_Timers_ResetScriptToggleBlocker")
AND
DB_GlobalFlag("LLMIME_TS_MimeIsMimicking")
AND
NOT DB_LLMIME_Mimicking_MimicNextAction(_)
THEN
LLMIME_ScriptToggler_ToggleScriptsOff();

QRY
LLMIME_ScriptToggler_QRY_CharacterNotMimicking((CHARACTERGUID)_Character)
AND
NOT LLMIME_QRY_CharacterIsAMime(_Character)
THEN
DB_NOOP(1);

QRY
LLMIME_ScriptToggler_QRY_CharacterNotMimicking((CHARACTERGUID)_Character)
AND
CharacterIsInCombat(_Character, 0)
THEN
DB_NOOP(1);

QRY
LLMIME_ScriptToggler_QRY_CharacterNotMimicking((CHARACTERGUID)_Character)
AND
CharacterIsDead(_Character, 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Mimicking_OnMimickingDisabled((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Mimicking_MimicNextAction(_)
AND
LLMIME_ScriptToggler_QRY_CharacterNotMimicking(_Mime)
THEN
LLMIME_ScriptToggler_ToggleScriptsOff();

PROC
LLMIME_ScriptToggler_ToggleScriptsOff()
AND
NOT DB_LLMIME_ScriptToggler_Temp_BlockScriptDisable(1)
AND
DB_GlobalFlag("LLMIME_TS_MimeIsMimicking")
THEN
TimerCancel("LLMIME_Timers_DisableMimickingScripts");
TimerLaunch("LLMIME_Timers_DisableMimickingScripts", 2500);
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:ToggleScriptsOff] Starting script disable timer.");

IF
TimerFinished("LLMIME_Timers_DisableMimickingScripts")
THEN
LLMIME_ScriptToggler_Internal_DisableScripts();

PROC
LLMIME_ScriptToggler_Internal_DisableScripts()
AND
NOT DB_LLMIME_ScriptToggler_Temp_BlockScriptDisable(1)
AND
DB_GlobalFlag("LLMIME_TS_MimeIsMimicking")
THEN
GlobalClearFlag("LLMIME_TS_MimeIsMimicking");
LLMIME_Mimicking_OnScriptsCompleting();
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:ToggleScriptsOff] No mimes are mimicking. Disabled mimicking scripts.");

PROC
LLMIME_ScriptToggler_Internal_DisableScripts()
AND
NOT DB_LLMIME_ScriptToggler_Temp_BlockScriptDisable(1)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
THEN
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:ToggleScriptsOff] Skipped disabling scripts - Mimes are mimicking again.");
//END_REGION

//REGION MIME_SKILL_SCRIPT_TOGGLE
PROC
LLMIME_ScriptToggler_AddActiveMime((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_ActiveMimes(_Mime)
THEN
DB_LLMIME_ActiveMimes(_Mime);

PROC
LLMIME_ScriptToggler_AddActiveMime((CHARACTERGUID)_Mime)
AND
NOT DB_GlobalFlag("LLMIME_TS_GameHasActiveMime")
THEN
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler] Activating mime scripts via global flag [LLMIME_TS_GameHasActiveMime].");
LeaderLib_ToggleScripts_EnableScript("LeaderLib_StatusEvents_DisableScriptEnabled", "Mimicry");
GlobalSetFlag("LLMIME_TS_GameHasActiveMime");

PROC
LLMIME_ScriptToggler_RemoveActiveMime((CHARACTERGUID)_Mime)
THEN
NOT DB_LLMIME_ActiveMimes(_Mime);
LLMIME_ScriptToggler_DisableSkillScript();

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_MimeInitialized")
AND
CharacterIsPartyMember(_Mime, 1)
THEN
LLMIME_ScriptToggler_AddActiveMime(_Mime);

PROC
LLMIME_ScriptToggler_AddActiveMime((CHARACTERGUID)_Mime)
THEN
TimerCancel("LLMIME_Timers_DisableMimeScripts");

IF
StoryEvent((CHARACTERGUID)_Mime, "Mimicry_MimeJobRemoved")
THEN
LLMIME_ScriptToggler_RemoveActiveMime(_Mime);

PROC
LLMIME_ScriptToggler_DisableSkillScript()
AND
NOT DB_LLMIME_ActiveMimes(_)
AND
DB_GlobalFlag("LLMIME_TS_GameHasActiveMime")
THEN
TimerCancel("LLMIME_Timers_DisableMimeScripts");
TimerLaunch("LLMIME_Timers_DisableMimeScripts", 2500);
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:Mimicry_MimeJobRemoved] Starting mime script disable timer.");

IF
TimerFinished("LLMIME_Timers_DisableMimeScripts")
AND
NOT DB_LLMIME_ActiveMimes(_)
THEN
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:LLMIME_Timers_DisableMimeScripts] Clearing global flag [LLMIME_TS_GameHasActiveMime].");
GlobalClearFlag("LLMIME_TS_GameHasActiveMime");
LeaderLib_ToggleScripts_DisableAllStatusEvents("Mimicry");
//END_REGION

//REGION MIME_FOLLOWER
PROC
LLMIME_Follower_OnMimeFollowerRecruited((CHARACTERGUID)_Mime)
THEN
TimerCancel("LLMIME_Timers_DisableMimeFollowerScripts");

PROC
LLMIME_Follower_OnMimeFollowerRecruited((CHARACTERGUID)_Mime)
AND
NOT DB_GlobalFlag("LLMIME_TS_MimeFollowerInParty")
THEN
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler] Activated script [LLMIME_10_TS_Follower_01_Recruited].");
GlobalSetFlag("LLMIME_TS_MimeFollowerInParty");
LLMIME_ScriptToggler_AddActiveMime(_Mime);

PROC
LLMIME_Follower_OnMimeFollowerDismissed((CHARACTERGUID)_Mime)
AND
DB_GlobalFlag("LLMIME_TS_MimeFollowerInParty")
THEN
LLMIME_ScriptToggler_RemoveActiveMime(_Mime);
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler] Disabling mime follower scripts after delay.");
TimerCancel("LLMIME_Timers_DisableMimeFollowerScripts");
TimerLaunch("LLMIME_Timers_DisableMimeFollowerScripts", 2000);

IF
TimerFinished("LLMIME_Timers_DisableMimeFollowerScripts")
AND
DB_LLMIME_Follower_Mime(_Mime)
AND
NOT DB_LeaderLib_PartyFollower(_, _Mime)
THEN
LeaderLog_Log("SYSTEM", "[Mimicry:ScriptToggler:LLMIME_Timers_DisableMimeScripts] Clearing global flag [LLMIME_TS_MimeFollowerInParty].");
GlobalClearFlag("LLMIME_TS_MimeFollowerInParty");
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"
