Version 1
SubGoalCombiner SGC_AND
INITSECTION
//ApplyStatus(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f, "LLMIME_CC_MASK_EFFECT", -1.0, 1, S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f);

KBSECTION

//REGION USER_EVENTS
IF
DB_AssignedDummyForUser(_UserName, _Dummy)
AND
CharacterGetReservedUserID(_Dummy, _UserID)
THEN
LLMIME_CC_SetCCIndex(_UserID, _UserName, _Dummy);

IF
UserConnected(_UserID, _UserName, _UserProfileID)
AND
DB_AssignedDummyForUser(_UserName, _Dummy)
THEN
LLMIME_CC_SetCCIndex(_UserID, _UserName, _Dummy);

//After a user is disconnected / a dummy is made available
IF
DB_AvailableDummy(_Dummy)
THEN
LLMIME_CC_Internal_ClearUserID(_Dummy);
//END_REGION

//REGION CC_USER_INDEX
QRY
LLMIME_CC_Internal_QRY_ClearUserID((CHARACTERGUID)_Dummy)
THEN
LLMIME_CC_Internal_ClearUserID(_Dummy);

PROC
LLMIME_CC_Internal_ClearUserID((CHARACTERGUID)_Dummy)
AND
DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, _Index)
THEN
NOT DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, _Index);

PROC
LLMIME_CC_SetCCIndex((INTEGER)_UserID, (STRING)_UserName, (CHARACTERGUID)_Dummy)
AND
_Dummy == S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406
AND
LLMIME_CC_Internal_QRY_ClearUserID(_Dummy)
THEN
DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, 1);

PROC
LLMIME_CC_SetCCIndex((INTEGER)_UserID, (STRING)_UserName, (CHARACTERGUID)_Dummy)
AND
_Dummy == S_GLO_CharacterCreationDummy_002_361dacdc-4135-4d3f-a9a2-3cad46ca246a
AND
LLMIME_CC_Internal_QRY_ClearUserID(_Dummy)
THEN
DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, 2);

PROC
LLMIME_CC_SetCCIndex((INTEGER)_UserID, (STRING)_UserName, (CHARACTERGUID)_Dummy)
AND
_Dummy == S_GLO_CharacterCreationDummy_003_dded8c22-b28e-45c1-a074-eb0954602c8a
AND
LLMIME_CC_Internal_QRY_ClearUserID(_Dummy)
THEN
DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, 3);

PROC
LLMIME_CC_SetCCIndex((INTEGER)_UserID, (STRING)_UserName, (CHARACTERGUID)_Dummy)
AND
_Dummy == S_GLO_CharacterCreationDummy_004_5f93cae7-6c10-4da1-b9a5-0efafc168c8e
THEN
DB_LLMIME_CC_Temp_Index(_Dummy, _UserID, 4);
//END_REGION

PROC
PROC_SetupOriginCharacters((GUIDSTRING)_Dest)
AND
ObjectExists(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f, 1)
THEN
TimerCancel("Timers_LLMIME_CC_SetCustomRaceData");
TimerLaunch("Timers_LLMIME_CC_SetCustomRaceData", 25);

/* [OSITOOLS_ONLY]
IF
TimerFinished("Timers_LLMIME_CC_SetCustomRaceData")
AND
CharacterGetRace(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f, 0, "LLMIME_DarkElf")
THEN
NRD_PlayerSetCustomDataString(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f, "Race", "Elf");

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, (STRING)_NextPreset)
AND
CharacterGetRace(_Player, 0, "LLMIME_DarkElf")
THEN
NRD_PlayerSetCustomDataString(_Player, "Race", "Elf");
*/

// Initial Origin Selection
IF
ObjectTransformed((CHARACTERGUID)_Player, LLMIME_Elves_Hero_Male_Mime_01acaf9e-866d-450e-9969-49dab771d973)
THEN
SetTag(_Player, "LLMIME_DARKELF");
LLMIME_CC_Internal_EquipArmor(_Player);

IF
SkillActivated(_Player, _Skill)
AND
GetTemplate(_Player, "LLMIME_Elves_Hero_Male_Mime_01acaf9e-866d-450e-9969-49dab771d973")
AND
NOT LeaderLib_Helper_QRY_HasItemTemplateInSlot(_Player, "Breast", "EQ_UNIQUE_Armor_LLMIME_Mime_ShadowArmor_UpperBody_A_ab189632-0769-4201-9e0e-afaa0e71172c")
THEN
SetTag(_Player, "LLMIME_DARKELF");
LLMIME_CC_Internal_EquipArmor(_Player);

/*
PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, (STRING)_NextPreset)
AND
_NextPreset != "LLMIME_Mime"
AND
//IsTagged(_Player, "LLMIME_MimeOrigin", 1)
String(_Player, _PlayerStr)
AND
StringContains(_PlayerStr, "_Mime_", 1)
THEN
SetTag(_Player, "LLMIME_DARKELF");
LLMIME_CC_Internal_EquipArmor(_Player);
*/

PROC
LLMIME_CC_Internal_EquipArmor((CHARACTERGUID)_Player)
AND
GetPosition(_Player, _x, _y, _z)
AND
DB_LLMIME_DarkElf_ShadowArmor(_Slot, _Template)
AND
CreateItemTemplateAtPosition(_Template, _x, _y, _z, _Armor)
THEN
CharacterEquipItem(_Player, _Armor);

// Mime Mask
PROC
LLMIME_CC_Internal_EquipArmor((CHARACTERGUID)_Player)
AND
CharacterGetOrigin(_Player, 1, "LLMIME_TheMime")
AND
GetPosition(_Player, _x, _y, _z)
AND
CreateItemTemplateAtPosition("fe7d25d5-290e-4014-b446-b18437741bd5", _x, _y, _z, _Armor)
THEN
CharacterEquipItem(_Player, _Armor);

/*
PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, (STRING)_LastPreset, "LLMIME_Mime")
AND
LLMIME_Origin_QRY_IsOriginMime(_Player)
THEN
LLMIME_CC_Internal_PlayMaskEffect(_Player, -1.0);

PROC
LeaderLib_CC_OnPresetChanged((CHARACTERGUID)_Player, "LLMIME_Mime", (STRING)_NextPreset)
AND
LLMIME_Origin_QRY_IsOriginMime(_Player)
AND
_NextPreset != "LLMIME_Mime"
THEN
LLMIME_CC_Internal_StopMaskEffect(_Player);
*/

IF
TimerFinished("LeaderLib_Timers_PostCC_Finished")
THEN
LLMIME_CC_ClearData();
GoalCompleted;

PROC
LLMIME_CC_ClearData()
THEN
SysClear("DB_LLMIME_CC_Temp_MaskFXHandle", 2);
SysClear("DB_LLMIME_CC_Temp_ActivePortraitMask", 3);
SysClear("DB_LLMIME_CC_Temp_Index", 3);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLMIME_21_CC"