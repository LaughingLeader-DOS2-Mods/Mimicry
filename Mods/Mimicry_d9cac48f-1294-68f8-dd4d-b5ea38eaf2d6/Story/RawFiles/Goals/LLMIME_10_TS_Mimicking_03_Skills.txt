Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_LLMIME_Mimicking_Temp_CasterWeapons(_Caster, _MainHand, _Offhand)
//DB_LLMIME_Mimicking_Temp_SkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType)
//DB_LLMIME_Mimicking_Temp_SkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType)
KBSECTION

//REGION SKILL_TRACKING
IF
CharacterUsedSkillOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority)
THEN
NOT DB_LLMIME_Mimicking_Temp_TargetPriority(_Caster, _Target, _TargetPriority);

IF
CharacterUsedSkillOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
THEN
LLMIME_Mimicking_OnSkillUsedOnTarget(_Caster, _Target, _Skill, _SkillType, _SkillElement);

IF
CharacterUsedSkillAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType, _SkillElement)
AND
LLMIME_Mimicking_QRY_MimesAvailable()
AND
LLMIME_Mimicking_QRY_CharacterIsMimickable(_Caster)
THEN
LLMIME_Mimicking_OnSkillUsedAtPosition(_Caster, _TargetX, _TargetY, _TargetZ, _Skill, _SkillType, _SkillElement);

IF
SkillCast(_Caster, _Skill, _SkillType, _SkillElement)
THEN
LLMIME_Mimicking_Skills_OnSkillCast(_Caster, _Skill);
//END_REGION

IF
CharacterUsedSkill(_Mime, _Skill, _SkillType, _SkillElement)
AND
ObjectGetFlag(_Mime, "Mimicry_IsMimicking", 1)
AND
IsSourceSkill(_Skill, 1)
AND
HasActiveStatus(_Mime, "LLMIME_MIMICKING_SOURCE", 1)
THEN
RemoveStatus(_Mime, "LLMIME_MIMICKING_SOURCE");

//REGION SUCCESS
IF
CharacterUsedSkill(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:Skills:CharacterUsedSkill] Mimicked skill [",_Skill,"] was used. Starting cast fail timer.");
LLMIME_Mimicking_ResetFailTimer(_Mime, _Caster, _Skill, "Skill", 3000);
ProcObjectTimer(_Mime, "LLMIME_Timers_CastFailTimer", 1500);

IF
SkillCast(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Skill, "Skill")
THEN
NOT DB_LLMIME_Mimicking_Temp_ResetDummyAfterMimicAction(_Mime, _Target, _Skill, "Skill");
LLMIME_Dummy_ResetDummyAfterDelay((GUIDSTRING)_Target, 2000);

IF
SkillCast(_Mime, _Skill, _SkillType, _SkillElement)
AND
DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill)
THEN
NOT DB_LLMIME_Mimicking_Skills_Temp_ListenForSuccess(_Mime, _Caster, _Skill);
LeaderLog_Log("DEBUG", "[LLMIME:Mimicking:Skills:SkillCast] Mimicked skill [",_Skill,"] was cast.");
ProcObjectTimerCancel(_Mime, "LLMIME_Timers_CastFailTimer");
LLMIME_Mimicking_OnMimicSuccess(_Mime, _Caster, _Skill, "Skill");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLMIME_10_ToggleScripts"
