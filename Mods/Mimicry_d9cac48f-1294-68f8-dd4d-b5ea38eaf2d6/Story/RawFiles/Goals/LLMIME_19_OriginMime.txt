Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LeaderLib_OriginSkills("Mimicry_MimeOrigin", "Target_LLMIME_PerfectDoppleganger");
KBSECTION

IF
GameModeStarted("Campaign", _IsEditorMode)
THEN
//LLMIME_Origin_RegisterMime();
DB_NOOP(1);

PROC
LLMIME_Origin_RegisterMime()
THEN
DB_NOOP(1);
//When these DB entries are added, the tags are immediately applied to the character, so register once the game has started
//DB_OriginProfessionTags((CHARACTERGUID)S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f,"SOLDIER");
//DB_OriginProfessionTags((CHARACTERGUID)S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f,"JESTER");
//DB_OriginProfessionTags((CHARACTERGUID)S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b,"SOLDIER");
//DB_OriginProfessionTags((CHARACTERGUID)S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b,"JESTER");

//Impportant so this character can be turned into an NPC again
//DB_OriginNPCAlignment((CHARACTERGUID)S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f, "Good NPC");
//DB_OriginNPCAlignment((CHARACTERGUID)S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b, "Good NPC");

/*
//Origins is done registering
IF
DB_Origins((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe)
THEN
DB_Origins((CHARACTERGUID)S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f);
*/

QRY
LLMIME_Origin_QRY_IsOriginMime((GUIDSTRING)_Character)
AND
IsTagged(_Character, "Mimicry_MimeOrigin", 1)
THEN
DB_NOOP(1);

QRY
LLMIME_Origin_QRY_IsOriginMime((GUIDSTRING)_Character)
AND
_Character == S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f
THEN
DB_NOOP(1);

QRY
LLMIME_Origin_QRY_IsOriginMime((GUIDSTRING)_Character)
AND
_Character == S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b
THEN
DB_NOOP(1);

PROC
PROC_Shared_CharacterCreationFinished()
AND
NOT DB_GlobalFlag("LeaderLib_IsEditorMode")
AND
DB_IsPlayer(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f)
THEN
LLMIME_Origin_Setup(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f);

PROC
PROC_Shared_CharacterCreationFinished()
AND
NOT DB_GlobalFlag("LeaderLib_IsEditorMode")
AND
DB_IsPlayer(S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b)
THEN
LLMIME_Origin_Setup(S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b);

PROC
LLMIME_Origin_Setup((CHARACTERGUID)_Mime)
AND
ObjectExists(_Mime, 1)
THEN
LeaderLib_ToggleScripts_EnableScript("LLMIME_TS_OriginMimeActive", "Mimicry");
SetTag(_Mime,"GENERIC");
SetTag(_Mime,"REALLY_GENERIC");
LLMIME_Origin_CreateMimeMask(_Mime);
ProcObjectTimerCancel(_Mime, "LLMIME_Timers_Origin_EquipMask");
ProcObjectTimer(_Mime, "LLMIME_Timers_Origin_EquipMask", 250);

/* [OSITOOLS_ONLY]
PROC
LLMIME_Origin_Setup((CHARACTERGUID)_Mime)
AND
ObjectExists(_Mime, 1)
AND
CharacterGetRace(_Mime, 0, "LLMIME_DarkElf")
THEN
NRD_PlayerSetCustomDataString(_Mime, "Race", "Undead_Elf");
*/

PROC
LLMIME_Origin_Debug_SetupMime((CHARACTERGUID)_Mime)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Debug:Origin:SetupMime] Applying henchman preset to mime.");
CharacterApplyHenchmanPreset(_Mime, "LLMIME_MimeOrigin");
CharacterAddSkill(_Mime, "Target_LLMIME_PerfectDoppelganger", 0);
CharacterAddSkill(_Mime, "Shout_FleshSacrifice", 0);

QRY
LLMIME_Origin_MaskIsInInventory((CHARACTERGUID)_Mime)
AND
ItemTemplateIsInCharacterInventory(_Mime, "EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _Count)
AND
_Count > 1
THEN
DB_NOOP(1);

//Editor bug - Mask may be a thing without existing
QRY
LLMIME_Origin_MaskExists((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _Mask)
AND
ObjectExists(_Mask, 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Origin_Debug_DeleteNonExistentMask((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _Mask)
AND
ObjectExists(_Mask, 0)
THEN
ItemRemove(_Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:Debug:DeleteNonExistentMask] Deleted a non-existing mask.");

PROC
LLMIME_Origin_Debug_RemovePresetMasks((CHARACTERGUID)_Mime)
AND
ItemTemplateIsInCharacterInventory(_Mime, "EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Count)
AND
_Count > 0
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Origin:Debug:RemovePresetMasks] Removed Mime preset masks from origin character.");
ItemTemplateRemoveFrom("EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mime, _Count);

PROC
LLMIME_Origin_CreateMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_GlobalFlag("LeaderLib_IsEditorMode")
AND
NOT LLMIME_Origin_MaskIsInInventory(_Mime)
THEN
ItemTemplateAddTo("EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _Mime, 1, 0);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:CreateMimeMask] Adding mask to the inventory of Mime.");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Mime, "LLMIME_Timers_Origin_EquipMask")
THEN
LLMIME_Origin_Debug_RemovePresetMasks(_Mime);
LLMIME_Origin_EquipMimeMask(_Mime);

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _Mask)
AND
ObjectExists(_Mask, 1)
THEN
CharacterEquipItem(_Mime, _Mask);
ItemLockUnEquip(_Mask, 1);
DB_LLMIME_Origin_MaskEquipped(_Mime, _Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] Equipped and locked mask to the mime origin character.");

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Origin_MaskEquipped(_Mime, _)
AND
NOT LLMIME_Origin_MaskExists(_Mime)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition("EQ_UNIQUE_Armor_LLMIME_Mime_Mask_Origin_A_4afe8cd3-eb9f-4760-9ba4-a0b4433a4c06", _x, _y, _z, _Mask)
THEN
LLMIME_Origin_Debug_DeleteNonExistentMask(_Mime);
CharacterEquipItem(_Mime, _Mask);
ItemLockUnEquip(_Mask, 1);
DB_LLMIME_Origin_MaskEquipped(_Mime, _Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] Found mask had issues. Created a new one at the mime's position and equipped it.");

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Origin_MaskEquipped(_Mime, _)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] [ERROR] Mask was not equipped!");

IF
DB_LLMIME_Origin_MaskEquipped(_Mime, _Mask)
THEN
SetStoryEvent(_Mime, "Mimicry_Events_Mime_MaskEquipped");
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_Mimicry"