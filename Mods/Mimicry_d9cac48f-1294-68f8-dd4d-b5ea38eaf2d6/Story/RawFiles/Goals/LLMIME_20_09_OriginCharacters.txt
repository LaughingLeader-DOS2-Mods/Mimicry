Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LeaderLib_OriginSkills("Mimicry_MimeOrigin", "Target_LLMIME_PerfectDoppleganger");
LLMIME_Origin_Setup(S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f);
LLMIME_Origin_Setup(S_Player_LLMIME_MimeF_45299363-87bb-4e78-ad0d-787dacc2659b);


KBSECTION

PROC
LLMIME_Origin_Setup((CHARACTERGUID)_Mime)
AND
ObjectExists(_Mime, 1)
THEN
SetTag(_Mime,"GENERIC");
SetTag(_Mime,"REALLY_GENERIC");
LLMIME_Origin_Debug_SetupMime(_Mime);
LLMIME_Origin_CreateMimeMask(_Mime);
ProcObjectTimer(_Mime, "LLMIME_Timers_Origin_EquipMask", 250);

PROC
LLMIME_Origin_Debug_SetupMime((CHARACTERGUID)_Mime)
AND
DB_GlobalFlag("LeaderLib_IsEditorMode")
THEN
CharacterApplyHenchmanPreset(_Mime, "LLMIME_Mime");
CharacterAddSkill(_Mime, "Target_LLMIME_PerfectDoppelganger", 0);
CharacterAddSkill(_Mime, "Shout_FleshSacrifice", 0);

QRY
LLMIME_Origin_MaskIsInInventory((CHARACTERGUID)_Mime)
AND
ItemTemplateIsInCharacterInventory(_Mime, "EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Count)
AND
_Count > 1
THEN
DB_NOOP(1);

//Editor bug - Mask may be a thing without existing
QRY
LLMIME_Origin_MaskExists((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mask)
AND
ObjectExists(_Mask, 1)
THEN
DB_NOOP(1);

PROC
LLMIME_Origin_Debug_DeleteNonExistentMask((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mask)
AND
ObjectExists(_Mask, 0)
THEN
ItemRemove(_Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:Debug:DeleteNonExistentMask] Deleted a non-existing mask.");

PROC
LLMIME_Origin_CreateMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_GlobalFlag("LeaderLib_IsEditorMode")
AND
NOT LLMIME_Origin_MaskIsInInventory(_Mime)
THEN
ItemTemplateAddTo("EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mime, 1, 0);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:CreateMimeMask] Adding mask to the inventory of Mime.");

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Mime, "LLMIME_Timers_Origin_EquipMask")
THEN
LLMIME_Origin_EquipMimeMask(_Mime);

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
GetItemForItemTemplateInInventory(_Mime, "EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _Mask)
AND
ObjectExists(_Mask, 1)
THEN
CharacterEquipItem(_Mime, _Mask);
ItemLockUnEquip(_Mask, 1);
DB_LLMIME_Origin_MaskEquipped(_Mime, _Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] Equipped and locked mask to the mime origin character.");

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Origin_MaskEquipped(_Mime, _)
AND
NOT LLMIME_Origin_MaskExists(_Mime)
AND
GetPosition(_Mime, _x, _y, _z)
AND
CreateItemTemplateAtPosition("EQ_Armor_LLMIME_Mime_Mask_A_8e66ce79-8c8e-4c22-a8ea-5a99977f4ea8", _x, _y, _z, _Mask)
THEN
LLMIME_Origin_Debug_DeleteNonExistentMask(_Mime);
CharacterEquipItem(_Mime, _Mask);
ItemLockUnEquip(_Mask, 1);
DB_LLMIME_Origin_MaskEquipped(_Mime, _Mask);
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] Found mask had issues. Created a new one at the mime's position and equipped it.");

PROC
LLMIME_Origin_EquipMimeMask((CHARACTERGUID)_Mime)
AND
NOT DB_LLMIME_Origin_MaskEquipped(_Mime, _)
THEN
LeaderLog_Log("DEBUG", "[Mimicry:Origin:EquipMimeMask] [ERROR] Mask was not equipped!");

IF
ItemUnEquipFailed(_Mask, S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f)
AND
IsTagged(_Mask, "LLMIME_MimeMask", 1)
THEN
Proc_StartDialog(1, "LLMIME_AD_MaskWontComeOff", S_Player_LLMIME_Mime_191f180c-6270-4cd1-a44a-e02454bd846f);

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LLMIME_20_Init"