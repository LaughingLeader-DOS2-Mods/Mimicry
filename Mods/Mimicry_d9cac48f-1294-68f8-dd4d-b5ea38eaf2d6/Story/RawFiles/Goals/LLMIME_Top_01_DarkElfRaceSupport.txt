Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_LLMIME_DarkElf_ShadowArmor("Boots", "EQ_UNIQUE_Armor_LLMIME_Mime_ShadowArmor_Boots_A_dc5f5ec9-b402-440a-9791-1ed10928fea4");
DB_LLMIME_DarkElf_ShadowArmor("Gloves", "EQ_UNIQUE_Armor_LLMIME_Mime_ShadowArmor_Gloves_A_bb35910a-a8e2-4a6f-aa38-955b0803a102");
DB_LLMIME_DarkElf_ShadowArmor("LowerBody", "EQ_UNIQUE_Armor_LLMIME_Mime_ShadowArmor_LowerBody_A_177d1408-b39d-4940-9b7b-68f5bfa5f38b");
DB_LLMIME_DarkElf_ShadowArmor("UpperBody", "EQ_UNIQUE_Armor_LLMIME_Mime_ShadowArmor_UpperBody_A_ab189632-0769-4201-9e0e-afaa0e71172c");
KBSECTION
//REGION CHARACTER_CREATION_RACEDATA_REVERT
// [BEGIN_NO_OSITOOLS]
PROC
LLMIME_DarkElf_RespecMirror_Setup((CHARACTERGUID)_Player)
THEN
DB_NOOP(1);

PROC
LLMIME_DarkElf_RespecMirror_Done((CHARACTERGUID)_Player)
THEN
DB_NOOP(1);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLMIME_DarkElf_RespecMirror_Setup((CHARACTERGUID)_Player)
AND
IsTagged(_Player, "LLMIME_DARKELF", 1)
AND
NOT CharacterGetRace(_Player, 0, "LLMIME_DarkElf")
THEN
NRD_PlayerSetCustomDataString(_Player, "Race", "LLMIME_DarkElf");

PROC
LLMIME_DarkElf_RespecMirror_Done((CHARACTERGUID)_Player)
AND
IsTagged(_Player, "LLMIME_DARKELF", 1)
AND
CharacterGetRace(_Player, 0, "LLMIME_DarkElf")
THEN
NRD_PlayerSetCustomDataString(_Player, "Race", "Elf");
*/
//END_REGION

IF
DB_Illusionist(_Player,_Item)
THEN
LLMIME_DarkElf_RespecMirror_Setup(_Player);

//Failed
PROC
PROC_HandleMagicMirrorResult((CHARACTERGUID)_Player,0)
THEN
LLMIME_DarkElf_RespecMirror_Done(_Player);

//Finished respec
PROC
Proc_HomesteadTeleportAfterMirror((CHARACTERGUID)_Player,(ITEMGUID)_Mirror,(TRIGGERGUID)_Trigger)
THEN
LLMIME_DarkElf_RespecMirror_Done(_Player);

QRY
QRY_IsTrueRace((CHARACTERGUID)_Character, "Elf")
AND
IsTagged(_Character, "LLMIME_DARKELF", 1)
THEN
DB_NOOP(1);

//REGION SHADOWFORM
IF
CharacterStatusRemoved(_Char, "LLMIME_SHADOWFORM", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLMIME_TS_ShadowFormActive", "Mimicry", 500);

IF
CharacterStatusApplied(_Char, "LLMIME_SHADOWFORM", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLMIME_TS_ShadowFormActive", "Mimicry");
//END_REGION

//REGION PRESET_ARMOR
PROC
LLMIME_DarkElf_RedeemShadowArmor((CHARACTERGUID)_Char)
AND
DB_LLMIME_DarkElf_ShadowArmor(_Slot, _Template)
THEN
LeaderLib_Helper_AddAndEquipItemTemplate(_Char, _Template, 0);

IF
ItemTemplateAddedToCharacter(ITEMGUID_LOOT_LLMIME_Redeem_ShadowArmor_867bb3cd-77ed-4666-a073-2242e1c302f0, _Token, _Char)
AND
DB_CurrentLevel(_Level)
AND
IsGameLevel(_Level, 1)
THEN
LLMIME_DarkElf_RedeemShadowArmor(_Char);
ItemRemove(_Token);

IF
ItemTemplateAddedToCharacter(ITEMGUID_LOOT_LLMIME_Redeem_ShadowArmor_867bb3cd-77ed-4666-a073-2242e1c302f0, _Token, _Char)
AND
DB_CurrentLevel(_Level)
AND
IsGameLevel(_Level, 0)
THEN
DB_LLMIME_DarkElf_Temp_RedeemShadowArmor(_Char);
ItemRemove(_Token);
SysActivateGoal("LLMIME_Top_TS_RedeemItems");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "__AAA_Z_LaughingLeader_LeaderLib_Top"